/-
Copyright (c) 2021 David W√§rn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David W√§rn, Scott Morrison
-/
import data.opposite

/-!
# Quivers

This module defines quivers. A quiver on a type `V` of vertices assigns to every
pair `a b : V` of vertices a type `a ‚ü∂ b` of arrows from `a` to `b`. This
is a very permissive notion of directed graph.

## Implementation notes

Currently `quiver` is defined with `arrow : V ‚Üí V ‚Üí Sort v`.
This is different from the category theory setup,
where we insist that morphisms live in some `Type`.
There's some balance here: it's nice to allow `Prop` to ensure there are no multiple arrows,
but it is also results in error-prone universe signatures when constraints require a `Type`.
-/

open opposite

-- We use the same universe order as in category theory.
-- See note [category_theory universes]
universes v v‚ÇÅ v‚ÇÇ u u‚ÇÅ u‚ÇÇ

/--
A quiver `G` on a type `V` of vertices assigns to every pair `a b : V` of vertices
a type `a ‚ü∂ b` of arrows from `a` to `b`.

For graphs with no repeated edges, one can use `quiver.{0} V`, which ensures
`a ‚ü∂ b : Prop`. For multigraphs, one can use `quiver.{v+1} V`, which ensures
`a ‚ü∂ b : Type v`.

Because `category` will later extend this class, we call the field `hom`.
Except when constructing instances, you should rarely see this, and use the `‚ü∂` notation instead.
-/
class quiver (V : Type u) :=
(hom : V ‚Üí V ‚Üí Sort v)

infixr ` ‚ü∂ `:10 := quiver.hom -- type as \h

/--
A morphism of quivers. As we will later have categorical functors extend this structure,
we call it a `prefunctor`.
-/
structure prefunctor (V : Type u‚ÇÅ) [quiver.{v‚ÇÅ} V] (W : Type u‚ÇÇ) [quiver.{v‚ÇÇ} W] :=
(obj [] : V ‚Üí W)
(map : Œ† {X Y : V}, (X ‚ü∂ Y) ‚Üí (obj X ‚ü∂ obj Y))

namespace prefunctor

@[ext]
lemma ext {V : Type u} [quiver.{v‚ÇÅ} V] {W : Type u‚ÇÇ} [quiver.{v‚ÇÇ} W]
  {F G : prefunctor V W}
  (h_obj : ‚àÄ X, F.obj X = G.obj X)
  (h_map : ‚àÄ (X Y : V) (f : X ‚ü∂ Y),
           F.map f = eq.rec_on (h_obj Y).symm (eq.rec_on (h_obj X).symm (G.map f))) : F = G :=
begin
  cases F with F_obj _, cases G with G_obj _,
  obtain rfl : F_obj = G_obj, by { ext X, apply h_obj },
  congr,
  funext X Y f,
  simpa using h_map X Y f,
end

/--
The identity morphism between quivers.
-/
@[simps]
def id (V : Type*) [quiver V] : prefunctor V V :=
{ obj := id,
  map := Œª X Y f, f, }

instance (V : Type*) [quiver V] : inhabited (prefunctor V V) := ‚ü®id V‚ü©

/--
Composition of morphisms between quivers.
-/
@[simps]
def comp {U : Type*} [quiver U] {V : Type*} [quiver V] {W : Type*} [quiver W]
  (F : prefunctor U V) (G : prefunctor V W) : prefunctor U W :=
{ obj := Œª X, G.obj (F.obj X),
  map := Œª X Y f, G.map (F.map f), }

@[simp] lemma comp_id {U : Type*} [quiver U] {V : Type*} [quiver V] (F : prefunctor U V) :
  F.comp (id _) = F := by { cases F, refl, }

@[simp] lemma id_comp {U : Type*} [quiver U] {V : Type*} [quiver V] (F : prefunctor U V) :
  (id _).comp F = F := by { cases F, refl, }

@[simp]
lemma comp_assoc
  {U V W Z : Type*} [quiver U] [quiver V] [quiver W] [quiver Z]
  (F : prefunctor U V) (G : prefunctor V W) (H : prefunctor W Z) :
  (F.comp G).comp H = F.comp (G.comp H) := rfl

infix ` ‚•§q `:50 := prefunctor

infix ` ‚ãôq `:50 := prefunctor.comp

notation `ùü≠q` := id

end prefunctor

namespace quiver

/-- `V·µí·µñ` reverses the direction of all arrows of `V`. -/
instance opposite {V} [quiver V] : quiver V·µí·µñ :=
‚ü®Œª a b, (unop b) ‚ü∂ (unop a)‚ü©

/--
The opposite of an arrow in `V`.
-/
def hom.op {V} [quiver V] {X Y : V} (f : X ‚ü∂ Y) : op Y ‚ü∂ op X := f
/--
Given an arrow in `V·µí·µñ`, we can take the "unopposite" back in `V`.
-/
def hom.unop {V} [quiver V] {X Y : V·µí·µñ} (f : X ‚ü∂ Y) : unop Y ‚ü∂ unop X := f

attribute [irreducible] quiver.opposite

/-- A type synonym for a quiver with no arrows. -/
@[nolint has_nonempty_instance]
def empty (V) : Type u := V

instance empty_quiver (V : Type u) : quiver.{u} (empty V) := ‚ü®Œª a b, pempty‚ü©

@[simp] lemma empty_arrow {V : Type u} (a b : empty V) : (a ‚ü∂ b) = pempty := rfl

/-- A quiver is thin if it has no parallel arrows. -/
@[reducible] def is_thin (V : Type u) [quiver V] := ‚àÄ (a b : V), subsingleton (a ‚ü∂ b)

section push
/-!
### Pushing the quiver structure on `V` along a map `V ‚Üí W`
-/

/-- 
A type synonym for the (underlying set of objects of a) quiver 
obtained by pushing arrows of `V` along the map `œÉ : V ‚Üí W` 
-/
@[nolint unused_arguments]
def push {V : Type u} [quiver V] {W : Type u‚ÇÇ} (œÉ : V ‚Üí W) := W

instance {V : Type u} [quiver V] {W : Type u‚ÇÇ} (œÉ : V ‚Üí W) [h : nonempty W] : nonempty (push œÉ) := h

namespace push

variables {V : Type*} [quiver V] {W : Type*} (œÉ : V ‚Üí W)

/-- The quiver structure obtained by pushing arrows of `V` along the map `œÉ : V ‚Üí W` -/
@[nolint has_nonempty_instance]
inductive push_quiver {V : Type u} [quiver.{v} V] {W : Type u‚ÇÇ} (œÉ : V ‚Üí W) :
  W ‚Üí W ‚Üí Type (max u u‚ÇÇ v)
| arrow {X Y : V} (f : X ‚ü∂ Y) : push_quiver (œÉ X) (œÉ Y)

/-- The `quiver` instance obtained by pushing arrows of `V` along the map `œÉ : V ‚Üí W` -/
instance : quiver (push œÉ) := ‚ü®Œª X Y, push_quiver œÉ X Y‚ü©

/-- The prefunctor induced by pushing arrows via `œÉ` -/
def of : prefunctor V (push œÉ) :=
{ obj := œÉ,
  map := Œª X Y f, push_quiver.arrow f}

@[simp] lemma of_obj : ((of œÉ)).obj = œÉ := rfl

variables {W' : Type*} [quiver W'] (œÜ : prefunctor V W') (œÑ : W ‚Üí W') (h : ‚àÄ x, œÜ.obj x = œÑ (œÉ x) )

include œÜ h
/-- Any map `œÜ : V ‚Üí W'` factoring through `œÑ : W ‚Üí W'` lifts to a prefunctor from `V` to `W`. -/
def lift : prefunctor (push œÉ) W' :=
{ obj := œÑ,
  map := @push_quiver.rec V _ W œÉ
    (Œª X Y f, œÑ X ‚ü∂ œÑ Y)
    (Œª X Y f, by {rw [‚Üêh X,‚Üêh Y], exact œÜ.map f}) }

lemma lift_spec_obj : (lift œÉ œÜ œÑ h).obj = œÑ := rfl

lemma lift_spec_comm : of œÉ ‚ãôq lift œÉ œÜ œÑ h = œÜ :=
begin
  fapply prefunctor.ext,
  { rintros, simp only [prefunctor.comp_obj], symmetry, exact h X, },
  { rintros _ _ f, simp only [prefunctor.comp_map],
    apply eq_of_heq,
    iterate 2 { apply (cast_heq _ _).trans },
    symmetry,
    iterate 2 { apply (eq_rec_heq _ _).trans },
    refl, },
end

lemma lift_unique (Œ¶ : (push œÉ) ‚•§q W') (Œ¶‚ÇÄ : Œ¶.obj = œÑ) (Œ¶comm : of œÉ ‚ãôq Œ¶ = œÜ) :
  Œ¶ = lift œÉ œÜ œÑ h :=
begin
  dsimp only [of,lift],
  fapply prefunctor.ext,
  { rintros, simp_rw [‚ÜêŒ¶‚ÇÄ], },
  { rintros _ _ ‚ü®‚ü©, subst_vars, simp only [prefunctor.comp_map, cast_eq], refl, }
end

end push

end push

section cast
/-!
### Rewriting arrows along equalities of vertices
-/

variables {U : Type*} [quiver.{u+1} U]

/-- Change the endpoints of an arrow using equalities. -/
def hom.cast {u v u' v' : U} (hu : u = u') (hv : v = v') (e : u ‚ü∂ v) : u' ‚ü∂ v' :=
eq.rec (eq.rec e hv) hu

lemma hom.cast_eq_cast {u v u' v' : U} (hu : u = u') (hv : v = v') (e : u ‚ü∂ v) :
  e.cast hu hv = cast (by rw [hu, hv]) e :=
eq.drec (eq.drec (eq.refl (hom.cast (eq.refl u) (eq.refl v) e)) hu) hv

@[simp] lemma hom.cast_rfl_rfl {u v : U} (e : u ‚ü∂ v) :
  e.cast rfl rfl = e := rfl

@[simp] lemma hom.cast_cast {u v u' v' u'' v'' : U} (e : u ‚ü∂ v)
  (hu : u = u') (hv : v = v') (hu' : u' = u'') (hv' : v' = v'') :
  (e.cast hu hv).cast hu' hv' = e.cast (hu.trans hu') (hv.trans hv') :=
by { subst_vars, refl }

lemma hom.cast_heq {u v u' v' : U} (hu : u = u') (hv : v = v') (e : u ‚ü∂ v) :
  e.cast hu hv == e :=
by { rw hom.cast_eq_cast, exact cast_heq _ _ }

lemma hom.cast_eq_iff_heq {u v u' v' : U} (hu : u = u') (hv : v = v')
  (e : u ‚ü∂ v) (e' : u' ‚ü∂ v') : e.cast hu hv = e' ‚Üî e == e' :=
by { rw hom.cast_eq_cast, exact cast_eq_iff_heq }

lemma hom.eq_cast_iff_heq {u v u' v' : U} (hu : u = u') (hv : v = v')
  (e : u ‚ü∂ v) (e' : u' ‚ü∂ v') : e' = e.cast hu hv ‚Üî e' == e :=
‚ü®Œª h, ((e.cast_eq_iff_heq hu hv e').1 h.symm).symm,
 Œª h, ((e.cast_eq_iff_heq hu hv e').2 h.symm).symm‚ü©

end cast

end quiver
