/-
Copyright (c) 2022 Felix Weilacher. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Felix Weilacher
-/
import topology.separation
import topology.bases

/-!
# Perfect Sets

In this file we define perfect subsets of a topological space, and prove some basic properties,
including a version of the Cantor-Bendixson Theorem.

## Main Definitions

* `perfect C`: A set `C` is perfect, meaning it is closed and every point of it
  is an accumulation point of itself.

## Main Statements

* `perf_nonempty.splitting`: A perfect nonempty set contains two disjoint perfect nonempty subsets.
  The main inductive step in the construction of an embedding from the Cantor space to a
  perfect nonempty complete metric space.
* `ctble_union_perfect_of_closed`: One version of the Cantor-Bendixson Theorem: A closed
  set in a second countable space can be written as the union of a countable set and a perfect set.

## Implementation Notes

We do not require perfect sets to be nonempty, a condition which is often part of
the definition of perfect. We include an extra definition `perf_nonempty`, which bundles
these two conditions.

We define a nonstandard predicate, `preperfect`, which drops the closed-ness requirement
from the definition of perfect. In T1 spaces, this is equivalent to having a perfect closure,
see `preperfect_iff_perfect_closure`.

## References

* [kechris1995] (Chapter 6)

## Tags

accumulation point, perfect set, Cantor-Bendixson.

-/

section

open_locale topological_space filter
open topological_space filter set

variables {Œ± : Type*} [topological_space Œ±]

/-- If `x` is an accumulation point of a set `C` and `U` is a neighborhood of `x`,
then `x` is an accumulation point of `U ‚à© C`. -/
theorem acc_pt.nhd_inter {x : Œ±} {C U: set Œ±} (h_acc : acc_pt x (ùìü C)) (hU : U ‚àà ùìù x) :
  acc_pt x (ùìü (U ‚à© C)) :=
begin
  have : ùìù[‚â†] x ‚â§ ùìü U,
  { rw le_principal_iff,
    exact mem_nhds_within_of_mem_nhds hU, },
  rw [acc_pt, ‚Üê inf_principal, ‚Üê inf_assoc, inf_of_le_left this],
  exact h_acc,
end

/-- A set `C` is preperfect if all of its points are accumulation points of itself.
If `C` is nonempty, this is equivalent to the closure of `C` being perfect.
See `preperfect_iff_closure_perfect`.-/
--Note : This is my own term, feel free to suggest a better one :P
def preperfect (C : set Œ±) : Prop := ‚àÄ x ‚àà C, acc_pt x (ùìü C)

/-- A set `C` is called perfect if it is closed and all of its
points are accumulation points of itself.
Note that we do not require `C` to be nonempty as is common,
but see `perf_nonempty`.-/
structure perfect (C : set Œ±) : Prop :=
  (closed : is_closed C)
  (acc : preperfect C)

/-- A set is called nonempty perfect if it is closed, nonempty, and
all of its points are accumulation points of itself.-/
structure perf_nonempty (C : set Œ±) extends perfect C : Prop :=
  (nonempty : C.nonempty)

lemma preperfect_iff_nhds {C : set Œ±} : preperfect C ‚Üî ‚àÄ x ‚àà C, ‚àÄ U ‚àà ùìù x, ‚àÉ y ‚àà U ‚à© C, y ‚â† x
  := by simp only[preperfect, acc_pt_iff_nhds]

/-- The intersection of a preperfect set and an open set is preperfect-/
theorem preperfect.open_inter {C U : set Œ±} (hC : preperfect C) (hU : is_open U) :
  preperfect (U ‚à© C) :=
begin
  rintros x ‚ü®xU, xC‚ü©,
  apply (hC _ xC).nhd_inter,
  exact hU.mem_nhds xU,
end

/-- The closure of a preperfect set is perfect.
For a converse, see `preperfect_iff_perfect_closure`-/
theorem preperfect.perfect_closure {C : set Œ±} (hC : preperfect C) :
  perfect (closure C) :=
begin
  split, {apply is_closed_closure},
  intros x hx,
  by_cases h : x ‚àà C; apply acc_pt.mono _ (principal_mono.mpr subset_closure),
  { exact hC _ h },
  have : {x}·∂ú ‚à© C = C := by simp[h],
  rw [acc_pt, nhds_within, inf_assoc, inf_principal, this],
  rw [closure_eq_cluster_pts] at hx,
  exact hx,
end

/-- In a T1 space, being preperfect is equivalent to having perfect closure.-/
theorem preperfect_iff_perfect_closure [t1_space Œ±] {C : set Œ±} :
  preperfect C ‚Üî perfect (closure C) :=
begin
  split; intro h, {exact h.perfect_closure},
  intros x xC,
  have H := h.acc _ (subset_closure xC),
  rw acc_pt_iff_frequently at *,
  have : ‚àÄ y , y ‚â† x ‚àß y ‚àà closure C ‚Üí ‚àÉ·∂† z in ùìù y, z ‚â† x ‚àß z ‚àà C,
  { rintros y ‚ü®hyx, yC‚ü©,
    simp only [‚Üê mem_compl_singleton_iff, @and_comm _ (_ ‚àà C) , ‚Üê frequently_nhds_within_iff,
      hyx.nhds_within_compl_singleton, ‚Üê mem_closure_iff_frequently],
    exact yC, },
  rw ‚Üê frequently_frequently_nhds,
  exact H.mono this,
end

theorem perfect.closure_nhd_inter {C U: set Œ±} (hC : perfect C) (x : Œ±) (xC : x ‚àà C) (xU : x ‚àà U)
  (Uop : is_open U) : perf_nonempty (closure (U ‚à© C)) :=
begin
  split,
  { apply preperfect.perfect_closure,
    exact (hC.acc).open_inter Uop, },
  apply nonempty.mono subset_closure,
  exact ‚ü®x,‚ü®xU,xC‚ü©‚ü©,
end

/-- Given a perfect nonempty set in a T2.5 space, we can find two disjoint perfect subsets
This is the main inductive step in the proof of the Cantor-Bendixson Theorem-/
lemma perf_nonempty.splitting [t2_5_space Œ±] {C : set Œ±} (hC : perf_nonempty C) :
  ‚àÉ C‚ÇÄ C‚ÇÅ : set Œ±, (perf_nonempty C‚ÇÄ ‚àß C‚ÇÄ ‚äÜ C) ‚àß (perf_nonempty C‚ÇÅ ‚àß C‚ÇÅ ‚äÜ C) ‚àß disjoint C‚ÇÄ C‚ÇÅ :=
begin
  cases hC.nonempty with y yC,
  obtain ‚ü®x, xC, hxy‚ü© : ‚àÉ x ‚àà C, x ‚â† y,
  { have := hC.acc _ yC,
    rw acc_pt_iff_nhds at this,
    rcases this univ (univ_mem) with ‚ü®x,xC,hxy‚ü©,
    exact ‚ü®x,xC.2,hxy‚ü©, },
  obtain ‚ü®U, xU, Uop, V, yV, Vop, hUV‚ü© := exists_open_nhds_disjoint_closure hxy,
  use [closure (U ‚à© C), closure (V ‚à© C)],
  split,
  { split, { apply hC.closure_nhd_inter x xC xU Uop, },
    rw hC.closed.closure_subset_iff,
    apply inter_subset_right, },
  split,
  { split, { apply hC.closure_nhd_inter y yC yV Vop, },
    rw hC.closed.closure_subset_iff,
    apply inter_subset_right, },
  apply disjoint.mono _ _ hUV; apply closure_mono; apply inter_subset_left,
end

section kernel

/-- The Cantor-Bendixson Theorem: Any closed subset of a second countable space
can be written as the union of a countable set and a perfect set.-/
theorem ctble_union_perfect_of_closed [second_countable_topology Œ±] {C : set Œ±}
  (hclosed : is_closed C) : ‚àÉ V D : set Œ±, (V.countable) ‚àß (perfect D) ‚àß (C = V ‚à™ D) :=
begin
  have := topological_space.exists_countable_basis Œ±,
  rcases this with ‚ü®b,bct,bnontrivial,bbasis‚ü©,
  let v := {U ‚àà b | (U ‚à© C).countable},
  let V := ‚ãÉ U ‚àà v, U,
  let D := C \ V,
  have Vct : (V ‚à© C).countable,
  { simp[V,Union_inter],
    apply set.countable.bUnion,
    { apply @set.countable.mono _ _ b,
      { apply set.inter_subset_left, },
      exact bct, },
    apply set.inter_subset_right, },
  use [V ‚à© C,D],
  refine ‚ü®Vct, ‚ü®_, _‚ü©, _‚ü©,
  { apply hclosed.sdiff,
    apply is_open_bUnion,
    rintros U ‚ü®Ub,-‚ü©,
    exact is_topological_basis.is_open bbasis Ub, },
  { rw preperfect_iff_nhds,
    intros x xD E xE,
    have : ¬¨ (E ‚à© D).countable,
    { intro h,
      obtain ‚ü®U,hUb,xU,hU‚ü© : ‚àÉ U ‚àà b, x ‚àà U ‚àß U ‚äÜ E,
      { exact (is_topological_basis.mem_nhds_iff bbasis).mp xE, },
      have : U ‚àà v,
      { use hUb,
        dsimp,
        apply @countable.mono _ _ ((E ‚à© D) ‚à™ (V ‚à© C)),
        { rintros y ‚ü®yU,yC‚ü©,
          by_cases y ‚àà V,
          { right,
            exact ‚ü®h,yC‚ü©, },
          left,
          split,
          { exact hU yU, },
          exact ‚ü®yC, h‚ü©, },
        apply countable.union h Vct, },
      apply xD.2,
      exact mem_bUnion this xU, },
    by_contradiction,
    push_neg at h,
    apply this,
    have : E ‚à© D ‚äÜ {x}, {exact h},
    apply countable.mono this,
    apply set.countable_singleton, },
  dsimp[D],
  rw[inter_comm,inter_union_diff],
end

/-- Any uncountable closed set in a second countable space contains a nonempty perfect subset.-/
theorem perf_nonempty_of_closed_unctble [second_countable_topology Œ±] {C : set Œ±}
  (hclosed : is_closed C) (hunc : ¬¨ C.countable) : ‚àÉ D : set Œ±, (perf_nonempty D) ‚àß (D ‚äÜ C) :=
begin
  rcases ctble_union_perfect_of_closed hclosed with ‚ü®V,D,Vct,Dperf,VD‚ü©,
  use D,
  split,
  { split, swap,
    { rw ‚Üê ne_empty_iff_nonempty,
      by_contradiction,
      rw [h, union_empty] at VD,
      rw VD at hunc,
      contradiction, },
    exact Dperf, },
  rw VD,
  apply subset_union_right,
end

end kernel

end
