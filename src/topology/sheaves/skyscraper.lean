/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang
-/
import algebraic_geometry.sheafed_space
import topology.sheaves.sheaf_condition.opens_le_cover
import topology.sheaves.stalks
import category_theory.preadditive.injective

/-!
# Skyscraper (pre)sheaves

A skyscraper (pre)sheaf is a (pre)sheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper (pre)sheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.

## Main definitions

* `skyscraper_presheaf`: `skyscraper_presheaf p‚ÇÄ A` is the skyscraper presheaf at point `p‚ÇÄ` with
  value `A`.
* `skyscraper_sheaf`: the skyscraper presheaf satisfies the sheaf condition.

## Main statements

* `skyscraper_presheaf_stalk_of_specializes`: if `y ‚àà closure {p‚ÇÄ}` then the stalk of
  `skyscraper_presheaf p‚ÇÄ S` at `y` is `S`.
* `skyscraper_presheaf_stalk_of_not_specializes`: if `y ‚àâ closure {p‚ÇÄ}` then the stalk of
  `skyscraper_presheaf p‚ÇÄ S` at `y` is `*` the terminal object.

-/

noncomputable theory

section

open topological_space
open category_theory category_theory.limits
open Top
open opposite

universes u v w

variables {X : Top.{u}} (p‚ÇÄ : X) {C : Type v} [category.{w} C] (S : C)
variables [has_terminal C] [Œ† (U : opens X), decidable (p‚ÇÄ ‚àà U)]

/--
A skyscraper presheaf is a presheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper presheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.
-/
@[simps] def skyscraper_presheaf : presheaf C X :=
{ obj := Œª U, if p‚ÇÄ ‚àà unop U then S else terminal C,
  map := Œª U V i, if h : p‚ÇÄ ‚àà unop V
    then eq_to_hom $ by erw [if_pos h, if_pos (le_of_hom i.unop h)]
    else (terminal_is_terminal.if_neg h).from _,
  map_id' := Œª U,
  begin
    split_ifs,
    { apply eq_to_hom_refl },
    { exact (terminal_is_terminal.if_neg h).hom_ext _ _ },
  end,
  map_comp' := Œª U V W iVU iWV,
  begin
    by_cases hW : p‚ÇÄ ‚àà unop W,
    { have hV : p‚ÇÄ ‚àà unop V := le_of_hom iWV.unop hW,
      simp only [dif_pos hW, dif_pos hV, eq_to_hom_trans] },
    { rw [dif_neg hW], apply (terminal_is_terminal.if_neg hW).hom_ext }
  end }

section

variables {p‚ÇÄ}

lemma skyscraper_presheaf_obj_of_mem {U : opens X} (h : p‚ÇÄ ‚àà U) :
  (skyscraper_presheaf p‚ÇÄ S).obj (op U) = S := if_pos h

lemma skyscraper_presheaf_obj_of_not_mem {U : opens X} (h : p‚ÇÄ ‚àâ U) :
  (skyscraper_presheaf p‚ÇÄ S).obj (op U) = terminal C := if_neg h

end

/--
A skyscraper sheaf is a sheaf supported at a single point: if `p‚ÇÄ ‚àà X` is a specified
point, then the skyscraper sheaf `ùìï` with value `A` is defined by `U ‚Ü¶ A` if `p‚ÇÄ ‚àà U` and
`U ‚Ü¶ *` if `p‚ÇÄ ‚àâ A` where `*` is some terminal object.
-/
def skyscraper_sheaf : sheaf C X :=
‚ü®_, (skyscraper_presheaf p‚ÇÄ S).is_sheaf_iff_is_sheaf_opens_le_cover.mpr $ Œª Œπ U, nonempty.intro
 { lift := Œª c, if h : p‚ÇÄ ‚àà (presheaf.sheaf_condition.opens_le_cover_cocone U).X
    then c.œÄ.app (op ‚ü®_, ‚ü®(opens.mem_supr.mp h).some, le_refl _‚ü©‚ü©) ‚â´ eq_to_hom
      begin
       dsimp, rw [if_pos h, if_pos (opens.mem_supr.mp h).some_spec],
      end
    else (terminal_is_terminal.if_neg h).from _,
   fac' := Œª c j,
   begin
    dsimp, split_ifs with h0, swap,
    { exact (terminal_is_terminal.if_neg h0).hom_ext _ _, },
    by_cases h1 : p‚ÇÄ ‚àà (presheaf.sheaf_condition.opens_le_cover_cocone U).X;
    split_ifs, swap,
    { rw [eq_comp_eq_to_hom], exact (terminal_is_terminal.if_neg h1).hom_ext _ _, },
    rw [category.assoc, eq_to_hom_trans],
    transitivity c.œÄ.app (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©) ‚â´
      eq_to_hom _,
    rotate, { dsimp, rw [if_pos h0, if_pos], exact ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü© },
    { have := @nat_trans.naturality _ _ _ _ _ _ c.œÄ j
        (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©)
        (quiver.hom.op (hom_of_le _) : op j.unop ‚ü∂ _), swap, exact inf_le_right,
      dsimp at this, simp only [category.id_comp] at this,
      have h : p‚ÇÄ ‚àà U _ ‚äì j.unop.obj := ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü©,
      split_ifs at this with h', swap, { exact false.elim (h' h), },
      rw [this, category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id] },
    { have := @nat_trans.naturality _ _ _ _ _ _ c.œÄ
        (op ‚ü®_, ‚ü®(opens.mem_supr.mp h1).some, le_refl _‚ü©‚ü©)
        (op ‚ü®U (opens.mem_supr.mp h1).some ‚äì j.unop.obj, ‚ü®_, inf_le_left‚ü©‚ü©)
        (quiver.hom.op (hom_of_le inf_le_left)),
      dsimp at this, simp only [category.id_comp] at this,
      have h : p‚ÇÄ ‚àà U _ ‚äì j.unop.obj := ‚ü®(opens.mem_supr.mp h1).some_spec, h0‚ü©,
      split_ifs at this with h', swap, { exact false.elim (h' h), },
      rw [this, category.assoc, eq_to_hom_trans], },
   end,
   uniq' := Œª s f h0,
   begin
    dsimp at *, split_ifs with h1, swap, { exact (terminal_is_terminal.if_neg h1).hom_ext _ _ },
    specialize h0 (op ‚ü®_, ‚ü®(opens.mem_supr.mp h1).some, le_refl _‚ü©‚ü©),
    split_ifs at h0 with h2, swap, { exact false.elim (h2 (opens.mem_supr.mp h1).some_spec) },
    rw [‚Üêh0, category.assoc, eq_to_hom_trans, eq_to_hom_refl, category.comp_id],
   end }‚ü©

end

section

-- In this section, we calculate the stalks for skyscraper presheaves.
-- We need to restrict universe level.

open topological_space
open category_theory category_theory.limits
open Top
open opposite

universes u v

variables {X : Top.{u}} (p‚ÇÄ : X) {C : Type v} [category.{u} C] (S : C) [has_terminal C]

private lemma mem_nhds_of_not_specializes {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) :
  ‚àÉ (U : open_nhds y), p‚ÇÄ ‚àâ U.1 :=
begin
  have := (not_iff_not.mpr specializes_iff_forall_open).mp h,
  push_neg at this,
  rcases this with ‚ü®s, o, h‚ÇÅ, h‚ÇÇ‚ü©,
  exact ‚ü®‚ü®‚ü®s, o‚ü©, h‚ÇÅ‚ü©, h‚ÇÇ‚ü©,
end

variable [Œ† (U : opens X), decidable (p‚ÇÄ ‚àà U)]
/--
The cocone at `S` for the salk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àà closure {p‚ÇÄ}`
-/
@[simps] def skyscraper_presheaf_cocone_of_specializes {y : X} (h : p‚ÇÄ ‚§≥ y) :
  cocone ((open_nhds.inclusion y).op ‚ãô skyscraper_presheaf p‚ÇÄ S) :=
{ X := S,
  Œπ := eq_to_hom $ category_theory.functor.ext
  begin
    intros U, dsimp, rw if_pos, exact h.mem_open U.unop.1.2 U.unop.2,
  end
  begin
    intros U V inc,
    dsimp, rw [category.id_comp, eq_to_hom_trans],
    have hV : p‚ÇÄ ‚àà (open_nhds.inclusion y).obj (unop V) := h.mem_open V.unop.1.2 V.unop.2,
    have hU : p‚ÇÄ ‚àà (open_nhds.inclusion y).obj (unop U):= h.mem_open U.unop.1.2 U.unop.2,
    split_ifs, refl,
  end }

/--
The canonical map `S ‚ü∂ (skyscraper_presheaf p‚ÇÄ S t).stalk y` when `y ‚àà closure {p‚ÇÄ}`
-/
noncomputable def skyscraper_presheaf_from [has_colimits C] {y : X} :
  S ‚ü∂ (skyscraper_presheaf p‚ÇÄ S).stalk y :=
eq_to_hom (skyscraper_presheaf_obj_of_mem S (by trivial : p‚ÇÄ ‚àà ‚ä§)).symm ‚â´
  (skyscraper_presheaf p‚ÇÄ S).germ (‚ü®y, trivial‚ü© : (‚ä§ : opens X))

/--
The cocone at `S` for the stalk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àà closure {p‚ÇÄ}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_specializes [has_colimits C]
  {y : X} (h : p‚ÇÄ ‚§≥ y) : is_colimit (skyscraper_presheaf_cocone_of_specializes p‚ÇÄ S h) :=
{ desc := Œª c, (skyscraper_presheaf_from p‚ÇÄ S ‚â´ colimit.desc _ _ : S ‚ü∂ c.X),
  fac' := Œª c U,
  begin
    dsimp,
    simp only [skyscraper_presheaf_from, presheaf.germ, category.comp_id,
      category.assoc, colimit.Œπ_desc, eq_to_hom_trans_assoc],
    have := c.Œπ.naturality (hom_of_le $ (le_top : unop U ‚â§ _)).op,
    dsimp at this,
    have h' : p‚ÇÄ ‚àà (open_nhds.inclusion y).obj (unop U) := h.mem_open U.unop.1.2 U.unop.2,
    have h'' : p‚ÇÄ ‚àà (open_nhds.inclusion y).obj ‚ä§ := trivial,
    split_ifs at this,
    rw [category.comp_id, eq_eq_to_hom_comp] at this,
    rw [eq_to_hom_app, ‚Üêcategory.assoc, eq_to_hom_trans, this],
    congr,
  end,
  uniq' := Œª c f h,
  begin
    simp only [skyscraper_presheaf_from, presheaf.germ, category.assoc],
    erw [colimit.Œπ_desc],
    specialize h (op ‚ü®‚ä§, trivial‚ü©),
    erw [‚Üêh],
    simp only [skyscraper_presheaf_cocone_of_specializes_Œπ, eq_to_hom_app,
      eq_to_hom_trans_assoc, eq_to_hom_refl, category.id_comp],
  end }

/--
If `y ‚àà closure {p‚ÇÄ}`, then the stalk of `skyscraper_presheaf p‚ÇÄ S` at `y` is `S`
-/
@[reducible]
noncomputable def skyscraper_presheaf_stalk_of_specializes [has_colimits C]
  {y : X} (h : p‚ÇÄ ‚§≥ y) : (skyscraper_presheaf p‚ÇÄ S).stalk y ‚âÖ S :=
colimit.iso_colimit_cocone ‚ü®_, skyscraper_presheaf_cocone_is_colimit_of_specializes p‚ÇÄ S h‚ü©

/--
The cocone at `*` for the salk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àâ closure {p‚ÇÄ}`
-/
@[simps] def skyscraper_presheaf_cocone {y : X} :
  cocone ((open_nhds.inclusion y).op ‚ãô skyscraper_presheaf p‚ÇÄ S) :=
{ X := terminal C,
  Œπ :=
  { app := Œª U, terminal.from _,
    naturality' := Œª U V inc, terminal_is_terminal.hom_ext _ _ } }

/--
The canonical map `* ‚ü∂ (skyscraper_presheaf p‚ÇÄ S).stalk y` when `y ‚àâ closure {p‚ÇÄ}`
-/
noncomputable def skyscraper_presheaf_of_not_specializes [has_colimits C]
  {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) : terminal C ‚ü∂ (skyscraper_presheaf p‚ÇÄ S).stalk y :=
eq_to_hom (skyscraper_presheaf_obj_of_not_mem S $
  (mem_nhds_of_not_specializes p‚ÇÄ h).some_spec).symm ‚â´
  (skyscraper_presheaf p‚ÇÄ S).germ (‚ü®y, (mem_nhds_of_not_specializes p‚ÇÄ h).some.2‚ü© :
    (mem_nhds_of_not_specializes p‚ÇÄ h).some.1)

/--
The cocone at `*` for the salk functor of `skyscraper_presheaf p‚ÇÄ S` when `y ‚àâ closure {p‚ÇÄ}` is a
colimit
-/
noncomputable def skyscraper_presheaf_cocone_is_colimit_of_not_specializes [has_colimits C]
  {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) : is_colimit (skyscraper_presheaf_cocone p‚ÇÄ S) :=
{ desc := Œª c, (eq_to_hom ((skyscraper_presheaf_obj_of_not_mem _
      (mem_nhds_of_not_specializes p‚ÇÄ h).some_spec).symm)) ‚â´
    presheaf.germ (skyscraper_presheaf p‚ÇÄ S)
      ‚ü®y, (mem_nhds_of_not_specializes p‚ÇÄ h).some.2‚ü© ‚â´ colimit.desc _ _,
  fac' := Œª c U,
  begin
    simp only [presheaf.germ, skyscraper_presheaf_cocone_Œπ_app],
    erw [colimit.Œπ_desc],
    dsimp,
    generalize_proofs h1 h2 h3,
    have eq0 : c.Œπ.app (op {obj := h1.some.1, property := h1.some.2}) =
      eq_to_hom (by { congr, ext, refl }) ‚â´ c.Œπ.app (op h1.some) ‚â´
      eq_to_hom (by { congr, ext, refl }),
    { symmetry, rw [eq_eq_to_hom_comp], symmetry,
      convert (c.Œπ.naturality) _, swap,
      { refine eq_to_hom _, congr, ext, refl, },
      { rw eq_to_hom_map, }, },
    erw [eq0, ‚Üêcategory.assoc (eq_to_hom h3), eq_to_hom_trans, ‚Üêcategory.assoc, ‚Üêcategory.assoc,
      eq_comp_eq_to_hom, eq_to_hom_refl, category.comp_id],
    transitivity _ ‚â´ c.Œπ.app (op (U.unop ‚äì h1.some)),
    work_on_goal 2
    { refine (terminal_is_terminal.if_neg _).from _,
      exact Œª h, h1.some_spec h.2, },
    work_on_goal 2
    { have := c.Œπ.naturality ((hom_of_le inf_le_left).op : op U.unop ‚ü∂ op (unop U ‚äì h1.some)),
      erw [category.comp_id] at this,
      erw ‚Üêthis,
      congr,
      refine (terminal_is_terminal.if_neg _).hom_ext _ _,
      exact Œª h, h1.some_spec h.2, },
    have := c.Œπ.naturality ((hom_of_le inf_le_right).op : op h1.some ‚ü∂ op (unop U ‚äì h1.some)),
    erw [category.comp_id] at this,
    erw [‚Üêthis, ‚Üêcategory.assoc],
    congr' 1,
    refine (terminal_is_terminal.if_neg _).hom_ext _ _,
    exact Œª h, h1.some_spec h.2,
  end,
  uniq' := Œª c f H,
  begin
    erw [colimit.Œπ_desc, ‚ÜêH],
    simp only [skyscraper_presheaf_cocone_Œπ_app],
    dsimp,
    have : terminal.from
      (ite (p‚ÇÄ ‚àà (mem_nhds_of_not_specializes p‚ÇÄ h).some.1) S (terminal C)) =
      eq_to_hom (if_neg (mem_nhds_of_not_specializes p‚ÇÄ h).some_spec) :=
    terminal_is_terminal.hom_ext _ _,
    erw [this, ‚Üêcategory.assoc, eq_to_hom_trans, eq_to_hom_refl, category.id_comp]
  end }

/--
If `y ‚àâ closure {p‚ÇÄ}`, then the stalk of `skyscraper_presheaf p‚ÇÄ S` at `y` is `*`
-/
@[reducible]
noncomputable def skyscraper_presheaf_stalk_of_not_specializes [has_colimits C]
  {y : X} (h : ¬¨p‚ÇÄ ‚§≥ y) : (skyscraper_presheaf p‚ÇÄ S).stalk y ‚âÖ terminal C :=
colimit.iso_colimit_cocone ‚ü®_, skyscraper_presheaf_cocone_is_colimit_of_not_specializes _ S h‚ü©

end
