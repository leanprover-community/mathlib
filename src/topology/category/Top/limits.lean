/-
Copyright (c) 2017 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Scott Morrison, Mario Carneiro
-/
import topology.category.Top.basic
import category_theory.limits.types
import category_theory.limits.preserves.basic

/-!
# The category of topological spaces has all limits and colimits

Further, these limits and colimits are preserved by the forgetful functor --- that is, the
underlying types are just the limits in the category of types.
-/

open topological_space
open category_theory
open category_theory.limits
open opposite

universes u v

noncomputable theory

namespace Top

variables {J : Type u} [small_category J]

local notation `forget` := forget Top

/--
A choice of limit cone for a functor `F : J ‚•§ Top`.
Generally you should just use `limit.cone F`, unless you need the actual definition
(which is in terms of `types.limit_cone`).
-/
def limit_cone (F : J ‚•§ Top.{u}) : cone F :=
{ X := Top.of {u : Œ† j : J, F.obj j | ‚àÄ {i j : J} (f : i ‚ü∂ j), F.map f (u i) = u j},
  œÄ :=
  { app := Œª j,
    { to_fun := Œª u, u.val j,
      continuous_to_fun := show continuous ((Œª u : Œ† j : J, F.obj j, u j) ‚àò subtype.val),
        by continuity } } }

/--
A choice of limit cone for a functor `F : J ‚•§ Top` whose topology is defined as an
infimum of topologies infimum.
Generally you should just use `limit.cone F`, unless you need the actual definition
(which is in terms of `types.limit_cone`).
-/
def limit_cone_infi (F : J ‚•§ Top.{u}) : cone F :=
{ X := ‚ü®(types.limit_cone (F ‚ãô forget)).X, ‚®Öj,
        (F.obj j).str.induced ((types.limit_cone (F ‚ãô forget)).œÄ.app j)‚ü©,
  œÄ :=
  { app := Œª j, ‚ü®(types.limit_cone (F ‚ãô forget)).œÄ.app j,
                 continuous_iff_le_induced.mpr (infi_le _ _)‚ü©,
    naturality' := Œª j j' f,
                   continuous_map.coe_inj ((types.limit_cone (F ‚ãô forget)).œÄ.naturality f) } }

/--
The chosen cone `Top.limit_cone F` for a functor `F : J ‚•§ Top` is a limit cone.
Generally you should just use `limit.is_limit F`, unless you need the actual definition
(which is in terms of `types.limit_cone_is_limit`).
-/
def limit_cone_is_limit (F : J ‚•§ Top.{u}) : is_limit (limit_cone F) :=
{ lift := Œª S, { to_fun := Œª x, ‚ü®Œª j, S.œÄ.app _ x, Œª i j f, by { dsimp, erw ‚Üê S.w f, refl }‚ü© },
  uniq' := Œª S m h, by { ext : 3, simpa [‚Üê h] } }

/--
The chosen cone `Top.limit_cone_infi F` for a functor `F : J ‚•§ Top` is a limit cone.
Generally you should just use `limit.is_limit F`, unless you need the actual definition
(which is in terms of `types.limit_cone_is_limit`).
-/
def limit_cone_infi_is_limit (F : J ‚•§ Top.{u}) : is_limit (limit_cone_infi F) :=
by { refine is_limit.of_faithful forget (types.limit_cone_is_limit _) (Œª s, ‚ü®_, _‚ü©) (Œª s, rfl),
     exact continuous_iff_coinduced_le.mpr (le_infi $ Œª j,
       coinduced_le_iff_le_induced.mp $ (continuous_iff_coinduced_le.mp (s.œÄ.app j).continuous :
         _) ) }

instance Top_has_limits : has_limits.{u} Top.{u} :=
{ has_limits_of_shape := Œª J ùí•, by exactI
  { has_limit := Œª F, has_limit.mk { cone := limit_cone F, is_limit := limit_cone_is_limit F } } }

instance forget_preserves_limits : preserves_limits (forget : Top.{u} ‚•§ Type u) :=
{ preserves_limits_of_shape := Œª J ùí•,
  { preserves_limit := Œª F,
    by exactI preserves_limit_of_preserves_limit_cone
      (limit_cone_is_limit F) (types.limit_cone_is_limit (F ‚ãô forget)) } }

/--
A choice of colimit cocone for a functor `F : J ‚•§ Top`.
Generally you should just use `colimit.coone F`, unless you need the actual definition
(which is in terms of `types.colimit_cocone`).
-/
def colimit_cocone (F : J ‚•§ Top.{u}) : cocone F :=
{ X := ‚ü®(types.colimit_cocone (F ‚ãô forget)).X, ‚®Ü j,
        (F.obj j).str.coinduced ((types.colimit_cocone (F ‚ãô forget)).Œπ.app j)‚ü©,
  Œπ :=
  { app := Œª j, ‚ü®(types.colimit_cocone (F ‚ãô forget)).Œπ.app j,
                 continuous_iff_coinduced_le.mpr (le_supr _ j)‚ü©,
    naturality' := Œª j j' f,
                   continuous_map.coe_inj ((types.colimit_cocone (F ‚ãô forget)).Œπ.naturality f) } }

/--
The chosen cocone `Top.colimit_cocone F` for a functor `F : J ‚•§ Top` is a colimit cocone.
Generally you should just use `colimit.is_colimit F`, unless you need the actual definition
(which is in terms of `types.colimit_cocone_is_colimit`).
-/
def colimit_cocone_is_colimit (F : J ‚•§ Top.{u}) : is_colimit (colimit_cocone F) :=
by { refine is_colimit.of_faithful forget (types.colimit_cocone_is_colimit _) (Œª s, ‚ü®_, _‚ü©)
       (Œª s, rfl),
     exact continuous_iff_le_induced.mpr (supr_le $ Œª j,
       coinduced_le_iff_le_induced.mp $ (continuous_iff_coinduced_le.mp (s.Œπ.app j).continuous :
         _) ) }

instance Top_has_colimits : has_colimits.{u} Top.{u} :=
{ has_colimits_of_shape := Œª J ùí•, by exactI
  { has_colimit := Œª F, has_colimit.mk { cocone := colimit_cocone F, is_colimit :=
    colimit_cocone_is_colimit F } } }

instance forget_preserves_colimits : preserves_colimits (forget : Top.{u} ‚•§ Type u) :=
{ preserves_colimits_of_shape := Œª J ùí•,
  { preserves_colimit := Œª F,
    by exactI preserves_colimit_of_preserves_colimit_cocone
      (colimit_cocone_is_colimit F) (types.colimit_cocone_is_colimit (F ‚ãô forget)) } }

end Top

namespace Top

section cofiltered_limit

variables {J : Type u} [small_category J] [is_cofiltered J] (F : J ‚•§ Top.{u})
  (C : cone F) (hC : is_limit C)

include hC

/--
Given a *compatible* collection of topological bases for the factors in a cofiltered limit
which contain `set.univ` and are closed under intersections, the induced *naive* collection
of sets in the limit is, in fact, a topological basis.
-/
theorem is_topological_basis_cofiltered_limit
  (T : Œ† j, set (set (F.obj j))) (hT : ‚àÄ j, is_topological_basis (T j))
  (univ : ‚àÄ (i : J), set.univ ‚àà T i)
  (inter : ‚àÄ i (U1 U2 : set (F.obj i)), U1 ‚àà T i ‚Üí U2 ‚àà T i ‚Üí U1 ‚à© U2 ‚àà T i)
  (compat : ‚àÄ (i j : J) (f : i ‚ü∂ j) (V : set (F.obj j)) (hV : V ‚àà T j), (F.map f) ‚Åª¬π' V ‚àà T i) :
  is_topological_basis { U : set C.X | ‚àÉ j (V : set (F.obj j)), V ‚àà T j ‚àß U = C.œÄ.app j ‚Åª¬π' V } :=
begin
  classical,
  -- The limit cone for `F` whose topology is defined as an infimum.
  let D := limit_cone_infi F,
  -- The isomorphism between the cone point of `C` and the cone point of `D`.
  let E : C.X ‚âÖ D.X := hC.cone_point_unique_up_to_iso (limit_cone_infi_is_limit _),
  have hE : inducing E.hom := (Top.homeo_of_iso E).inducing,
  -- Reduce to the assertion of the theorem with `D` instead of `C`.
  suffices : is_topological_basis
    { U : set D.X | ‚àÉ j (V : set (F.obj j)), V ‚àà T j ‚àß U = D.œÄ.app j ‚Åª¬π' V },
  { convert this.inducing hE,
    ext U0,
    split,
    { rintro ‚ü®j, V, hV, rfl‚ü©,
      refine ‚ü®D.œÄ.app j ‚Åª¬π' V, ‚ü®j, V, hV, rfl‚ü©, rfl‚ü© },
    { rintro ‚ü®W, ‚ü®j, V, hV, rfl‚ü©, rfl‚ü©,
      refine ‚ü®j, V, hV, rfl‚ü© } },
  -- Using `D`, we can apply the characterization of the topological basis of a
  -- topology defined as an infimum...
  convert is_topological_basis_infi hT (Œª j (x : D.X), D.œÄ.app j x),
  ext U0,
  split,
  { rintros  ‚ü®j, V, hV, rfl‚ü©,
    let U : Œ† i, set (F.obj i) := Œª i, if h : i = j then (by {rw h, exact V}) else set.univ,
    refine ‚ü®U,{j},_,_‚ü©,
    { rintro i h,
      rw finset.mem_singleton at h,
      dsimp [U],
      rw dif_pos h,
      subst h,
      exact hV },
    { dsimp [U],
      simp } },
  { rintros ‚ü®U, G, h1, h2‚ü©,
    obtain ‚ü®j, hj‚ü© := is_cofiltered.inf_objs_exists G,
    let g : ‚àÄ e (he : e ‚àà G), j ‚ü∂ e := Œª _ he, (hj he).some,
    let Vs : J ‚Üí set (F.obj j) := Œª e, if h : e ‚àà G then F.map (g e h) ‚Åª¬π' (U e) else set.univ,
    let V : set (F.obj j) := ‚ãÇ (e : J) (he : e ‚àà G), Vs e,
    refine ‚ü®j, V, _, _‚ü©,
    { -- An intermediate claim used to apply induction along `G : finset J` later on.
      have : ‚àÄ (S : set (set (F.obj j))) (E : finset J) (P : J ‚Üí set (F.obj j))
        (univ : set.univ ‚àà S)
        (inter : ‚àÄ A B : set (F.obj j), A ‚àà S ‚Üí B ‚àà S ‚Üí A ‚à© B ‚àà S)
        (cond : ‚àÄ (e : J) (he : e ‚àà E), P e ‚àà S), (‚ãÇ e (he : e ‚àà E), P e) ‚àà S,
      { intros S E,
        apply E.induction_on,
        { intros P he hh,
          simpa },
        { intros a E ha hh1 hh2 hh3 hh4 hh5,
          rw finset.set_bInter_insert,
          refine hh4 _ _ (hh5 _ (finset.mem_insert_self _ _)) (hh1 _ hh3 hh4 _),
          intros e he,
          exact hh5 e (finset.mem_insert_of_mem he) } },
      -- use the intermediate claim to finish off the goal using `univ` and `inter`.
      refine this _ _ _ (univ _) (inter _) _,
      intros e he,
      dsimp [Vs],
      rw dif_pos he,
      exact compat j e (g e he) (U e) (h1 e he), },
    { -- conclude...
      rw h2,
      dsimp [V],
      rw set.preimage_Inter,
      congr' 1,
      ext1 e,
      rw set.preimage_Inter,
      congr' 1,
      ext1 he,
      dsimp [Vs],
      rw [dif_pos he, ‚Üê set.preimage_comp],
      congr' 1,
      change _ = ‚áë(D.œÄ.app j ‚â´ F.map (g e he)),
      rw D.w } }
end

end cofiltered_limit

section topological_konig

/-!
## Topological K≈ënig's lemma

A topological version of K≈ënig's lemma is that the inverse limit of nonempty compact Hausdorff
spaces is nonempty.  (Note: this can be generalized further to inverse limits of nonempty compact
T0 spaces, where all the maps are closed maps; see [Stone1979] --- however there is an erratum
for Theorem 4 that the element in the inverse limit can have cofinally many components that are
not closed points.)

TODO: The theorem hold also in the case `{J : Type u} [category J] [is_cofiltered J]`.
See https://stacks.math.columbia.edu/tag/086J for the Set version and
See https://stacks.math.columbia.edu/tag/0032 for how to lift this to general cofiltered categories
rather than thin ones.
-/

variables {J : Type u} [directed_order J]
variables (F : J·µí·µñ ‚•§ Top.{u})

/--
The partial sections of an inverse system of topological spaces from an index `j` are sections
when restricted to all objects less than or equal to `j`.
-/
def partial_sections (j : J·µí·µñ) : set (Œ† j, F.obj j) :=
{ u | ‚àÄ {j'} (f : j ‚ü∂ j'), F.map f (u j) = u j'}

lemma partial_sections.nonempty [Œ† (j : J·µí·µñ), nonempty (F.obj j)] (j : J·µí·µñ) :
  (partial_sections F j).nonempty :=
begin
  classical,
  use Œª (j' : J·µí·µñ),
    if h : j'.unop ‚â§ j.unop then
      F.map h.hom.op (classical.arbitrary (F.obj j))
    else
      classical.arbitrary _,
  intros j' fle,
  simp only [dif_pos fle.unop.le, dif_pos le_rfl],
  dsimp, simp,
end

lemma partial_sections.directed : directed (‚äá) (partial_sections F) :=
begin
  intros j j',
  obtain ‚ü®j'', hj''‚ü© := directed_order.directed j.unop j'.unop,
  use op j'',
  split,
  { intros u hu j''' f''',
    rw [‚Üêhu ((hom_of_le hj''.1).op ‚â´ f'''), ‚Üêhu],
    simp only [Top.comp_app, functor.map_comp] },
  { intros u hu j''' f''',
    rw [‚Üêhu ((hom_of_le hj''.2).op ‚â´ f'''), ‚Üêhu],
    simp only [Top.comp_app, functor.map_comp] },
end

lemma partial_sections.closed [Œ† (j : J·µí·µñ), t2_space (F.obj j)] (j : J·µí·µñ) :
  is_closed (partial_sections F j) :=
begin
  have hps : partial_sections F j =
    ‚ãÇ (f : Œ£ j', j ‚ü∂ j'), {u : Œ† (j : J·µí·µñ), F.obj j | F.map f.2 (u j) = u f.1},
  { ext u,
    simp only [set.mem_Inter, sigma.forall, set.mem_set_of_eq],
    exact ‚ü®Œª hu j' f, hu f, Œª hu j' f, hu j' f‚ü© },
  rw hps,
  apply is_closed_Inter,
  rintros ‚ü®j', f‚ü©,
  let proj : Œ† (j' : J·µí·µñ), C((Œ† (j : J·µí·µñ), F.obj j), F.obj j') :=
    Œª j', ‚ü®Œª u, u j', continuous_apply j'‚ü©,
  exact is_closed_eq
    (((F.map f).continuous.comp (proj j).continuous).comp continuous_id)
    ((proj j').continuous.comp continuous_id),
end

lemma nonempty_limit_cone_of_compact_t2_inverse_system
  [Œ† (j : J·µí·µñ), nonempty (F.obj j)]
  [Œ† (j : J·µí·µñ), compact_space (F.obj j)]
  [Œ† (j : J·µí·µñ), t2_space (F.obj j)] :
  nonempty (Top.limit_cone F).X :=
begin
  by_cases h : nonempty J·µí·µñ,
  { haveI := h,
    obtain ‚ü®u, hu‚ü© := is_compact.nonempty_Inter_of_directed_nonempty_compact_closed
      (partial_sections F) (partial_sections.directed F) (partial_sections.nonempty F)
      (Œª j, is_closed.is_compact (partial_sections.closed F j)) (partial_sections.closed F),
    use u,
    intros j j' f,
    specialize hu (partial_sections F j),
    simp only [forall_prop_of_true, set.mem_range_self] at hu,
    exact hu f, },
  { exact ‚ü®‚ü®Œª j, (h ‚ü®j‚ü©).elim, Œª j, (h ‚ü®j‚ü©).elim‚ü©‚ü©, },
end

end topological_konig

end Top

section fintype_konig

/-- This bootstraps `nonempty_sections_of_fintype_inverse_system`. In this version,
the `F` functor is between categories of the same universe, and it is an easy
corollary to `Top.nonempty_limit_cone_of_compact_t2_inverse_system`. -/
lemma nonempty_sections_of_fintype_inverse_system.init
  {J : Type u} [directed_order J] (F : J·µí·µñ ‚•§ Type u)
  [hf : Œ† (j : J·µí·µñ), fintype (F.obj j)] [hne : Œ† (j : J·µí·µñ), nonempty (F.obj j)] :
  F.sections.nonempty :=
begin
  let F' : J·µí·µñ ‚•§ Top := F ‚ãô Top.discrete,
  haveI : Œ† (j : J·µí·µñ), fintype (F'.obj j) := hf,
  haveI : Œ† (j : J·µí·µñ), nonempty (F'.obj j) := hne,
  obtain ‚ü®‚ü®u, hu‚ü©‚ü© := Top.nonempty_limit_cone_of_compact_t2_inverse_system F',
  exact ‚ü®u, Œª _ _ f, hu f‚ü©,
end

/-- Gives the induced directed order on the `ulift` of a type with a directed order.
This is not an instance because `preorder.small_category` will conflict with
`category_theory.ulift_category`. -/
def ulift.directed_order (Œ± : Type u) [directed_order Œ±] : directed_order (ulift.{v} Œ±) :=
{ le := Œª i j, i.down ‚â§ j.down,
  le_refl := Œª i, le_refl i.down,
  le_trans := Œª i j k hij hjk, le_trans hij hjk,
  directed := Œª i j, begin
    obtain ‚ü®k, hk‚ü© := directed_order.directed i.down j.down,
    exact ‚ü®ulift.up k, hk‚ü©,
  end }

/-- The inverse limit of nonempty finite types is nonempty.

This may be regarded as a generalization of K≈ënig's lemma.
To specialize: given a locally finite connected graph, take `J` to be `‚Ñï` and
`F j` to be length-`j` paths that start from an arbitrary fixed vertex.
Elements of `F.sections` can be read off as infinite rays in the graph. -/
theorem nonempty_sections_of_fintype_inverse_system
  {J : Type u} [directed_order J] (F : J·µí·µñ ‚•§ Type v)
  [Œ† (j : J·µí·µñ), fintype (F.obj j)] [Œ† (j : J·µí·µñ), nonempty (F.obj j)] :
  F.sections.nonempty :=
begin
  -- Step 1: lift everything to the `max u v` universe.
  let J' := ulift.{v} J,
  letI hd : directed_order J' := ulift.directed_order J,
  -- We want `J'` to have the category structure from its inherited directed order,
  -- rather than the `category_theory.ulift_category` structure.
  letI : small_category J' := @preorder.small_category _ hd.to_preorder,
  -- The equivalence in `category.ulift` does not apply to the `directed_order`, so we
  -- quickly implement one of its functors here.
  let down : J' ‚•§ J :=
  { obj := ulift.down,
    map := Œª i j f, hom_of_le (le_of_hom f : i ‚â§ j) },
  let tu : Type v ‚•§ Type (max u v) := ulift_functor.{u v},
  let F' : (ulift.{v} J)·µí·µñ ‚•§ Type (max u v) := down.op ‚ãô F ‚ãô tu,
  haveI : ‚àÄ i, nonempty (F'.obj i) := Œª i,
    ‚ü®ulift.up (classical.arbitrary (F.obj (op i.unop.down)))‚ü©,
  haveI : ‚àÄ i, fintype (F'.obj i) := Œª i,
    fintype.of_equiv (F.obj (op i.unop.down)) equiv.ulift.symm,
  -- Step 2: apply the bootstrap theorem
  obtain ‚ü®u, hu‚ü© := nonempty_sections_of_fintype_inverse_system.init F',
  -- Step 3: interpret the results
  use Œª j, (u (op (ulift.up j.unop))).down,
  intros j j' f,
  let f' : ulift.up.{v} j'.unop ‚ü∂ ulift.up.{v} j.unop :=
    hom_of_le (le_of_hom f.unop : unop j' ‚â§ unop j),
  have h := hu f'.op,
  simp only [functor.comp_map, ulift_functor_map, functor.op_map] at h,
  simp only [‚Üêh],
  congr,
end

end fintype_konig
