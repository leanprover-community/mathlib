/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import topology.algebra.order.basic

/-!
-/

open filter set function order_dual
open_locale topological_space filter interval

variables {X : Type*} [linear_order X] {a b c : X} {s t : set X}

@[simp] lemma ord_connected_component_mem_nhds [topological_space X] [order_topology X] :
  ord_connected_component s a ‚àà ùìù a ‚Üî s ‚àà ùìù a :=
begin
  refine ‚ü®Œª h, mem_of_superset h ord_connected_component_subset, Œª h, _‚ü©,
  rcases exists_Icc_mem_subset_of_mem_nhds h with ‚ü®b, c, ha, ha', hs‚ü©,
  exact mem_of_superset ha' (subset_ord_connected_component ha hs)
end

namespace order_normal

def sep_set (s t : set X) : set X :=
(‚ãÉ x ‚àà s, ord_connected_component t·∂ú x) ‚à© (‚ãÉ x ‚àà t, ord_connected_component s·∂ú x)

lemma sep_set_comm (s t : set X) : sep_set s t = sep_set t s := inter_comm _ _

lemma disjoint_left_sep_set : disjoint s (sep_set s t) :=
disjoint.inter_right' _ $ disjoint_Union‚ÇÇ_right.2 $ Œª x hx, disjoint_compl_right.mono_right $
  ord_connected_component_subset

lemma disjoint_right_sep_set : disjoint t (sep_set s t) :=
sep_set_comm t s ‚ñ∏ disjoint_left_sep_set

lemma dual_sep_set : sep_set (of_dual ‚Åª¬π' s) (of_dual ‚Åª¬π' t) = of_dual ‚Åª¬π' (sep_set s t) :=
by simp only [sep_set, mem_preimage, ‚Üê to_dual.surjective.Union_comp, of_dual_to_dual,
  dual_ord_connected_component, ‚Üê preimage_compl, preimage_inter, preimage_Union]

def nhd (s t : set X) : set X :=
‚ãÉ x ‚àà s, ord_connected_component (t·∂ú ‚à© (ord_connected_section $ sep_set s t)·∂ú) x

lemma disjoint_nhd : disjoint (nhd s t) (nhd t s) :=
begin
  rintro x ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©,
  rcases mem_Union‚ÇÇ.1 hx‚ÇÅ with ‚ü®a, has, ha‚ü©, clear hx‚ÇÅ,
  rcases mem_Union‚ÇÇ.1 hx‚ÇÇ with ‚ü®b, hbt, hb‚ü©, clear hx‚ÇÇ,
  rw [mem_ord_connected_component, subset_inter_iff] at ha hb,
  wlog hab : a ‚â§ b := le_total a b using [a b s t, b a t s] tactic.skip,
  rotate, from Œª h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ, this h‚ÇÇ h‚ÇÅ h‚ÇÑ h‚ÇÉ,
  cases ha with ha ha', cases hb with hb hb',
  have hsub : [a, b] ‚äÜ (sep_set s t).ord_connected_section·∂ú,
  { rw [sep_set_comm, interval_swap] at hb',
    calc [a, b] ‚äÜ [a, x] ‚à™ [x, b] : interval_subset_interval_union_interval
    ... ‚äÜ (sep_set s t).ord_connected_section·∂ú : union_subset ha' hb' },
  clear ha' hb',
  cases le_total x a with hxa hax,
  { exact hb (Icc_subset_interval' ‚ü®hxa, hab‚ü©) has },
  cases le_total b x with hbx hxb,
  { exact ha (Icc_subset_interval ‚ü®hab, hbx‚ü©) hbt },
  have : x ‚àà sep_set s t,
  { exact ‚ü®mem_Union‚ÇÇ.2 ‚ü®a, has, ha‚ü©, mem_Union‚ÇÇ.2 ‚ü®b, hbt, hb‚ü©‚ü© },
  lift x to sep_set s t using this,
  suffices : ord_connected_component (sep_set s t) x ‚äÜ [a, b],
    from hsub (this $ ord_connected_proj_mem_ord_connected_component _ _) (mem_range_self _),
  rintros y (hy : [‚Üëx, y] ‚äÜ sep_set s t),
  rw [interval_of_le hab, mem_Icc, ‚Üê not_lt, ‚Üê not_lt],
  refine ‚ü®Œª hya, _, Œª hyb, _‚ü©,
  { exact disjoint_left.1 disjoint_left_sep_set has (hy $ Icc_subset_interval' ‚ü®hya.le, hax‚ü©) },
  { exact disjoint_left.1 disjoint_right_sep_set hbt (hy $ Icc_subset_interval ‚ü®hxb, hyb.le‚ü©) }
end

variables [topological_space X] [order_topology X]

lemma compl_section_sep_set_mem_nhds_within_Ici (hd : disjoint s (closure t)) (ha : a ‚àà s) :
  (ord_connected_section $ sep_set s t)·∂ú ‚àà ùìù[‚â•] a :=
begin
  have hmem : t·∂ú ‚àà ùìù[‚â•] a,
  { refine mem_nhds_within_of_mem_nhds _,
    rw [‚Üê mem_interior_iff_mem_nhds, interior_compl],
    exact disjoint_left.1 hd ha },
  rcases exists_Icc_mem_subset_of_mem_nhds_within_Ici hmem with ‚ü®b, hab, hmem', hsub‚ü©,
  by_cases H : disjoint (Icc a b) (ord_connected_section $ sep_set s t),
  { exact mem_of_superset hmem' (disjoint_left.1 H) },
  { simp only [set.disjoint_left, not_forall, not_not] at H,
    rcases H with ‚ü®c, ‚ü®hac, hcb‚ü©, hc‚ü©,
    have hsub' : Icc a b ‚äÜ ord_connected_component t·∂ú a,
      from subset_ord_connected_component (left_mem_Icc.2 hab) hsub,
    replace hac : a < c := hac.lt_of_ne (ne.symm $ ne_of_mem_of_not_mem hc $
      disjoint_left.1 (disjoint_left_sep_set.mono_right ord_connected_section_subset) ha),
    refine mem_of_superset (Ico_mem_nhds_within_Ici (left_mem_Ico.2 hac)) (Œª x hx hx', _),
    refine hx.2.ne (eq_of_mem_ord_connected_section_of_interval_subset hx' hc _),
    refine subset_inter (subset_Union‚ÇÇ_of_subset a ha _) _,
    { exact ord_connected.interval_subset infer_instance (hsub' ‚ü®hx.1, hx.2.le.trans hcb‚ü©)
        (hsub' ‚ü®hac.le, hcb‚ü©) },
    { rcases mem_Union‚ÇÇ.1 (ord_connected_section_subset hx').2 with ‚ü®y, hyt, hxy‚ü©,
      refine subset_Union‚ÇÇ_of_subset y hyt (ord_connected.interval_subset infer_instance hxy _),
      refine subset_ord_connected_component left_mem_interval hxy _,
      suffices : c < y,
      { rw [interval_of_ge (hx.2.trans this).le],
        exact ‚ü®hx.2.le, this.le‚ü© },
      refine lt_of_not_le (Œª hyc, _),
      have hya : y < a, from not_le.1 (Œª hay, hsub ‚ü®hay, hyc.trans hcb‚ü© hyt),
      exact hxy (Icc_subset_interval ‚ü®hya.le, hx.1‚ü©) ha } }
end

lemma compl_section_sep_set_mem_nhds_within_Iic (hd : disjoint s (closure t)) (ha : a ‚àà s) :
  (ord_connected_section $ sep_set s t)·∂ú ‚àà ùìù[‚â§] a :=
have hd' : disjoint (of_dual ‚Åª¬π' s) (closure $ of_dual ‚Åª¬π' t) := hd,
have ha' : to_dual a ‚àà of_dual ‚Åª¬π' s := ha,
by simpa only [dual_sep_set, dual_ord_connected_section]
  using compl_section_sep_set_mem_nhds_within_Ici hd' ha'

lemma compl_section_sep_set_mem_nhds (hd : disjoint s (closure t)) (ha : a ‚àà s) :
  (ord_connected_section $ sep_set s t)·∂ú ‚àà ùìù a :=
begin
  rw [‚Üê nhds_left_sup_nhds_right, mem_sup],
  exact ‚ü®compl_section_sep_set_mem_nhds_within_Iic hd ha,
    compl_section_sep_set_mem_nhds_within_Ici hd ha‚ü©
end

lemma nhd_mem_nhds_set (hd : disjoint s (closure t)) : nhd s t ‚àà ùìùÀ¢ s :=
bUnion_mem_nhds_set $ Œª x hx, ord_connected_component_mem_nhds.2 $
  inter_mem (by { rw [‚Üê mem_interior_iff_mem_nhds, interior_compl], exact disjoint_left.1 hd hx })
    (compl_section_sep_set_mem_nhds hd hx)

lemma t5 (h‚ÇÅ : disjoint (closure s) t) (h‚ÇÇ : disjoint s (closure t)) :
  disjoint (ùìùÀ¢ s) (ùìùÀ¢ t) :=
filter.disjoint_iff.2
  ‚ü®nhd s t, nhd_mem_nhds_set h‚ÇÇ, nhd t s, nhd_mem_nhds_set h‚ÇÅ.symm, disjoint_nhd‚ü©

end order_normal

@[priority 100]
instance order_topology.t5_space [topological_space X] [order_topology X] : t5_space X :=
‚ü®Œª s t, order_normal.t5‚ü©
