/-
Copyright (c) 2019 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Floris van Doorn
-/
import category_theory.limits.shapes.products
import category_theory.discrete_category

universes v u

open category_theory
open category_theory.functor
open opposite

namespace category_theory.limits

variables {C : Type u} [category.{v} C]
variables {J : Type v} [small_category J]
variable (F : J ‚•§ C·µí·µñ)

/--
If `F.left_op : J·µí·µñ ‚•§ C` has a chosen colimit, we can construct a chosen limit for `F : J ‚•§ C·µí·µñ`.
-/
def has_limit_of_has_colimit_left_op [has_colimit F.left_op] : has_limit F :=
{ cone := cone_of_cocone_left_op (colimit.cocone F.left_op),
  is_limit :=
  { lift := Œª s, (colimit.desc F.left_op (cocone_left_op_of_cone s)).op,
    fac' := Œª s j,
    begin
      rw [cone_of_cocone_left_op_œÄ_app, colimit.cocone_Œπ, ‚Üêop_comp,
          colimit.Œπ_desc, cocone_left_op_of_cone_Œπ_app, has_hom.hom.op_unop],
      refl, end,
    uniq' := Œª s m w,
    begin
      -- It's a pity we can't do this automatically.
      -- Usually something like this would work by limit.hom_ext,
      -- but the opposites get in the way of this firing.
      have u := (colimit.is_colimit F.left_op).uniq (cocone_left_op_of_cone s) (m.unop),
      convert congr_arg (Œª f : _ ‚ü∂ _, f.op) (u _), clear u,
      intro j,
      rw [cocone_left_op_of_cone_Œπ_app, colimit.cocone_Œπ],
      convert congr_arg (Œª f : _ ‚ü∂ _, f.unop) (w (unop j)), clear w,
      rw [cone_of_cocone_left_op_œÄ_app, colimit.cocone_Œπ, has_hom.hom.unop_op],
      refl,
    end } }

/--
If `C` has chosen colimits of shape `J·µí·µñ`, we can construct chosen limits in `C·µí·µñ` of shape `J`.
-/
def has_limits_of_shape_op_of_has_colimits_of_shape [has_colimits_of_shape J·µí·µñ C] :
  has_limits_of_shape J C·µí·µñ :=
{ has_limit := Œª F, has_limit_of_has_colimit_left_op F }

local attribute [instance] has_limits_of_shape_op_of_has_colimits_of_shape

/--
If `C` has chosen colimits, we can construct chosen limits for `C·µí·µñ`.
-/
def has_limits_op_of_has_colimits [has_colimits C] : has_limits C·µí·µñ :=
{ has_limits_of_shape := Œª J ùí•, by { resetI, apply_instance } }

/--
If `F.left_op : J·µí·µñ ‚•§ C` has a chosen limit, we can construct a chosen colimit for `F : J ‚•§ C·µí·µñ`.
-/
def has_colimit_of_has_limit_left_op [has_limit F.left_op] : has_colimit F :=
{ cocone := cocone_of_cone_left_op (limit.cone F.left_op),
  is_colimit :=
  { desc := Œª s, (limit.lift F.left_op (cone_left_op_of_cocone s)).op,
    fac' := Œª s j,
    begin
      rw [cocone_of_cone_left_op_Œπ_app, limit.cone_œÄ, ‚Üêop_comp,
          limit.lift_œÄ, cone_left_op_of_cocone_œÄ_app, has_hom.hom.op_unop],
      refl, end,
    uniq' := Œª s m w,
    begin
      have u := (limit.is_limit F.left_op).uniq (cone_left_op_of_cocone s) (m.unop),
      convert congr_arg (Œª f : _ ‚ü∂ _, f.op) (u _), clear u,
      intro j,
      rw [cone_left_op_of_cocone_œÄ_app, limit.cone_œÄ],
      convert congr_arg (Œª f : _ ‚ü∂ _, f.unop) (w (unop j)), clear w,
      rw [cocone_of_cone_left_op_Œπ_app, limit.cone_œÄ, has_hom.hom.unop_op],
      refl,
    end } }

/--
If `C` has chosen colimits of shape `J·µí·µñ`, we can construct chosen limits in `C·µí·µñ` of shape `J`.
-/
def has_colimits_of_shape_op_of_has_limits_of_shape [has_limits_of_shape J·µí·µñ C] :
  has_colimits_of_shape J C·µí·µñ :=
{ has_colimit := Œª F, has_colimit_of_has_limit_left_op F }

local attribute [instance] has_colimits_of_shape_op_of_has_limits_of_shape

/--
If `C` has chosen limits, we can construct chosen colimits for `C·µí·µñ`.
-/
def has_colimits_op_of_has_limits [has_limits C] : has_colimits C·µí·µñ :=
{ has_colimits_of_shape := Œª J ùí•, by { resetI, apply_instance } }

variables (X : Type v)
/--
If `C` has products indexed by `X`, then `C·µí·µñ` has coproducts indexed by `X`.
-/
def has_coproducts_opposite [has_products_of_shape X C] :
  has_coproducts_of_shape X C·µí·µñ :=
begin
  haveI : has_limits_of_shape (discrete X)·µí·µñ C :=
    has_limits_of_shape_of_equivalence (discrete.opposite X).symm,
  apply_instance
end

/--
If `C` has coproducts indexed by `X`, then `C·µí·µñ` has products indexed by `X`.
-/
def has_products_opposite [has_coproducts_of_shape X C] :
  has_products_of_shape X C·µí·µñ :=
begin
  haveI : has_colimits_of_shape (discrete X)·µí·µñ C :=
    has_colimits_of_shape_of_equivalence (discrete.opposite X).symm,
  apply_instance
end

end category_theory.limits
