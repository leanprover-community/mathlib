/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Markus Himmel
-/
import algebra.homology.chain_complex
import algebra.homology.image_to_kernel_map

/-!
# (Co)homology groups for complexes

We setup that part of the theory of homology groups which works in
any category with kernels and images.

We define the homology groups themselves, and show that they induce maps on kernels.

Under the additional assumption that our category has equalizers and functorial images, we construct
induced morphisms on images and functorial induced morphisms in homology.

## Chains and cochains

Throughout we work with complexes graded by an arbitrary `[add_comm_group Œ≤]`,
with a differential with grading `b : Œ≤`.
Thus we're simultaneously doing homology and cohomology groups
(and in future, e.g., enabling computing homologies for successive pages of spectral sequences).

At the end of the file we set up abbreviations `cohomology` and `graded_cohomology`,
so that when you're working with a `C : cochain_complex V`, you can write `C.cohomology i`
rather than the confusing `C.homology i`.
-/

universes v u

open category_theory
open category_theory.limits

variables {V : Type u} [category.{v} V] [has_zero_morphisms V]

variables {Œ≤ : Type} [add_comm_group Œ≤] {b : Œ≤}

namespace homological_complex

section has_kernels

variable [has_kernels V]

/-- The map induced by a chain map between the kernels of the differentials. -/
def kernel_map {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  kernel (C.d i) ‚ü∂ kernel (C'.d i) :=
kernel.lift _ (kernel.Œπ _ ‚â´ f.f i)
begin
  rw [category.assoc, ‚Üêcomm_at f, ‚Üêcategory.assoc, kernel.condition, has_zero_morphisms.zero_comp],
end

@[simp, reassoc]
lemma kernel_map_condition {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  kernel_map f i ‚â´ kernel.Œπ (C'.d i) = kernel.Œπ (C.d i) ‚â´ f.f i :=
by simp [kernel_map]

@[simp]
lemma kernel_map_id (C : homological_complex V b) (i : Œ≤) :
  kernel_map (ùüô C) i = ùüô _ :=
(cancel_mono (kernel.Œπ (C.d i))).1 $ by simp

@[simp]
lemma kernel_map_comp {C C' C'' : homological_complex V b} (f : C ‚ü∂ C')
  (g : C' ‚ü∂ C'') (i : Œ≤) :
  kernel_map (f ‚â´ g) i = kernel_map f i ‚â´ kernel_map g i :=
(cancel_mono (kernel.Œπ (C''.d i))).1 $ by simp

/-- The kernels of the differentials of a complex form a `Œ≤`-graded object. -/
def kernel_functor : homological_complex V b ‚•§ graded_object Œ≤ V :=
{ obj := Œª C i, kernel (C.d i),
  map := Œª X Y f i, kernel_map f i }

end has_kernels

section has_image_maps
variables [has_images V] [has_image_maps V]

/-- A morphism of complexes induces a morphism on the images of the differentials in every
    degree. -/
abbreviation image_map {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  image (C.d i) ‚ü∂ image (C'.d i) :=
image.map (arrow.hom_mk' (comm_at f i).symm)

@[simp]
lemma image_map_Œπ {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  image_map f i ‚â´ image.Œπ (C'.d i) = image.Œπ (C.d i) ‚â´ f.f (i + b) :=
image.map_hom_mk'_Œπ (comm_at f i).symm

end has_image_maps

variables [has_images V] [has_equalizers V]

/--
The connecting morphism from the image of `d i` to the kernel of `d (i ¬± 1)`.
-/
def image_to_kernel_map (C : homological_complex V b) (i : Œ≤) :
  image (C.d i) ‚ü∂ kernel (C.d (i+b)) :=
category_theory.image_to_kernel_map (C.d i) (C.d (i+b)) (by simp)

@[simp, reassoc]
lemma image_to_kernel_map_condition (C : homological_complex V b) (i : Œ≤) :
  image_to_kernel_map C i ‚â´ kernel.Œπ (C.d (i + b)) = image.Œπ (C.d i) :=
by simp [image_to_kernel_map, category_theory.image_to_kernel_map]

@[reassoc]
lemma image_to_kernel_map_comp_kernel_map [has_image_maps V]
  {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  image_to_kernel_map C i ‚â´ kernel_map f (i + b) = image_map f i ‚â´ image_to_kernel_map C' i :=
by { ext, simp }

variables [has_cokernels V]

/-- The `i`-th homology group of the complex `C`. -/
def homology_group (i : Œ≤) (C : homological_complex V b) : V :=
cokernel (image_to_kernel_map C (i-b))

variables [has_image_maps V]

/-- A chain map induces a morphism in homology at every degree. -/
def homology_map {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  C.homology_group i ‚ü∂ C'.homology_group i :=
cokernel.desc _ (kernel_map f (i - b + b) ‚â´ cokernel.œÄ _) $
  by simp [image_to_kernel_map_comp_kernel_map_assoc]

@[simp, reassoc]
lemma homology_map_condition {C C' : homological_complex V b} (f : C ‚ü∂ C') (i : Œ≤) :
  cokernel.œÄ (image_to_kernel_map C (i - b)) ‚â´ homology_map f i =
    kernel_map f (i - b + b) ‚â´ cokernel.œÄ _ :=
by simp [homology_map]

@[simp]
lemma homology_map_id (C : homological_complex V b) (i : Œ≤) :
  homology_map (ùüô C) i = ùüô (C.homology_group i) :=
begin
  ext,
  simp only [homology_map_condition, kernel_map_id, category.id_comp],
  erw [category.comp_id]
end

@[simp]
lemma homology_map_comp {C C' C'' : homological_complex V b} (f : C ‚ü∂ C') (g : C' ‚ü∂ C'') (i : Œ≤) :
  homology_map (f ‚â´ g) i = homology_map f i ‚â´ homology_map g i :=
by { ext, simp }

variables (V)

/-- The `i`-th homology functor from `Œ≤` graded complexes to `V`. -/
@[simps]
def homology (i : Œ≤) : homological_complex V b ‚•§ V :=
{ obj := Œª C, C.homology_group i,
  map := Œª C C' f, homology_map f i, }

/-- The homology functor from `Œ≤` graded complexes to `Œ≤` graded objects in `V`. -/
@[simps]
def graded_homology : homological_complex V b ‚•§ graded_object Œ≤ V :=
{ obj := Œª C i, C.homology_group i,
  map := Œª C C' f i, homology_map f i }

end homological_complex

/-!
We now set up abbreviations so that you can write `C.cohomology i` or `(graded_cohomology V).map f`,
etc., when `C` is a cochain complex.
-/

namespace cochain_complex

variables [has_images V] [has_equalizers V] [has_cokernels V]

/-- The `i`-th cohomology group of the cochain complex `C`. -/
abbreviation cohomology_group (C : cochain_complex V) (i : ‚Ñ§) : V :=
C.homology_group i

variables [has_image_maps V]

/-- A chain map induces a morphism in cohomology at every degree. -/
abbreviation cohomology_map {C C' : cochain_complex V} (f : C ‚ü∂ C') (i : ‚Ñ§) :
  C.cohomology_group i ‚ü∂ C'.cohomology_group i :=
homological_complex.homology_map f i

variables (V)

/-- The `i`-th homology functor from cohain complexes to `V`. -/
abbreviation cohomology (i : ‚Ñ§) : cochain_complex V ‚•§ V :=
homological_complex.homology V i


/-- The cohomology functor from cochain complexes to `‚Ñ§`-graded objects in `V`. -/
abbreviation graded_cohomology : cochain_complex V ‚•§ graded_object ‚Ñ§ V :=
homological_complex.graded_homology V

end cochain_complex
