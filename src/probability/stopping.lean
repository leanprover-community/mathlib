/-
Copyright (c) 2021 Kexing Ying. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kexing Ying
-/
import measure_theory.constructions.borel_space
import measure_theory.function.l1_space
import measure_theory.function.strongly_measurable
import data.nat.succ_pred
import topology.instances.discrete

/-!
# Filtration and stopping time

This file defines some standard definition from the theory of stochastic processes including
filtrations and stopping times. These definitions are used to model the amount of information
at a specific time and is the first step in formalizing stochastic processes.

## Main definitions

* `measure_theory.filtration`: a filtration on a measurable space
* `measure_theory.adapted`: a sequence of functions `u` is said to be adapted to a
  filtration `f` if at each point in time `i`, `u i` is `f i`-measurable
* `measure_theory.filtration.natural`: the natural filtration with respect to a sequence of
  measurable functions is the smallest filtration to which it is adapted to
* `measure_theory.is_stopping_time`: a stopping time with respect to some filtration `f` is a
  function `œÑ` such that for all `i`, the preimage of `{j | j ‚â§ i}` along `œÑ` is
  `f i`-measurable
* `measure_theory.is_stopping_time.measurable_space`: the œÉ-algebra associated with a stopping time

## Tags

filtration, stopping time, stochastic process

-/

open topological_space filter
open_locale classical measure_theory nnreal ennreal topological_space big_operators

namespace measure_theory

/-- A `filtration` on measurable space `Œ±` with œÉ-algebra `m` is a monotone
sequence of of sub-œÉ-algebras of `m`. -/
structure filtration {Œ± : Type*} (Œπ : Type*) [preorder Œπ] (m : measurable_space Œ±) :=
(seq : Œπ ‚Üí measurable_space Œ±)
(mono : monotone seq)
(le : ‚àÄ i : Œπ, seq i ‚â§ m)

variables {Œ± Œ≤ Œπ : Type*} {m : measurable_space Œ±}

section preorder

variables [preorder Œπ]

instance : has_coe_to_fun (filtration Œπ m) (Œª _, Œπ ‚Üí measurable_space Œ±) :=
‚ü®Œª f, f.seq‚ü©

/-- The constant filtration which is equal to `m` for all `i : Œπ`. -/
def const_filtration (m : measurable_space Œ±) : filtration Œπ m :=
‚ü®Œª _, m, monotone_const, Œª _, le_rfl‚ü©

instance : inhabited (filtration Œπ m) :=
‚ü®const_filtration m‚ü©

lemma measurable_set_of_filtration {f : filtration Œπ m} {s : set Œ±} {i : Œπ}
  (hs : measurable_set[f i] s) : measurable_set[m] s :=
f.le i s hs

/-- A measure is œÉ-finite with respect to filtration if it is œÉ-finite with respect
to all the sub-œÉ-algebra of the filtration. -/
class sigma_finite_filtration (Œº : measure Œ±) (f : filtration Œπ m) : Prop :=
(sigma_finite : ‚àÄ i : Œπ, sigma_finite (Œº.trim (f.le i)))

instance sigma_finite_of_sigma_finite_filtration (Œº : measure Œ±) (f : filtration Œπ m)
  [hf : sigma_finite_filtration Œº f] (i : Œπ) :
  sigma_finite (Œº.trim (f.le i)) :=
by apply hf.sigma_finite -- can't exact here

variables [measurable_space Œ≤]

section adapted_process

variables {u v : Œπ ‚Üí Œ± ‚Üí Œ≤} {f : filtration Œπ m}

/-- A sequence of functions `u` is adapted to a filtration `f` if for all `i`,
`u i` is `f i`-measurable. -/
def adapted (f : filtration Œπ m) (u : Œπ ‚Üí Œ± ‚Üí Œ≤) : Prop :=
‚àÄ i : Œπ, measurable[f i] (u i)

namespace adapted

lemma add [has_add Œ≤] [has_measurable_add‚ÇÇ Œ≤] (hu : adapted f u) (hv : adapted f v) :
  adapted f (u + v) :=
Œª i, (hu i).add (hv i)

lemma neg [has_neg Œ≤] [has_measurable_neg Œ≤] (hu : adapted f u) : adapted f (-u) :=
Œª i, (hu i).neg

lemma smul [has_scalar ‚Ñù Œ≤] [has_measurable_smul ‚Ñù Œ≤] (c : ‚Ñù) (hu : adapted f u) :
  adapted f (c ‚Ä¢ u) :=
Œª i, (hu i).const_smul c

end adapted

variable (Œ≤)
lemma adapted_zero [has_zero Œ≤] (f : filtration Œπ m) : adapted f (0 : Œπ ‚Üí Œ± ‚Üí Œ≤) :=
Œª i, @measurable_zero Œ≤ Œ± _ (f i) _

variable {Œ≤}

/-- Progressively measurable process. The usual definition uses the interval `[0,i]`, which we
replace by `set.Iic i`. We recover the usual definition for `‚Ñù‚â•0` or `‚Ñï`. -/
def prog_measurable [measurable_space Œπ] (f : filtration Œπ m) (u : Œπ ‚Üí Œ± ‚Üí Œ≤) : Prop :=
‚àÄ i, measurable[prod.measurable_space' subtype.measurable_space (f i)]
  (Œª p : set.Iic i √ó Œ±, u p.1 p.2)

lemma prog_measurable_const [measurable_space Œπ] (f : filtration Œπ m) (b : Œ≤) :
  prog_measurable f ((Œª _ _, b) : Œπ ‚Üí Œ± ‚Üí Œ≤) :=
Œª i, @measurable_const _ _ _ (prod.measurable_space' subtype.measurable_space (f i)) _

namespace prog_measurable

variables [measurable_space Œπ]

protected lemma adapted (h : prog_measurable f u) : adapted f u :=
begin
  intro i,
  have : u i = (Œª p : set.Iic i √ó Œ±, u p.1 p.2) ‚àò (Œª x, (‚ü®i, set.mem_Iic.mpr le_rfl‚ü©, x)) := rfl,
  rw this,
  refine (h i).comp ((@measurable_const _ _ _ (f i) _).prod_mk (@measurable_id _ (f i))),
end

protected lemma comp {t : Œπ ‚Üí Œ± ‚Üí Œπ} (h : prog_measurable f u) (ht : prog_measurable f t)
  (ht_le : ‚àÄ i x, t i x ‚â§ i) :
  prog_measurable f (Œª i x, u (t i x) x) :=
begin
  intro i,
  dsimp only,
  have : (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u (t (p.fst : Œπ) p.snd) p.snd)
    = (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u (p.fst : Œπ) p.snd) ‚àò (Œª p : ‚Ü•(set.Iic i) √ó Œ±,
      (‚ü®t (p.fst : Œπ) p.snd, set.mem_Iic.mpr ((ht_le _ _).trans p.fst.prop)‚ü©, p.snd)) := rfl,
  rw this,
  exact (h i).comp ((ht i).subtype_mk.prod_mk (@measurable_snd _ _ _ (f i))),
end

section arithmetic

@[to_additive] protected lemma mul [has_mul Œ≤] [has_measurable_mul‚ÇÇ Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i x, u i x * v i x) :=
Œª i, (hu i).mul (hv i)

@[to_additive] protected lemma finset_prod {Œ≥} [comm_monoid Œ≤] [has_measurable_mul‚ÇÇ Œ≤]
  {U : Œ≥ ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤} {s : finset Œ≥} (h : ‚àÄ c ‚àà s, prog_measurable f (U c)) :
  prog_measurable f (Œª i a, ‚àè c in s, U c i a) :=
begin
  revert h,
  refine finset.induction _ _ s,
  { simp only [finset.prod_empty], exact Œª _, prog_measurable_const _ _, },
  { intros c t hct h_ind h_pm,
    have : (Œª i a, ‚àè c' in insert c t, U c' i a) = (Œª i a, (U c i a) * ‚àè c' in t, U c' i a),
    { ext i a, rw finset.prod_insert hct, },
    rw this,
    exact prog_measurable.mul (h_pm c (finset.mem_insert_self _ _))
      (h_ind (Œª c' hc', h_pm c' (finset.mem_insert_of_mem hc'))), },
end

@[to_additive] protected lemma inv [has_inv Œ≤] [has_measurable_inv Œ≤] (hu : prog_measurable f u) :
  prog_measurable f (Œª i x, (u i x)‚Åª¬π) :=
Œª i, (hu i).inv

@[to_additive] protected lemma div [has_div Œ≤] [has_measurable_div‚ÇÇ Œ≤]
  (hu : prog_measurable f u) (hv : prog_measurable f v) :
  prog_measurable f (Œª i x, u i x / v i x) :=
Œª i, (hu i).div (hv i)

end arithmetic

end prog_measurable

lemma prog_measurable_of_tendsto' {Œ≥} [measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  (fltr : filter Œ≥) [fltr.ne_bot] [fltr.is_countably_generated] {U : Œ≥ ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U fltr (ùìù u)) :
  prog_measurable f u :=
begin
  refine Œª i, @measurable_of_tendsto_metric' (set.Iic i √ó Œ±) Œ≤ (prod.measurable_space' _ (f i))
    _ _ _ Œ≥ _ _ fltr _ _ (Œª l, h l i) _,
  rw tendsto_pi_nhds at h_tendsto ‚ä¢,
  intro x,
  specialize h_tendsto x.fst,
  rw tendsto_nhds at h_tendsto ‚ä¢,
  exact Œª s hs h_mem, h_tendsto {g : Œ± ‚Üí Œ≤ | g x.snd ‚àà s}
    (hs.preimage (continuous_apply x.snd)) h_mem,
end

lemma prog_measurable_of_tendsto [measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  {U : ‚Ñï ‚Üí Œπ ‚Üí Œ± ‚Üí Œ≤}
  (h : ‚àÄ l, prog_measurable f (U l)) (h_tendsto : tendsto U at_top (ùìù u)) :
  prog_measurable f u :=
prog_measurable_of_tendsto' at_top h h_tendsto

/-- A continuous and adapted process is progressively measurable. -/
theorem adapted.prog_measurable_of_continuous [emetric_space Œπ] [measurable_space Œπ]
  [second_countable_topology Œπ] [opens_measurable_space Œπ] [metric_space Œ≤] [borel_space Œ≤]
  (h : adapted f u) (hu_cont : ‚àÄ x, continuous (Œª i, u i x)) :
  prog_measurable f u :=
begin
  intro i,
  obtain ‚ü®t_sf, ht_sf‚ü© : ‚àÉ t : ‚Ñï ‚Üí @simple_func (set.Iic i) _ (set.Iic i), ‚àÄ j x,
    tendsto (Œª n, u (t n j) x) at_top (ùìù $ u j x),
  { have h_str_meas : strongly_measurable (id : set.Iic i ‚Üí set.Iic i),
      from @strongly_measurable_id (set.Iic i) subtype.measurable_space _ _ _,
    refine ‚ü®h_str_meas.approx, Œª j x, _‚ü©,
    have h_tendsto := h_str_meas.tendsto_approx j,
    exact ((hu_cont x).tendsto j).comp ((continuous_induced_dom.tendsto j).comp h_tendsto), },
  let U := Œª (n : ‚Ñï) (p : set.Iic i √ó Œ±), u (t_sf n p.fst) p.snd,
  have h_tendsto : tendsto U at_top (ùìù (Œª p, u p.fst p.snd)),
  { rw tendsto_pi_nhds,
    exact Œª p, ht_sf p.fst p.snd, },
  refine @measurable_of_tendsto_metric (set.Iic i √ó Œ±) Œ≤
    (prod.measurable_space' subtype.measurable_space (f i)) _ _ _ _ _ (Œª n, _) h_tendsto,
  change measurable[prod.measurable_space' subtype.measurable_space (f i)]
    (Œª p : set.Iic i √ó Œ±, u (t_sf n p.fst) p.snd),
  have h_meas : measurable[prod.measurable_space' subtype.measurable_space (f i)]
    (Œª (p : (t_sf n).range √ó Œ±), u ‚Üëp.fst p.snd),
  { have : (Œª (p : ‚Ü•((t_sf n).range) √ó Œ±), u ‚Üë(p.fst) p.snd)
        = (Œª (p : Œ± √ó ((t_sf n).range)), u ‚Üë(p.snd) p.fst) ‚àò prod.swap,
      by refl,
    rw [this,  @measurable_swap_iff Œ± ‚Ü•((t_sf n).range) Œ≤ (f i)],
    haveI : encodable (t_sf n).range, from fintype.encodable ‚Ü•(t_sf n).range,
    exact measurable_from_prod_encodable (Œª j, (h j).mono (f.mono (j : set.Iic i).prop) le_rfl), },
  have : (Œª p : set.Iic i √ó Œ±, u (t_sf n p.fst) p.snd)
    = (Œª p : ‚Ü•(t_sf n).range √ó Œ±, u p.fst p.snd)
      ‚àò (Œª p : set.Iic i √ó Œ±, (‚ü®t_sf n p.fst, simple_func.mem_range_self _ _‚ü©, p.snd)),
  { refl, },
  rw this,
  refine h_meas.comp (measurable.prod_mk _ (@measurable_snd _ _ _ (f i))),
  exact ((t_sf n).measurable.comp (@measurable_fst _ _ _ (f i))).subtype_mk,
end

end adapted_process

namespace filtration

/-- Given a sequence of functions, the natural filtration is the smallest sequence
of œÉ-algebras such that that sequence of functions is measurable with respect to
the filtration. -/
def natural (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (hum : ‚àÄ i, measurable (u i)) : filtration Œπ m :=
{ seq := Œª i, ‚®Ü j ‚â§ i, measurable_space.comap (u j) infer_instance,
  mono := Œª i j hij, bsupr_le_bsupr' $ Œª k hk, le_trans hk hij,
  le := Œª i, bsupr_le (Œª j hj s hs, let ‚ü®t, ht, ht'‚ü© := hs in ht' ‚ñ∏ hum j ht) }

lemma adapted_natural {u : Œπ ‚Üí Œ± ‚Üí Œ≤} (hum : ‚àÄ i, measurable[m] (u i)) :
  adapted (natural u hum) u :=
Œª i, measurable.le (le_bsupr_of_le i (le_refl i) le_rfl) (Œª s hs, ‚ü®s, hs, rfl‚ü©)

end filtration

/-- A stopping time with respect to some filtration `f` is a function
`œÑ` such that for all `i`, the preimage of `{j | j ‚â§ i}` along `œÑ` is measurable
with respect to `f i`.

Intuitively, the stopping time `œÑ` describes some stopping rule such that at time
`i`, we may determine it with the information we have at time `i`. -/
def is_stopping_time (f : filtration Œπ m) (œÑ : Œ± ‚Üí Œπ) :=
‚àÄ i : Œπ, measurable_set[f i] $ {x | œÑ x ‚â§ i}

lemma is_stopping_time.measurable_set_le {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ}
  (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable_set[f i] {x | œÑ x ‚â§ i} :=
hœÑ i

variables {f : filtration ‚Ñï m} {œÑ : Œ± ‚Üí ‚Ñï}

lemma is_stopping_time.measurable_set_eq (hœÑ : is_stopping_time f œÑ) (i : ‚Ñï) :
  measurable_set[f i] {x | œÑ x = i} :=
begin
  cases i,
  { convert (hœÑ 0),
    simp only [set.set_of_eq_eq_singleton, le_zero_iff] },
  { rw (_ : {x | œÑ x = i + 1} = {x | œÑ x ‚â§ i + 1} \ {x | œÑ x ‚â§ i}),
    { exact (hœÑ (i + 1)).diff (f.mono (nat.le_succ _) _ (hœÑ i)) },
    { ext, simp only [set.mem_diff, not_le, set.mem_set_of_eq],
      split,
      { intro h, simp [h] },
      { rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©,
        linarith } } }
end

lemma is_stopping_time.measurable_set_ge (hœÑ : is_stopping_time f œÑ) (i : ‚Ñï) :
  measurable_set[f i] {x | i ‚â§ œÑ x} :=
begin
  have : {a : Œ± | i ‚â§ œÑ a} = (set.univ \ {a | œÑ a ‚â§ i}) ‚à™ {a | œÑ a = i},
  { ext1 a,
    simp only [true_and, set.mem_univ, set.mem_diff, not_le, set.mem_union_eq,
      set.mem_set_of_eq],
    rw le_iff_lt_or_eq,
    by_cases h : œÑ a = i,
    { simp [h], },
    { simp only [h, ne.symm h, or_false, or_iff_left_iff_imp], }, },
  rw this,
  exact (measurable_set.univ.diff (hœÑ i)).union (hœÑ.measurable_set_eq i),
end

lemma is_stopping_time.measurable_set_eq_le
  {f : filtration ‚Ñï m} {œÑ : Œ± ‚Üí ‚Ñï} (hœÑ : is_stopping_time f œÑ) {i j : ‚Ñï} (hle : i ‚â§ j) :
  measurable_set[f j] {x | œÑ x = i} :=
f.mono hle _ $ hœÑ.measurable_set_eq i

lemma is_stopping_time.measurable_set_lt (hœÑ : is_stopping_time f œÑ) (i : ‚Ñï) :
  measurable_set[f i] {x | œÑ x < i} :=
begin
  convert (hœÑ i).diff (hœÑ.measurable_set_eq i),
  ext,
  change œÑ x < i ‚Üî œÑ x ‚â§ i ‚àß œÑ x ‚â† i,
  rw lt_iff_le_and_ne,
end

lemma is_stopping_time.measurable_set_lt_le
  (hœÑ : is_stopping_time f œÑ) {i j : ‚Ñï} (hle : i ‚â§ j) :
  measurable_set[f j] {x | œÑ x < i} :=
f.mono hle _ $ hœÑ.measurable_set_lt i

lemma is_stopping_time_of_measurable_set_eq
  {f : filtration ‚Ñï m} {œÑ : Œ± ‚Üí ‚Ñï} (hœÑ : ‚àÄ i, measurable_set[f i] {x | œÑ x = i}) :
  is_stopping_time f œÑ :=
begin
  intro i,
  rw show {x | œÑ x ‚â§ i} = ‚ãÉ k ‚â§ i, {x | œÑ x = k}, by { ext, simp },
  refine measurable_set.bUnion (set.countable_encodable _) (Œª k hk, _),
  exact f.mono hk _ (hœÑ k),
end

lemma is_stopping_time_const {f : filtration Œπ m} (i : Œπ) :
  is_stopping_time f (Œª x, i) :=
Œª j, by simp

end preorder

namespace is_stopping_time

protected lemma max [linear_order Œπ] {f : filtration Œπ m} {œÑ œÄ : Œ± ‚Üí Œπ}
  (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) :
  is_stopping_time f (Œª x, max (œÑ x) (œÄ x)) :=
begin
  intro i,
  simp_rw [max_le_iff, set.set_of_and],
  exact (hœÑ i).inter (hœÄ i),
end

protected lemma min [linear_order Œπ] {f : filtration Œπ m} {œÑ œÄ : Œ± ‚Üí Œπ}
  (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) :
  is_stopping_time f (Œª x, min (œÑ x) (œÄ x)) :=
begin
  intro i,
  simp_rw [min_le_iff, set.set_of_or],
  exact (hœÑ i).union (hœÄ i),
end

lemma add_const
  [add_group Œπ] [preorder Œπ] [covariant_class Œπ Œπ (function.swap (+)) (‚â§)]
  [covariant_class Œπ Œπ (+) (‚â§)]
  {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) {i : Œπ} (hi : 0 ‚â§ i) :
  is_stopping_time f (Œª x, œÑ x + i) :=
begin
  intro j,
  simp_rw [‚Üê le_sub_iff_add_le],
  exact f.mono (sub_le_self j hi) _ (hœÑ (j - i)),
end

section preorder

variables [preorder Œπ] {f : filtration Œπ m}

/-- The associated œÉ-algebra with a stopping time. -/
protected def measurable_space
  {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) : measurable_space Œ± :=
{ measurable_set' := Œª s, ‚àÄ i : Œπ, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}),
  measurable_set_empty :=
    Œª i, (set.empty_inter {x | œÑ x ‚â§ i}).symm ‚ñ∏ @measurable_set.empty _ (f i),
  measurable_set_compl := Œª s hs i,
    begin
      rw (_ : s·∂ú ‚à© {x | œÑ x ‚â§ i} = (s·∂ú ‚à™ {x | œÑ x ‚â§ i}·∂ú) ‚à© {x | œÑ x ‚â§ i}),
      { refine measurable_set.inter _ _,
        { rw ‚Üê set.compl_inter,
          exact (hs i).compl },
        { exact hœÑ i} },
      { rw set.union_inter_distrib_right,
        simp only [set.compl_inter_self, set.union_empty] }
    end,
  measurable_set_Union := Œª s hs i,
    begin
      rw forall_swap at hs,
      rw set.Union_inter,
      exact measurable_set.Union (hs i),
    end }

@[protected]
lemma measurable_set {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) (s : set Œ±) :
  measurable_set[hœÑ.measurable_space] s ‚Üî
  ‚àÄ i : Œπ, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}) :=
iff.rfl

lemma measurable_space_mono
  {œÑ œÄ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) (hœÄ : is_stopping_time f œÄ) (hle : œÑ ‚â§ œÄ) :
  hœÑ.measurable_space ‚â§ hœÄ.measurable_space :=
begin
  intros s hs i,
  rw (_ : s ‚à© {x | œÄ x ‚â§ i} = s ‚à© {x | œÑ x ‚â§ i} ‚à© {x | œÄ x ‚â§ i}),
  { exact (hs i).inter (hœÄ i) },
  { ext,
    simp only [set.mem_inter_eq, iff_self_and, and.congr_left_iff, set.mem_set_of_eq],
    intros hle' _,
    exact le_trans (hle _) hle' },
end

lemma measurable_space_le [encodable Œπ] {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) :
  hœÑ.measurable_space ‚â§ m :=
begin
  intros s hs,
  change ‚àÄ i, measurable_set[f i] (s ‚à© {x | œÑ x ‚â§ i}) at hs,
  rw (_ : s = ‚ãÉ i, s ‚à© {x | œÑ x ‚â§ i}),
  { exact measurable_set.Union (Œª i, f.le i _ (hs i)) },
  { ext x, split; rw set.mem_Union,
    { exact Œª hx, ‚ü®œÑ x, hx, le_rfl‚ü© },
    { rintro ‚ü®_, hx, _‚ü©,
      exact hx } }
end

section nat

lemma measurable_set_eq_const {f : filtration ‚Ñï m}
  {œÑ : Œ± ‚Üí ‚Ñï} (hœÑ : is_stopping_time f œÑ) (i : ‚Ñï) :
  measurable_set[hœÑ.measurable_space] {x | œÑ x = i} :=
begin
  rw hœÑ.measurable_set,
  intro j,
  by_cases i ‚â§ j,
  { rw (_ : {x | œÑ x = i} ‚à© {x | œÑ x ‚â§ j} = {x | œÑ x = i}),
    { exact hœÑ.measurable_set_eq_le h },
    { ext,
      simp only [set.mem_inter_eq, and_iff_left_iff_imp, set.mem_set_of_eq],
      rintro rfl,
      assumption } },
  { rw (_ : {x | œÑ x = i} ‚à© {x | œÑ x ‚â§ j} = ‚àÖ),
    { exact @measurable_set.empty _ (f j) },
    { ext,
      simp only [set.mem_empty_eq, set.mem_inter_eq, not_and, not_le, set.mem_set_of_eq, iff_false],
      rintro rfl,
      rwa not_le at h } }
end

end nat

end preorder

section linear_order

variable [linear_order Œπ]

lemma measurable [topological_space Œπ] [measurable_space Œπ]
  [borel_space Œπ] [order_topology Œπ] [second_countable_topology Œπ]
  {f : filtration Œπ m} {œÑ : Œ± ‚Üí Œπ} (hœÑ : is_stopping_time f œÑ) :
  measurable[hœÑ.measurable_space] œÑ :=
begin
  refine @measurable_of_Iic Œπ Œ± _ _ _ hœÑ.measurable_space _ _ _ _ _,
  simp_rw [hœÑ.measurable_set, set.preimage, set.mem_Iic],
  intros i j,
  rw (_ : {x | œÑ x ‚â§ i} ‚à© {x | œÑ x ‚â§ j} = {x | œÑ x ‚â§ min i j}),
  { exact f.mono (min_le_right i j) _ (hœÑ (min i j)) },
  { ext,
    simp only [set.mem_inter_eq, iff_self, le_min_iff, set.mem_set_of_eq] }
end

end linear_order

end is_stopping_time

section linear_order

/-- Given a map `u : Œπ ‚Üí Œ± ‚Üí E`, its stopped value with respect to the stopping
time `œÑ` is the map `x ‚Ü¶ u (œÑ x) x`. -/
def stopped_value (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (œÑ : Œ± ‚Üí Œπ) : Œ± ‚Üí Œ≤ :=
Œª x, u (œÑ x) x

variable [linear_order Œπ]

/-- Given a map `u : Œπ ‚Üí Œ± ‚Üí E`, the stopped process with respect to `œÑ` is `u i x` if
`i ‚â§ œÑ x`, and `u (œÑ x) x` otherwise.

Intuitively, the stopped process stops evolving once the stopping time has occured. -/
def stopped_process (u : Œπ ‚Üí Œ± ‚Üí Œ≤) (œÑ : Œ± ‚Üí Œπ) : Œπ ‚Üí Œ± ‚Üí Œ≤ :=
Œª i x, u (min i (œÑ x)) x

lemma stopped_process_eq_of_le {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ}
  {i : Œπ} {x : Œ±} (h : i ‚â§ œÑ x) : stopped_process u œÑ i x = u i x :=
by simp [stopped_process, min_eq_left h]

lemma stopped_process_eq_of_ge {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ}
  {i : Œπ} {x : Œ±} (h : œÑ x ‚â§ i) : stopped_process u œÑ i x = u (œÑ x) x :=
by simp [stopped_process, min_eq_right h]

section prog_measurable

variables [measurable_space Œπ] [topological_space Œπ] [order_topology Œπ]
  [second_countable_topology Œπ] [borel_space Œπ]
  [measurable_space Œ≤]
  {u : Œπ ‚Üí Œ± ‚Üí Œ≤} {œÑ : Œ± ‚Üí Œπ} {f : filtration Œπ m}

lemma prog_measurable_min_stopping_time (hœÑ : is_stopping_time f œÑ) :
  prog_measurable f (Œª i x, min i (œÑ x)) :=
begin
  intro i,
  let m_prod : measurable_space (set.Iic i √ó Œ±) := prod.measurable_space' _ (f i),
  let m_set : ‚àÄ t : set (set.Iic i √ó Œ±), measurable_space t :=
    Œª _, @subtype.measurable_space (set.Iic i √ó Œ±) _ m_prod,
  let s := {p : set.Iic i √ó Œ± | œÑ p.2 ‚â§ i},
  have hs : measurable_set[m_prod] s, from @measurable_snd (set.Iic i) Œ± _ (f i) _ (hœÑ i),
  have h_meas_fst : ‚àÄ t : set (set.Iic i √ó Œ±),
      measurable[m_set t] (Œª x : t, ((x : set.Iic i √ó Œ±).fst : Œπ)),
    from Œª t, (@measurable_subtype_coe (set.Iic i √ó Œ±) m_prod _).fst.subtype_coe,
  refine measurable_of_restrict_of_restrict_compl hs _ _,
  { refine @measurable.min _ _ _ _ _ (m_set s) _ _ _ _ _ (h_meas_fst s) _,
    refine @measurable_of_Iic Œπ s _ _ _ (m_set s) _ _ _ _ (Œª j, _),
    have h_set_eq : (Œª x : s, œÑ (x : set.Iic i √ó Œ±).snd) ‚Åª¬π' set.Iic j
      = (Œª x : s, (x : set.Iic i √ó Œ±).snd) ‚Åª¬π' {x | œÑ x ‚â§ min i j},
    { ext1 x,
      simp only [set.mem_preimage, set.mem_Iic, iff_and_self, le_min_iff, set.mem_set_of_eq],
      exact Œª _, x.prop, },
    rw h_set_eq,
    suffices h_meas : @measurable _ _ (m_set s) (f i) (Œª x : s, (x : set.Iic i √ó Œ±).snd),
      from h_meas (f.mono (min_le_left _ _) _ (hœÑ.measurable_set_le (min i j))),
    exact (@measurable_snd _ _ _ (f i)).comp (@measurable_subtype_coe _ m_prod _), },
  { suffices h_min_eq_left : (Œª x : s·∂ú, min ‚Üë((x : set.Iic i √ó Œ±).fst) (œÑ (x : set.Iic i √ó Œ±).snd))
      = Œª x : s·∂ú, ‚Üë((x : set.Iic i √ó Œ±).fst),
    { rw [set.restrict, h_min_eq_left],
      exact h_meas_fst _, },
    ext1 x,
    rw min_eq_left,
    have hx_fst_le : ‚Üë(x : set.Iic i √ó Œ±).fst ‚â§ i, from (x : set.Iic i √ó Œ±).fst.prop,
    refine hx_fst_le.trans (le_of_lt _),
    convert x.prop,
    simp only [not_le, set.mem_compl_eq, set.mem_set_of_eq], },
end

lemma prog_measurable.stopped_process (h : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) :
  prog_measurable f (stopped_process u œÑ) :=
h.comp (prog_measurable_min_stopping_time hœÑ) (Œª i x, min_le_left _ _)

lemma prog_measurable.adapted_stopped_process
  (h : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) :
  adapted f (stopped_process u œÑ) :=
(h.stopped_process hœÑ).adapted

lemma prog_measurable.measurable_stopped_process
  (hu : prog_measurable f u) (hœÑ : is_stopping_time f œÑ) (i : Œπ) :
  measurable (stopped_process u œÑ i) :=
(hu.adapted_stopped_process hœÑ i).le (f.le _)

end prog_measurable

end linear_order

section nat
/-! ### Filtrations indexed by `‚Ñï` -/

open filtration

variables {f : filtration ‚Ñï m} {u : ‚Ñï ‚Üí Œ± ‚Üí Œ≤} {œÑ œÄ : Œ± ‚Üí ‚Ñï}

lemma stopped_value_sub_eq_sum [add_comm_group Œ≤] (hle : œÑ ‚â§ œÄ) :
  stopped_value u œÄ - stopped_value u œÑ =
  Œª x, (‚àë i in finset.Ico (œÑ x) (œÄ x), (u (i + 1) - u i)) x :=
begin
  ext x,
  rw [finset.sum_Ico_eq_sub _ (hle x), finset.sum_range_sub, finset.sum_range_sub],
  simp [stopped_value],
end

lemma stopped_value_sub_eq_sum' [add_comm_group Œ≤] (hle : œÑ ‚â§ œÄ) {N : ‚Ñï} (hbdd : ‚àÄ x, œÄ x ‚â§ N) :
  stopped_value u œÄ - stopped_value u œÑ =
  Œª x, (‚àë i in finset.range (N + 1),
    set.indicator {x | œÑ x ‚â§ i ‚àß i < œÄ x} (u (i + 1) - u i)) x :=
begin
  rw stopped_value_sub_eq_sum hle,
  ext x,
  simp only [finset.sum_apply, finset.sum_indicator_eq_sum_filter],
  refine finset.sum_congr _ (Œª _ _, rfl),
  ext i,
  simp only [finset.mem_filter, set.mem_set_of_eq, finset.mem_range, finset.mem_Ico],
  exact ‚ü®Œª h, ‚ü®lt_trans h.2 (nat.lt_succ_iff.2 $ hbdd _), h‚ü©, Œª h, h.2‚ü©
end

section add_comm_monoid

variables [add_comm_monoid Œ≤]

/-- For filtrations indexed by `‚Ñï`, `adapted` and `prog_measurable` are equivalent. This lemma
provides `adapted f u ‚Üí prog_measurable f u`. See `prog_measurable.adapted` for the reverse
direction, which is true more generally. -/
lemma adapted.prog_measurable_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (h : adapted f u) : prog_measurable f u :=
begin
  intro i,
  have : (Œª p : ‚Ü•(set.Iic i) √ó Œ±, u ‚Üë(p.fst) p.snd)
    = Œª p : ‚Ü•(set.Iic i) √ó Œ±, ‚àë j in finset.range (i + 1), if ‚Üëp.fst = j then u j p.snd else 0,
  { ext1 p,
    rw finset.sum_ite_eq,
    have hp_mem : (p.fst : ‚Ñï) ‚àà finset.range (i + 1) := finset.mem_range_succ_iff.mpr p.fst.prop,
    simp only [hp_mem, if_true], },
  rw this,
  refine finset.measurable_sum _ (Œª j hj, measurable.ite _ _ _),
  { suffices h_meas : measurable[prod.measurable_space' _ (f i)]
        (Œª a : ‚Ü•(set.Iic i) √ó Œ±, (a.fst : ‚Ñï)),
      from h_meas (measurable_set_singleton j),
    exact (@measurable_fst _ Œ± _ (f i)).subtype_coe, },
  { have h_le : j ‚â§ i, from finset.mem_range_succ_iff.mp hj,
    exact (measurable.le (f.mono h_le) (h j)).comp (@measurable_snd _ Œ± _ (f i)), },
  { exact @measurable_const _ (set.Iic i √ó Œ±) _ (prod.measurable_space' _ (f i)) _, },
end

/-- For filtrations indexed by `‚Ñï`, the stopped process obtained from an adapted process is
adapted. -/
lemma adapted.stopped_process_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (hu : adapted f u) (hœÑ : is_stopping_time f œÑ) :
  adapted f (stopped_process u œÑ) :=
(hu.prog_measurable_of_nat.stopped_process hœÑ).adapted

lemma adapted.measurable_stopped_process_of_nat [measurable_space Œ≤] [has_measurable_add‚ÇÇ Œ≤]
  (hœÑ : is_stopping_time f œÑ) (hu : adapted f u) (n : ‚Ñï) :
  measurable (stopped_process u œÑ n) :=
hu.prog_measurable_of_nat.measurable_stopped_process hœÑ n

lemma stopped_value_eq {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  stopped_value u œÑ =
  Œª x, (‚àë i in finset.range (N + 1), set.indicator {x | œÑ x = i} (u i)) x :=
begin
  ext y,
  rw [stopped_value, finset.sum_apply, finset.sum_eq_single (œÑ y)],
  { rw set.indicator_of_mem,
    exact rfl },
  { exact Œª i hi hneq, set.indicator_of_not_mem hneq.symm _ },
  { intro hy,
    rw set.indicator_of_not_mem,
    exact Œª _, hy (finset.mem_range.2 $ lt_of_le_of_lt (hbdd _) (nat.lt_succ_self _)) }
end

lemma stopped_process_eq (n : ‚Ñï) :
  stopped_process u œÑ n =
  set.indicator {a | n ‚â§ œÑ a} (u n) +
    ‚àë i in finset.range n, set.indicator {a | œÑ a = i} (u i) :=
begin
  ext x,
  rw [pi.add_apply, finset.sum_apply],
  cases le_or_lt n (œÑ x),
  { rw [stopped_process_eq_of_le h, set.indicator_of_mem, finset.sum_eq_zero, add_zero],
    { intros m hm,
      rw finset.mem_range at hm,
      exact set.indicator_of_not_mem ((lt_of_lt_of_le hm h).ne.symm) _ },
    { exact h } },
  { rw [stopped_process_eq_of_ge (le_of_lt h), finset.sum_eq_single_of_mem (œÑ x)],
    { rw [set.indicator_of_not_mem, zero_add, set.indicator_of_mem],
      { exact rfl }, -- refl does not work
      { exact not_le.2 h } },
    { rwa [finset.mem_range] },
    { intros b hb hneq,
      rw set.indicator_of_not_mem,
      exact hneq.symm } },
end

end add_comm_monoid

section normed_group

variables [measurable_space Œ≤] [normed_group Œ≤] [has_measurable_add‚ÇÇ Œ≤] [borel_space Œ≤] {p : ‚Ñù‚â•0‚àû}
  {Œº : measure Œ±}

lemma mem_‚Ñíp_stopped_process (hœÑ : is_stopping_time f œÑ) (hu : ‚àÄ n, mem_‚Ñíp (u n) p Œº) (n : ‚Ñï) :
  mem_‚Ñíp (stopped_process u œÑ n) p Œº :=
begin
  rw stopped_process_eq,
  refine mem_‚Ñíp.add _ _,
  { exact mem_‚Ñíp.indicator (f.le n {a : Œ± | n ‚â§ œÑ a} (hœÑ.measurable_set_ge n)) (hu n) },
  { suffices : mem_‚Ñíp (Œª x, ‚àë (i : ‚Ñï) in finset.range n, {a : Œ± | œÑ a = i}.indicator (u i) x) p Œº,
    { convert this, ext1 x, simp only [finset.sum_apply] },
    refine mem_‚Ñíp_finset_sum _ (Œª i hi, mem_‚Ñíp.indicator _ (hu i)),
    exact f.le i {a : Œ± | œÑ a = i} (hœÑ.measurable_set_eq i) },
end

lemma integrable_stopped_process (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, integrable (u n) Œº) (n : ‚Ñï) :
  integrable (stopped_process u œÑ n) Œº :=
by { simp_rw ‚Üê mem_‚Ñíp_one_iff_integrable at hu ‚ä¢, exact mem_‚Ñíp_stopped_process hœÑ hu n, }

lemma mem_‚Ñíp_stopped_value (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, mem_‚Ñíp (u n) p Œº) {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  mem_‚Ñíp (stopped_value u œÑ) p Œº :=
begin
  rw stopped_value_eq hbdd,
  suffices : mem_‚Ñíp (Œª x, ‚àë (i : ‚Ñï) in finset.range (N + 1),
    {a : Œ± | œÑ a = i}.indicator (u i) x) p Œº,
  { convert this, ext1 x, simp only [finset.sum_apply] },
  refine mem_‚Ñíp_finset_sum _ (Œª i hi, mem_‚Ñíp.indicator _ (hu i)),
  exact f.le i {a : Œ± | œÑ a = i} (hœÑ.measurable_set_eq i)
end

lemma integrable_stopped_value (hœÑ : is_stopping_time f œÑ)
  (hu : ‚àÄ n, integrable (u n) Œº) {N : ‚Ñï} (hbdd : ‚àÄ x, œÑ x ‚â§ N) :
  integrable (stopped_value u œÑ) Œº :=
by { simp_rw ‚Üê mem_‚Ñíp_one_iff_integrable at hu ‚ä¢, exact mem_‚Ñíp_stopped_value hœÑ hu hbdd, }

end normed_group

end nat

end measure_theory
