/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/

import number_theory.number_field.embeddings
import measure_theory.group.geometry_of_numbers
import ring_theory.discriminant
import algebra.module.zlattice

/-!
# Canonical embedding of a number field
The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r_‚ÇÇ)` is the ring homomorphism
`K ‚Üí+* ‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that sends `x ‚àà K` to `(œÜ_‚ÇÅ(x),...,œÜ_r‚ÇÅ(x)) √ó (œà_‚ÇÅ(x),..., œà_r‚ÇÇ(x))` where
`œÜ_‚ÇÅ,...,œÜ_r‚ÇÅ` are its real embeddings and `œà_‚ÇÅ,..., œà_r‚ÇÇ` are its complex embeddings (up to
complex conjugation).

## Main Results
* `number_field.canonical_embedding.ring_of_integers.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

## Tags
number field, infinite places
-/

open_locale classical number_field

noncomputable theory

open number_field number_field.infinite_place module fintype finite_dimensional

variables (K : Type*) [field K]

localized "notation `E` :=
  ({w : infinite_place K // is_real w} ‚Üí ‚Ñù) √ó ({w : infinite_place K // is_complex w} ‚Üí ‚ÑÇ)"
  in canonical_embedding

lemma number_field.canonical_embedding.rank [number_field K] :
  finrank ‚Ñù E = finrank ‚Ñö K :=
begin
  haveI : module.free ‚Ñù ‚ÑÇ := infer_instance,
  rw module.free.finrank_prod,
  rw module.free.finrank_pi,
  rw module.free.finrank_pi_fintype,
  rw complex.finrank_real_complex,
  rw finset.sum_const,
  rw finset.card_univ,
  rw ‚Üê card_real_embeddings,
  rw algebra.id.smul_eq_mul,
  rw mul_comm,
  rw ‚Üê card_complex_embeddings,
  rw ‚Üê number_field.embeddings.card K ‚ÑÇ,
  rw fintype.card_subtype_compl,
  rw nat.add_sub_of_le (fintype.card_subtype_le _),
end

lemma number_field.canonical_embedding.nontrivial [number_field K] : nontrivial E :=
begin
  obtain ‚ü®w‚ü© := infinite_place.nonempty K,
  by_cases hw : is_real w,
  { convert nontrivial_prod_left,
    { convert @function.nontrivial _ _ _ real.nontrivial,
      use ‚ü®w, hw‚ü©, },
    exact nonempty_of_inhabited, },
 { convert nontrivial_prod_right,
   {  exact nonempty_of_inhabited, },
   {  convert @function.nontrivial _ _ _ complex.nontrivial,
      use ‚ü®w, not_is_real_iff_is_complex.mp hw‚ü©, }},
end

/-- The canonical embedding of a number field of signature `(s,t)` into `‚Ñù^s √ó ‚ÑÇ^t`. -/
def number_field.canonical_embedding : K ‚Üí+* E :=
ring_hom.prod
  (pi.ring_hom (Œª w, w.prop.embedding))
  (pi.ring_hom (Œª w, w.val.embedding))

lemma number_field.injective_canonical_embedding [number_field K] :
  function.injective (number_field.canonical_embedding K) :=
begin
  convert ring_hom.injective _,
  exact (number_field.canonical_embedding.nontrivial K),
end

namespace number_field.canonical_embedding

open number_field number_field.canonical_embedding number_field.infinite_place finite_dimensional
  measure_theory

variable {K}

@[simp]
lemma apply_at_real_infinite_place (w : {w : infinite_place K // is_real w}) (x : K) :
  (number_field.canonical_embedding K x).1 w = w.prop.embedding x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

@[simp]
lemma apply_at_complex_infinite_place (w : { w : infinite_place K // is_complex w}) (x : K) :
  (number_field.canonical_embedding K x).2 w = embedding w.val x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

lemma nnnorm_eq [number_field K] (x : K) :
  ‚Äñcanonical_embedding K x‚Äñ‚Çä = finset.univ.sup (Œª w : infinite_place K, ‚ü®w x, map_nonneg w x‚ü©) :=
begin
  rw [prod.nnnorm_def', pi.nnnorm_def, pi.nnnorm_def],
  rw ( _ : finset.univ = {w : infinite_place K | is_real w}.to_finset
    ‚à™ {w : infinite_place K | is_complex w}.to_finset),
  { rw [finset.sup_union, sup_eq_max],
    refine congr_arg2 _ _ _,
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K, is_real w))
        (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simpa only [apply_at_real_infinite_place, coe_nnnorm, real.norm_eq_abs,
        function.embedding.coe_subtype, subtype.coe_mk]
      using is_real.place_embedding_apply w.prop x, },
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K,
        is_complex w)) (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_complex_infinite_place, subtype.val_eq_coe, coe_nnnorm,
        complex.norm_eq_abs, function.embedding.coe_subtype, subtype.coe_mk, abs_embedding], }},
  { ext w,
    simp only [em (is_real w), set.mem_set_of_eq, finset.mem_union, set.mem_to_finset,
      finset.mem_univ, ‚Üêinfinite_place.not_is_real_iff_is_complex], },
end

lemma le_of_le [number_field K] (x : K) (r : ‚Ñù) :
  ‚Äñ(canonical_embedding K) x‚Äñ ‚â§ r ‚Üî ‚àÄ w : infinite_place K, w x ‚â§ r :=
begin
  obtain hr | hr := lt_or_le r 0,
  { split,
    { intro h,
      exfalso,
      exact (not_le.mpr (lt_of_le_of_lt h hr)) (norm_nonneg _), },
    { intro h,
      exfalso,
      obtain ‚ü®w‚ü© := infinite_place.nonempty K,
      exact (not_le.mpr (lt_of_le_of_lt (h w) hr)) (map_nonneg w _), }},
  { lift r to nnreal using hr,
    simp_rw [‚Üê coe_nnnorm, nnnorm_eq, nnreal.coe_le_coe, finset.sup_le_iff, finset.mem_univ,
      forall_true_left],
    split; { exact Œª h w, h w, }},
end

variable (K)

/-- The image of the ring of integers of `K` as a subring. -/
def integer_lattice : subring E :=
subring.map (canonical_embedding K) (ùìû K).to_subring

/-- The ring equiv between the ring of integers of `K` and the integer lattice. -/
def integer_linear_equiv [number_field K]: (ùìû K) ‚âÉ‚Çó[‚Ñ§] (integer_lattice K) :=
begin
  refine linear_equiv.of_bijective _ _,
  { refine linear_map.mk _ _ _,
    exact Œª x, ‚ü®canonical_embedding K x, x, subtype.mem x, rfl‚ü©,
    { intros _ _,
      simpa only [(canonical_embedding K).map_add, add_mem_class.coe_add], },
    { intros _ _,
      simpa only [zsmul_eq_mul, mul_mem_class.coe_mul, subring_class.coe_int_cast, map_mul,
        map_int_cast], }},
  { split,
    { intros x y hxy,
      rw ‚Üê subtype.coe_inj,
      apply injective_canonical_embedding K,
      rw linear_map.coe_mk at hxy,
      rwa subtype.mk_eq_mk at hxy, },
    { rintros ‚ü®_, ‚ü®a, ‚ü®ha, rfl‚ü©‚ü©‚ü©,
      use a,
      exact ha,
      refl, }},
end

lemma integer_lattice_discrete [number_field K] (r : ‚Ñù) :
  ((integer_lattice K : set E) ‚à© (metric.closed_ball 0 r)).finite :=
begin
  obtain hr | hr := lt_or_le r 0,
  { convert set.finite_empty,
    rw metric.closed_ball_eq_empty.mpr hr,
    exact set.inter_empty _, },
  { have heq : ‚àÄ x : K, canonical_embedding K x ‚àà (metric.closed_ball (0 : E) r) ‚Üî
      ‚àÄ (œÜ : K ‚Üí+* ‚ÑÇ), ‚ÄñœÜ x‚Äñ ‚â§ r,
    { simp_rw [‚Üê place_apply, ‚Üê infinite_place.coe_mk, mem_closed_ball_zero_iff,
        le_of_le],
      exact Œª x, le_iff_le x r, },
    convert set.finite.image (canonical_embedding K) (embeddings.finite_of_norm_le K ‚ÑÇ r),
    ext, split,
    { rintros ‚ü®‚ü®x, ‚ü®hx1, rfl‚ü©‚ü©, hx2‚ü©,
      exact ‚ü®x, ‚ü®‚ü®hx1, (heq x).mp hx2‚ü©, rfl‚ü©‚ü©, },
    { rintros ‚ü®x, ‚ü®‚ü® hx1, hx2‚ü©, rfl‚ü©‚ü©,
      exact ‚ü®‚ü®x, ‚ü®hx1, rfl‚ü©‚ü©, (heq x).mpr hx2‚ü©, }},
end

lemma integer_lattice.countable [number_field K] : countable (integer_lattice K) :=
begin
  suffices : (‚ãÉ n : ‚Ñï, ((integer_lattice K : set E) ‚à© (metric.closed_ball 0 n))).countable,
  { refine set.countable.to_subtype (set.countable.mono _ this),
    rintros _ ‚ü®x, ‚ü®hx, rfl‚ü©‚ü©,
    rw set.mem_Union,
    use nat.ceil (‚Äñcanonical_embedding K x‚Äñ),
    exact ‚ü®‚ü®x, hx, rfl‚ü©, mem_closed_ball_zero_iff.mpr (nat.le_ceil _)‚ü©, },
  { exact set.countable_Union (Œª n, (integer_lattice_discrete K n).countable), },
end

section basis

open_locale complex_conjugate

variable (K)

def _root_.number_field.embedding_embedding : K ‚Üí+* (K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ :=
{ to_fun := Œª x œÜ, œÜ x,
  map_zero' :=
  begin
    funext œÜ,
    exact map_zero œÜ,
  end,
  map_one' :=
  begin
    funext œÜ,
    exact map_one œÜ,
  end,
  map_add' :=
  begin
    intros x y,
    funext œÜ,
    exact map_add œÜ x y,
  end,
  map_mul' :=
  begin
    intros x y,
    funext œÜ,
    exact map_mul œÜ x y,
  end }

def comm_map : E ‚Üí‚Çó[‚Ñù] ((K ‚Üí+* ‚ÑÇ) ‚Üí ‚ÑÇ) :=
{ to_fun :=
  begin
  rintro ‚ü®xr, xc‚ü©,
  intro œÜ,
  by_cases h : complex_embedding.is_real œÜ,
  { exact xr (mk_real K ‚ü®œÜ, h‚ü©), },
  { let w := mk_complex K ‚ü®œÜ, h‚ü©,
    exact ite (w.1.embedding = œÜ) (xc w) (conj (xc w)), }
  end,
  map_add' :=
  begin
    rintros ‚ü®_, _‚ü© ‚ü®_, _‚ü©,
    ext1 œÜ,
    by_cases h : complex_embedding.is_real œÜ,
    { simp only [dif_pos h, prod.mk_add_mk, pi.add_apply, complex.of_real_add], },
    { simp only [dif_neg h, subtype.val_eq_coe, mk_complex_coe, subtype.coe_mk, prod.mk_add_mk,
        pi.add_apply, map_add],
      split_ifs;
      simp only [eq_self_iff_true], },
  end,
  map_smul' :=
  begin
    rintros _ ‚ü®_, _‚ü©,
    ext1 œÜ,
    by_cases h : complex_embedding.is_real œÜ,
    { simp only [dif_pos h, prod.smul_mk, pi.smul_apply, algebra.id.smul_eq_mul,
        complex.of_real_mul, ring_hom.id_apply, is_R_or_C.of_real_smul], },
    { simp only [dif_neg h, prod.smul_mk, pi.smul_apply, complex.real_smul, map_mul,
        is_R_or_C.conj_of_real, ring_hom.id_apply, mul_ite], }
  end }

lemma commutes (x : K) :
  number_field.embedding_embedding K x = comm_map K (canonical_embedding K x) :=
begin
  ext1 œÜ,
  simp only [canonical_embedding, _root_.number_field.embedding_embedding, comm_map,
    subtype.val_eq_coe, ring_hom.coe_mk, pi.ring_hom_apply, ring_hom.prod_apply, linear_map.coe_mk],
  by_cases h : complex_embedding.is_real œÜ,
  { simp only [dif_pos h],
    rw ‚Üê complex_embedding.is_real.coe_embedding_apply h x,
    congr,
    simp only [h, mk_real_coe, subtype.coe_mk, complex_embeddings.is_real.embedding_mk], },
  { simp only [dif_neg h],
    split_ifs with h1,
    { exact congr_fun (congr_arg coe_fn h1.symm) x, },
    { rw ((or_iff_right h1).mp (mk_complex_embedding K ‚ü®œÜ, h‚ü©)),
      simp only [complex_embedding.conjugate_coe_eq, star_ring_end_self_apply, subtype.coe_mk], }}
end

def lattice_basis [number_field K] : basis (free.choose_basis_index ‚Ñ§ (ùìû K)) ‚Ñù E :=
begin
  let b := integral_basis K,
  suffices : linear_independent ‚Ñù (Œª i, canonical_embedding K (b i )),
  { have t1 : ‚ä§ ‚â§ submodule.span ‚Ñù (set.range (canonical_embedding K ‚àò b)),
    { rw linear_independent_iff_card_le_finrank_span at this,
      rw ‚Üê free.finrank_eq_card_choose_basis_index at this,
      rw rank_eq_rank at this,
      rw ‚Üê number_field.canonical_embedding.rank at this,
      have t10 : finrank ‚Ñù E = finrank ‚Ñù (‚ä§ : submodule ‚Ñù E) := finrank_top.symm,
      rw t10 at this,
      exact le_of_eq (eq_of_le_of_finrank_le le_top this).symm, },
    refine basis.mk this t1, },
  let h : (K ‚Üí+* ‚ÑÇ) ‚âÉ free.choose_basis_index ‚Ñ§ (ùìû K),
  { refine equiv_of_card_eq _,
    rw ‚Üê finrank_eq_card_basis b,
    exact embeddings.card K ‚ÑÇ, },
  let eb : (K ‚Üí+* ‚ÑÇ) ‚Üí E := Œª i, canonical_embedding K (b (h i)),
  suffices : linear_independent ‚Ñù eb,
  { convert linear_independent.comp this h.symm (equiv.symm h).injective,
    ext1,
    simp only [eb, function.comp_app, equiv.apply_symm_apply], },
  suffices : linear_independent ‚Ñù ((comm_map K) ‚àò eb) ,
  { exact linear_independent.of_comp _ this, },
  let fb := Œª i, number_field.embedding_embedding K (b (h i)),
  have : (comm_map K) ‚àò eb = fb,
  { ext1 i,
    dsimp only [eb , fb],
    rw commutes _, },
  rw this,
  let B := pi.basis_fun ‚ÑÇ (K ‚Üí+* ‚ÑÇ),
  let M := B.to_matrix fb,
  let N := algebra.embeddings_matrix_reindex ‚Ñö ‚ÑÇ (Œª i, b (h i)) ring_hom.equiv_rat_alg_hom,
  have t0 : M = N.transpose,
  { ext1 œÜ j,
    dsimp only [B, M, N, fb, number_field.embedding_embedding],
    rw basis.to_matrix_apply _ _ œÜ j,
    rw pi.basis_fun_repr,
    dsimp [algebra.embeddings_matrix_reindex],
    refl, },
  have t1 := algebra.discr_not_zero_of_basis ‚Ñö b,
  have t2 := algebra.discr_eq_det_embeddings_matrix_reindex_pow_two ‚Ñö ‚ÑÇ (Œª i, b (h i))
    ring_hom.equiv_rat_alg_hom,
  have t3 : N.det ‚â† 0,
  { contrapose! t1,
    dsimp [N] at t1,
    rw t1 at t2,
    rw zero_pow (by norm_num : 0 < 2) at t2,
    rw map_eq_zero_iff _ (algebra_map ‚Ñö ‚ÑÇ).injective at t2,
    rw ‚Üê algebra.discr_reindex ‚Ñö b h.symm,
    convert t2,
    exact equiv.symm_symm h, },
  have t4 : M.det ‚â† 0,
  { have t40 := congr_arg matrix.det t0,
    rw t40,
    rwa matrix.det_transpose, },
  have t5 : is_unit(B.det fb),
  { rw basis.det_apply,
    rw is_unit_iff_ne_zero,
    exact t4, },
  rw ‚Üê is_basis_iff_det at t5,
  refine t5.1.restrict_scalars (smul_left_injective _ (by norm_num)),
end

lemma lattice_basis_apply [number_field K] (i : free.choose_basis_index ‚Ñ§ (ùìû K)) :
  (lattice_basis K) i = (canonical_embedding K) (integral_basis K i) :=
by simp only [lattice_basis, basis.coe_mk]

lemma integral_basis_span [number_field K] :
  (integer_lattice K : set E) = submodule.span ‚Ñ§ (set.range (lattice_basis K)) :=
begin
  have t1 : (canonical_embedding K).to_int_alg_hom.to_linear_map ''
    ((algebra_map (ùìû K) K).to_int_alg_hom.to_linear_map '' (set.range (ring_of_integers.basis K))) =
    set.range (lattice_basis K),
  { change (canonical_embedding K) '' ((algebra_map (ùìû K) K) ''
      (set.range (ring_of_integers.basis K))) = set.range (lattice_basis K),
    suffices : ‚àÄ i, (canonical_embedding K) ((algebra_map (ùìû K) K) (ring_of_integers.basis K i)) =
      (lattice_basis K) i,
    { rw ‚Üê set.range_comp,
      rw ‚Üê set.range_comp,
      refine congr_arg set.range _,
      funext i,
      exact this i, },
    intro i,
    rw lattice_basis_apply K i,
    rw integral_basis_apply K i, },
  have t2 := congr_arg (Œª s, submodule.span ‚Ñ§ s) t1,
  dsimp at t2,
  rw ‚Üê submodule.map_span at t2,
  rw ‚Üê submodule.map_span at t2,
  rw (ring_of_integers.basis K).span_eq at t2,
  rw ‚Üê t2,
  ext, split,
  { rintros ‚ü®a, ‚ü®ha, rfl‚ü©‚ü©,
    use a,
    split,
    { use a,
      exact ha,
      split,
      { trivial, },
      { refl, }},
    { refl, }},
  { rintros ‚ü®_, ‚ü®‚ü®b, ‚ü®_, rfl‚ü©‚ü©, rfl‚ü©‚ü©,
    use b,
    split,
    { exact subtype.mem b, },
    { refl, }},
end

end basis

variable [number_field K]

/-- The complex Haar measure giving measure 1 to the unit box with ‚ÑÇ ‚âÉ ‚Ñù √ó ‚Ñù -/
@[reducible]
def unit_measure : measure E :=
measure.prod (measure.pi (Œª _, volume)) (measure.pi (Œª _, complex.basis_one_I.add_haar))

instance : sigma_finite complex.basis_one_I.add_haar := infer_instance
instance : sigma_finite
  (measure.pi (Œª w : { w : infinite_place K // is_complex w}, complex.basis_one_I.add_haar)) :=
  infer_instance

instance : measure.is_add_haar_measure (unit_measure K) :=
begin
  haveI : measure.is_add_haar_measure complex.basis_one_I.add_haar := infer_instance,
  haveI : has_measurable_add ‚ÑÇ := infer_instance,
  have : measure.is_add_haar_measure (measure.pi (Œª w : { w : infinite_place K // is_complex w },
    complex.basis_one_I.add_haar)) := @measure.pi.is_add_haar_measure _ _ _ _ _ _ _ _ _ _,
  convert measure.prod.is_add_haar_measure _ _,
  any_goals { apply_instance, },
end

def convex_body_real (f : infinite_place K ‚Üí nnreal) : set ({w : infinite_place K // is_real w} ‚Üí ‚Ñù)
:= set.pi set.univ (Œª w, metric.ball 0 (f w))

def convex_body_complex (f : infinite_place K ‚Üí nnreal) :
  set ({w : infinite_place K // is_complex w} ‚Üí ‚ÑÇ) :=
set.pi set.univ (Œª w, metric.ball 0 (f w))

@[reducible]
def convex_body (f : infinite_place K ‚Üí nnreal): set E :=
(convex_body_real K f) √óÀ¢ (convex_body_complex K f)

lemma convex_body.symmetric (f : infinite_place K ‚Üí nnreal) :
  ‚àÄ x : E, x ‚àà (convex_body K f) ‚Üí -x ‚àà (convex_body K f) :=
begin
  intros x hx,
  refine set.mem_prod.1 ‚ü®_, _‚ü©,
  { intros w _,
    simpa only [prod.fst_neg, pi.neg_apply, mem_ball_zero_iff, real.norm_eq_abs, abs_neg]
      using mem_ball_zero_iff.1 (hx.1 w (set.mem_univ _)), },
  { intros w _,
    simpa only [prod.snd_neg, pi.neg_apply, mem_ball_zero_iff, complex.norm_eq_abs,
      absolute_value.map_neg] using mem_ball_zero_iff.mp (hx.right w (set.mem_univ w)), }
end

lemma convex_body.convex (f : infinite_place K ‚Üí nnreal) :
  convex ‚Ñù (convex_body K f) :=
begin
  refine convex.prod _ _;
  exact convex_pi (Œª i _, (convex_ball 0 (f i))),
end

lemma convex_body_real.volume (f : infinite_place K ‚Üí nnreal) :
  measure.pi (Œª _, volume) (convex_body_real K f) =
    2 ^ card {w : infinite_place K // is_real w} *
    finset.univ.prod (Œª w : {w : infinite_place K // is_real w}, f w) :=
begin
  rw convex_body_real,
  rw measure.pi_pi,
  simp_rw real.volume_ball,
  simp_rw ennreal.of_real_mul (by norm_num : 0 ‚â§ (2 : ‚Ñù)),
  simp only [ennreal.of_real_bit0, ennreal.of_real_one, ennreal.of_real_coe_nnreal],
  rw finset.prod_mul_distrib,
  rw finset.prod_const,
  rw finset.card_univ,
end

lemma convex_body_complex.volume (f : infinite_place K ‚Üí nnreal) :
  (measure.pi (Œª _, complex.basis_one_I.add_haar)) (convex_body_complex K f) =
  (complex.basis_one_I.add_haar) (metric.ball 0 1) ^
  card {w : infinite_place K // is_complex w} *
  finset.univ.prod (Œª w : {w : infinite_place K // is_complex w}, (f w) ^ 2) :=
begin
  haveI : measure.is_add_haar_measure complex.basis_one_I.add_haar := infer_instance,
  haveI : has_measurable_add ‚ÑÇ := infer_instance,
  haveI : measure.is_add_haar_measure (measure.pi (Œª w : { w : infinite_place K // is_complex w },
    complex.basis_one_I.add_haar)) := @measure.pi.is_add_haar_measure _ _ _ _ _ _ _ _ _ _,
  rw convex_body_complex,
  rw measure.pi_pi,
  conv { to_lhs, congr, skip, funext,
    rw measure.add_haar_ball complex.basis_one_I.add_haar 0 (f i).prop,
    rw ennreal.of_real_pow (f i).prop, },
  rw finset.prod_mul_distrib,
  rw finset.prod_const,
  rw mul_comm,
  rw complex.finrank_real_complex,
  rw finset.card_univ,
  simp_rw ennreal.of_real_coe_nnreal,
end

lemma convex_body_mem (x : K) (f : infinite_place K ‚Üí nnreal) :
  canonical_embedding K x ‚àà (convex_body K f) ‚Üî ‚àÄ w : infinite_place K, w x < f w :=
begin
  rw set.mem_prod,
  rw convex_body_real,
  rw convex_body_complex,
  rw set.mem_pi,
  rw set.mem_pi,
  simp only [set.mem_univ, mem_ball_zero_iff, forall_true_left, real.norm_eq_abs,
    subtype.forall, subtype.coe_mk, complex.norm_eq_abs],
  simp_rw apply_at_real_infinite_place,
  simp_rw apply_at_complex_infinite_place,
  simp_rw ‚Üê infinite_place.apply,
  simp_rw mk_embedding,
  split,
  { rintros ‚ü®hr, hc‚ü© w,
    by_cases h : is_real w,
    { convert hr w h,
      rw ‚Üê is_real.place_embedding_apply,
      refl, },
    { rw not_is_real_iff_is_complex at h,
      exact hc w h, }},
  { rintro h,
    split,
    { intros w hw,
      convert h w,
      rw ‚Üê is_real.place_embedding_apply,
      refl, },
    { intros w hw,
      exact h w, }}
end

def constant_volume : ennreal := 2 ^ card {w : infinite_place K // is_real w} *
  (complex.basis_one_I.add_haar) (metric.ball 0 1) ^ card {w : infinite_place K // is_complex w}

lemma constant_volume_pos : 0 < (constant_volume K) :=
begin
  refine ennreal.mul_pos _ _,
  { refine ennreal.pow_ne_zero _ _,
    exact ne_zero.ne 2, },
  { refine ennreal.pow_ne_zero _ _,
    refine ne_of_gt _,
    exact metric.measure_ball_pos _ _ (by norm_num), },
end

lemma constant_volume_lt_top : (constant_volume K) < ‚ä§ :=
begin
  refine ennreal.mul_lt_top _ _,
  { refine ne_of_lt _,
    refine ennreal.pow_lt_top _ _,
    exact lt_top_iff_ne_top.mpr ennreal.two_ne_top, },
  { refine ne_of_lt _,
    refine ennreal.pow_lt_top _ _,
    exact measure_ball_lt_top, },
end

lemma convex_body.volume (f : infinite_place K ‚Üí nnreal) :
  (unit_measure K) (convex_body K f) = (constant_volume K) *
    finset.univ.prod (Œª w : infinite_place K, (ite (w.is_real) (f w) (f w ^ 2))) :=
begin
  rw measure.prod_prod _ _,
  { rw convex_body_real.volume,
    rw convex_body_complex.volume,
    rw constant_volume,
    rw finset.prod_ite,
    have : ‚àÄ (w : infinite_place K), w ‚àà finset.filter (Œª w : infinite_place K, w.is_real)
      finset.univ ‚Üî w.is_real,
    { intro _,
      simp only [finset.mem_filter, finset.mem_univ, true_and], },
    rw finset.prod_subtype _ this _,
    have : ‚àÄ (w : infinite_place K), w ‚àà finset.filter (Œª w : infinite_place K, ¬¨ w.is_real)
      finset.univ ‚Üî w.is_complex,
    { intro _,
      simp only [not_is_real_iff_is_complex, finset.mem_filter, finset.mem_univ, true_and], },
    rw finset.prod_subtype _ this _,
    rw ‚Üê mul_assoc,
    nth_rewrite 1 mul_assoc,
    nth_rewrite 2 mul_comm,
    rw ‚Üê mul_assoc,
    rw ‚Üê mul_assoc, },
  { apply_instance, },
end

def minkowski_bound : ennreal := (unit_measure K) (zspan.fundamental_domain (lattice_basis K)) *
  2 ^ (finrank ‚Ñù E)

lemma minkowski_bound_lt_top : minkowski_bound K < ‚ä§ :=
begin
  refine ennreal.mul_lt_top _ _,
  { refine ne_of_lt _,
    refine metric.bounded.measure_lt_top _,
    exact zspan.metric.bounded_fundamental_domain (lattice_basis K), },
  { refine ne_of_lt _,
    refine ennreal.pow_lt_top _ _,
    exact lt_top_iff_ne_top.mpr ennreal.two_ne_top, },
end

lemma exists_ne_zero_mem_ring_of_integers_le {f : (infinite_place K) ‚Üí nnreal}
  (h : minkowski_bound K < (unit_measure K) (convex_body K f)) :
  ‚àÉ (a : ùìû K), a ‚â† 0 ‚àß ‚àÄ w : infinite_place K, w a < f w :=
begin
  have t1 := zspan.is_add_fundamental_domain (lattice_basis K) (unit_measure K),
  haveI : countable (submodule.span ‚Ñ§ (set.range (lattice_basis K))).to_add_subgroup,
    { change countable (submodule.span ‚Ñ§ (set.range (lattice_basis K)) : set E),
      rw ‚Üê integral_basis_span,
      exact integer_lattice.countable K, },
  have := exists_ne_zero_mem_lattice_of_measure_mul_two_pow_finrank_lt_measure
    (unit_measure K) t1 h (convex_body.symmetric K f) (convex_body.convex K f),
  obtain ‚ü®x, hnz, hmem‚ü© := this,
  rsuffices ‚ü®a, ha1, ha2‚ü© : ‚àÉ a : ùìû K, a ‚â† 0 ‚àß canonical_embedding K a = x,
  { rw ‚Üê ha2 at hmem,
    rw convex_body_mem at hmem,
    use a,
    exact ‚ü®ha1, hmem‚ü©, },
  have : (x : E) ‚àà (integer_lattice K),
  { rw ‚Üê set_like.mem_coe,
    rw integral_basis_span,
    have := set_like.coe_mem x,
    rwa ‚Üê set_like.mem_coe at this, },
  obtain ‚ü®z, hz1, hz2‚ü© := this,
  use z,
  exact hz1,
  split,
  { apply subtype.ne_of_val_ne,
    rw [subtype.val_eq_coe],
    rw [subtype.val_eq_coe],
    rw subtype.coe_mk,
    rw [algebra_map.coe_zero],
    rw ‚Üê map_ne_zero_iff _ (injective_canonical_embedding K),
    rw hz2,
    simp only [hnz, ne.def, submodule.coe_eq_zero, not_false_iff], },
  { exact hz2, },
end

end number_field.canonical_embedding
