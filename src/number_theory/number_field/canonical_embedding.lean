/-
Copyright (c) 2022 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import number_theory.number_field.embeddings

/-!
# Canonical embedding of a number field

The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` is the ring homomorphism
`K ‚Üí+* ‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` that sends `x ‚àà K` to `(œÜ_‚ÇÅ(x),...,œÜ_r‚ÇÅ(x)) √ó (œà_‚ÇÅ(x),..., œà_r‚ÇÇ(x))` where
`œÜ_‚ÇÅ,...,œÜ_r‚ÇÅ` are its real embeddings and `œà_‚ÇÅ,..., œà_r‚ÇÇ` are its complex embeddings (up to
complex conjugation).

## Main definitions and results

* `number_field.canonical_embedding.ring_of_integers.inter_ball_finite`: the intersection of the
image of the ring of integers by the canonical embedding and any ball centered at `0` of finite
radius is finite.

## Tags
number field, infinite places
-/

open_locale classical number_field

noncomputable theory

open number_field number_field.infinite_place module fintype finite_dimensional

variables (K : Type*) [field K]

namespace number_field.canonical_embedding

/-- The ambiant space `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ` with `(r‚ÇÅ, r‚ÇÇ)` the signature of `K`. -/
@[reducible]
def space :=
  ({w : infinite_place K // is_real w} ‚Üí ‚Ñù) √ó ({w : infinite_place K // is_complex w} ‚Üí ‚ÑÇ)

instance : comm_ring (space K) := prod.comm_ring

instance : module ‚Ñù (space K) := prod.module

lemma space_rank [number_field K] :
  finrank ‚Ñù (space K) = finrank ‚Ñö K :=
begin
  haveI : module.free ‚Ñù ‚ÑÇ := infer_instance,
  rw [module.free.finrank_prod, module.free.finrank_pi, module.free.finrank_pi_fintype,
    complex.finrank_real_complex, finset.sum_const, finset.card_univ, ‚Üê card_real_embeddings,
    algebra.id.smul_eq_mul, mul_comm, ‚Üê card_complex_embeddings, ‚Üê number_field.embeddings.card K ‚ÑÇ,
    fintype.card_subtype_compl, nat.add_sub_of_le (fintype.card_subtype_le _)],
end

instance [number_field K] : nontrivial (space K) :=
begin
  obtain ‚ü®w‚ü© := infinite_place.nonempty K,
  by_cases hw : is_real w,
  { convert nontrivial_prod_left,
    { convert @function.nontrivial _ _ _ real.nontrivial,
      use ‚ü®w, hw‚ü©, },
    exact nonempty_of_inhabited, },
 { convert nontrivial_prod_right,
   {  exact nonempty_of_inhabited, },
   {  convert @function.nontrivial _ _ _ complex.nontrivial,
      use ‚ü®w, not_is_real_iff_is_complex.mp hw‚ü©, }},
end

/-- The canonical embedding of a number field `K` of signature `(r‚ÇÅ, r‚ÇÇ)` into `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def _root_.number_field.canonical_embedding : K ‚Üí+* space K :=
ring_hom.prod
  (pi.ring_hom (Œª w, w.prop.embedding))
  (pi.ring_hom (Œª w, w.val.embedding))

lemma _root_.number_field.canonical_embedding_injective [number_field K] :
  function.injective (number_field.canonical_embedding K) := ring_hom.injective _

open number_field

variable {K}

@[simp]
lemma apply_at_real_infinite_place (w : {w : infinite_place K // is_real w}) (x : K) :
  (number_field.canonical_embedding K x).1 w = w.prop.embedding x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

@[simp]
lemma apply_at_complex_infinite_place (w : { w : infinite_place K // is_complex w}) (x : K) :
  (number_field.canonical_embedding K x).2 w = embedding w.val x :=
by simp only [canonical_embedding, ring_hom.prod_apply, pi.ring_hom_apply]

lemma nnnorm_eq [number_field K] (x : K) :
  ‚Äñcanonical_embedding K x‚Äñ‚Çä = finset.univ.sup (Œª w : infinite_place K, ‚ü®w x, map_nonneg w x‚ü©) :=
begin
  rw [prod.nnnorm_def', pi.nnnorm_def, pi.nnnorm_def],
  rw ( _ : finset.univ = {w : infinite_place K | is_real w}.to_finset
    ‚à™ {w : infinite_place K | is_complex w}.to_finset),
  { rw [finset.sup_union, sup_eq_max],
    refine congr_arg2 _ _ _,
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K, is_real w))
        (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_real_infinite_place, coe_nnnorm, real.norm_eq_abs,
        function.embedding.coe_subtype, subtype.coe_mk, is_real.abs_embedding_apply], },
    { convert (finset.univ.sup_map (function.embedding.subtype (Œª w : infinite_place K,
        is_complex w)) (Œª w, (‚ü®w x, map_nonneg w x‚ü© : nnreal))).symm using 2,
      ext w,
      simp only [apply_at_complex_infinite_place, subtype.val_eq_coe, coe_nnnorm,
        complex.norm_eq_abs, function.embedding.coe_subtype, subtype.coe_mk, abs_embedding], }},
  { ext w,
    simp only [em (is_real w), set.mem_set_of_eq, finset.mem_union, set.mem_to_finset,
      finset.mem_univ, ‚Üêinfinite_place.not_is_real_iff_is_complex], },
end

lemma le_of_le [number_field K] (x : K) (r : ‚Ñù) :
  ‚Äñcanonical_embedding K x‚Äñ ‚â§ r ‚Üî ‚àÄ w : infinite_place K, w x ‚â§ r :=
begin
  obtain hr | hr := lt_or_le r 0,
  { split,
    { intro h,
      exfalso,
      exact (not_le.mpr (lt_of_le_of_lt h hr)) (norm_nonneg _), },
    { intro h,
      exfalso,
      obtain ‚ü®w‚ü© := infinite_place.nonempty K,
      exact (not_le.mpr (lt_of_le_of_lt (h w) hr)) (map_nonneg w _), }},
  { lift r to nnreal using hr,
    simp_rw [‚Üê coe_nnnorm, nnnorm_eq, nnreal.coe_le_coe, finset.sup_le_iff, finset.mem_univ,
      forall_true_left],
    split; { exact Œª h w, h w, }},
end

variables (K)

/-- The image of `ùìû K` as a subring of `‚Ñù^r‚ÇÅ √ó ‚ÑÇ^r‚ÇÇ`. -/
def integer_lattice : subring (space K) :=
subring.map (canonical_embedding K) (ring_hom.range (algebra_map (ùìû K) K))

/-- The ring equiv between `ùìû K` and the integer lattice. -/
def equiv_integer_lattice [number_field K] :
  ùìû K ‚âÉ‚Çó[‚Ñ§] integer_lattice K :=
begin
  refine linear_equiv.of_bijective _ _,
  { refine linear_map.mk _ _ _,
    exact Œª x, ‚ü®canonical_embedding K (algebra_map (ùìû K) K x), algebra_map (ùìû K) K x,
      by simp only [subring.mem_carrier, ring_hom.mem_range, exists_apply_eq_apply], rfl‚ü©,
    { intros _ _,
      simpa only [map_add], },
    { intros _ _,
      simpa only [zsmul_eq_mul, map_mul, map_int_cast], }},
  { split,
    { intros _ _ h,
      rw [linear_map.coe_mk, subtype.mk_eq_mk] at h,
      exact (is_fraction_ring.injective (ùìû K) K) (canonical_embedding_injective K h), },
    { exact Œª ‚ü®_, ‚ü®_, ‚ü®‚ü®a, rfl‚ü©, rfl‚ü©‚ü©‚ü©, ‚ü®a, rfl‚ü©, }}
end

lemma integer_lattice.inter_ball_finite [number_field K] (r : ‚Ñù) :
  ((integer_lattice K : set (space K)) ‚à© (metric.closed_ball 0 r)).finite :=
begin
  obtain hr | hr := lt_or_le r 0,
  { convert set.finite_empty,
    rw metric.closed_ball_eq_empty.mpr hr,
    exact set.inter_empty _, },
  { have heq : ‚àÄ x : K, canonical_embedding K x ‚àà (metric.closed_ball (0 : (space K)) r) ‚Üî
      ‚àÄ (œÜ : K ‚Üí+* ‚ÑÇ), ‚ÄñœÜ x‚Äñ ‚â§ r,
    { simp_rw [‚Üê place_apply, ‚Üê infinite_place.coe_mk, mem_closed_ball_zero_iff, le_of_le],
      exact Œª x, le_iff_le x r, },
    convert set.finite.image (canonical_embedding K) (embeddings.finite_of_norm_le K ‚ÑÇ r),
    ext, split,
    { rintros ‚ü®‚ü®_, ‚ü®‚ü®x, rfl‚ü©, rfl‚ü©‚ü©, hx2‚ü©,
      exact ‚ü®x, ‚ü®‚ü®set_like.coe_mem x, (heq x).mp hx2‚ü©, rfl‚ü©‚ü©, },
    { rintros ‚ü®x, ‚ü®‚ü® hx1, hx2‚ü©, rfl‚ü©‚ü©,
      exact ‚ü®‚ü®x, ‚ü®‚ü®‚ü®x, hx1‚ü©, rfl‚ü©, rfl‚ü©‚ü©, (heq x).mpr hx2‚ü©, }},
end

instance [number_field K] : countable (integer_lattice K) :=
begin
  suffices : (‚ãÉ n : ‚Ñï, ((integer_lattice K : set (space K)) ‚à© (metric.closed_ball 0 n))).countable,
  { refine set.countable.to_subtype (set.countable.mono _ this),
    rintros _ ‚ü®x, ‚ü®hx, rfl‚ü©‚ü©,
    rw set.mem_Union,
    use nat.ceil (‚Äñcanonical_embedding K x‚Äñ),
    exact ‚ü®‚ü®x, hx, rfl‚ü©, mem_closed_ball_zero_iff.mpr (nat.le_ceil _)‚ü©, },
  { exact set.countable_Union (Œª n, (integer_lattice.inter_ball_finite K n).countable), },
end

end number_field.canonical_embedding
