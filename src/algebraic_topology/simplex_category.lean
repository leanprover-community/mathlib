/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/

import order.category.NonemptyFinLinOrd
import category_theory.skeletal
import data.finset.sort
import tactic.linarith

/-! # The simplex category

We construct a skeletal model of the simplex category, with objects `‚Ñï` and the
morphism `n ‚ü∂ m` being the monotone maps from `fin (n+1)` to `fin (m+1)`.

We show that this category is equivalent to `NonemptyFinLinOrd`.
-/

universe variables u

open category_theory

/-- The simplex category:
* objects are natural numbers `n : ‚Ñï`
* morphisms from `n` to `m` are monotone functions `fin (n+1) ‚Üí fin (m+1)`
-/
@[derive inhabited]
def simplex_category := ‚Ñï

namespace simplex_category

instance : small_category simplex_category :=
{ hom := Œª n m, preorder_hom (fin (n+1)) (fin (m+1)),
  id := Œª m, preorder_hom.id,
  comp := Œª _ _ _ f g, preorder_hom.comp g f, }

@[simp] lemma id_apply {n : simplex_category} (i : fin (n+1)) :
  (ùüô n : fin (n+1) ‚Üí fin (n+1)) i = i := rfl
@[simp] lemma comp_apply {l m n : simplex_category} (f : l ‚ü∂ m) (g : m ‚ü∂ n) (i : fin (l+1)) :
  (f ‚â´ g) i = g (f i) := rfl

/-- Interpet a natural number as an object of the simplex category. -/
@[reducible] def mk (n : ‚Ñï) : simplex_category := n
local notation `[`n`]` := mk n

section generators
/-!
## Generating maps for the simplex category

TODO: prove that the simplex category is equivalent to
one given by the following generators and relations.
-/

/-- The `i`-th face map from `[n]` to `[n+1]` -/
def Œ¥ {n} (i : fin (n+2)) : [n] ‚ü∂ [n+1] :=
(fin.succ_above i).to_preorder_hom

/-- The `i`-th degeneracy map from `[n+1]` to `[n]` -/
def œÉ {n} (i : fin (n+1)) : [n+1] ‚ü∂ [n] :=
{ to_fun := fin.pred_above i,
  monotone' := fin.pred_above_right_monotone i }

/-- The generic case of the first simplicial identity -/
lemma Œ¥_comp_Œ¥ {n} {i j : fin (n+2)} (H : i ‚â§ j) :
  Œ¥ i ‚â´ Œ¥ j.succ = Œ¥ j ‚â´ Œ¥ i.cast_succ :=
begin
  ext k,
  dsimp [Œ¥, fin.succ_above],
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  rcases k with ‚ü®k, _‚ü©,
  split_ifs; { simp at *; linarith },
end

/-- The special case of the first simplicial identity -/
lemma Œ¥_comp_Œ¥_self {n} {i : fin (n+2)} : Œ¥ i ‚â´ Œ¥ i.cast_succ = Œ¥ i ‚â´ Œ¥ i.succ :=
begin
  ext j,
  dsimp [Œ¥, fin.succ_above],
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  split_ifs; { simp at *; linarith },
end

/-- The second simplicial identity -/
lemma Œ¥_comp_œÉ_of_le {n} {i : fin (n+2)} {j : fin (n+1)} (H : i ‚â§ j.cast_succ) :
  Œ¥ i.cast_succ ‚â´ œÉ j.succ = œÉ j ‚â´ Œ¥ i :=
begin
  ext k,
  suffices : ite (j.succ.cast_succ < ite (k < i) k.cast_succ k.succ)
    (ite (k < i) (k:‚Ñï) (k + 1) - 1) (ite (k < i) k (k + 1)) =
      ite ((if h : (j:‚Ñï) < k
        then k.pred (by { rintro rfl, exact nat.not_lt_zero _ h })
        else k.cast_lt (by { cases j, cases k, linarith })).cast_succ < i)
          (ite (j.cast_succ < k) (k - 1) k) (ite (j.cast_succ < k) (k - 1) k + 1),
  { dsimp [Œ¥, œÉ, fin.succ_above, fin.pred_above], simpa with push_cast },
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  rcases k with ‚ü®k, _‚ü©,
  simp only [subtype.mk_le_mk, fin.cast_succ_mk] at H,
  dsimp, simp only [if_congr, subtype.mk_lt_mk, dif_ctx_congr],
  split_ifs,
  -- Most of the goals can now be handled by `linarith`,
  -- but we have to deal with two of them by hand.
  swap 8,
  { exact (nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) ‚Äπ_‚Ä∫)).symm, },
  swap 7,
  { have : k ‚â§ i := nat.le_of_pred_lt ‚Äπ_‚Ä∫, linarith, },
  -- Hope for the best from `linarith`:
  all_goals { try { refl <|> simp at * }; linarith, },
end

/-- The first part of the third simplicial identity -/
lemma Œ¥_comp_œÉ_self {n} {i : fin (n+1)} :
  Œ¥ i.cast_succ ‚â´ œÉ i = ùüô [n] :=
begin
  ext j,
  suffices : ite (fin.cast_succ i < ite (j < i) (fin.cast_succ j) j.succ)
    (ite (j < i) (j:‚Ñï) (j + 1) - 1) (ite (j < i) j (j + 1)) = j,
  { dsimp [Œ¥, œÉ, fin.succ_above, fin.pred_above], simpa with push_cast },
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  dsimp, simp only [if_congr, subtype.mk_lt_mk],
  split_ifs; { simp at *; linarith, },
end

/-- The second part of the third simplicial identity -/
lemma Œ¥_comp_œÉ_succ {n} {i : fin (n+1)} :
  Œ¥ i.succ ‚â´ œÉ i = ùüô [n] :=
begin
  ext j,
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  dsimp [Œ¥, œÉ, fin.succ_above, fin.pred_above],
  simp with push_cast,
  split_ifs; { simp at *; linarith, },
end

/-- The fourth simplicial identity -/
lemma Œ¥_comp_œÉ_of_gt {n} {i : fin (n+2)} {j : fin (n+1)} (H : j.cast_succ < i) :
  Œ¥ i.succ ‚â´ œÉ j.cast_succ = œÉ j ‚â´ Œ¥ i :=
begin
  ext k,
  dsimp [Œ¥, œÉ, fin.succ_above, fin.pred_above],
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  rcases k with ‚ü®k, _‚ü©,
  simp only [subtype.mk_lt_mk, fin.cast_succ_mk] at H,
  suffices : ite (_ < ite (k < i + 1) _ _) _ _ =
    ite _ (ite (j < k) (k - 1) k) (ite (j < k) (k - 1) k + 1),
  { simpa [apply_dite fin.cast_succ] with push_cast, },
  split_ifs,
  -- Most of the goals can now be handled by `linarith`,
  -- but we have to deal with three of them by hand.
  swap 2,
  { simp only [subtype.mk_lt_mk] at h_1,
    simp only [not_lt] at h_2,
    simp only [self_eq_add_right, one_ne_zero],
    exact lt_irrefl (k - 1) (lt_of_lt_of_le
      (nat.pred_lt (ne_of_lt (lt_of_le_of_lt (zero_le _) h_1)).symm)
      (le_trans (nat.le_of_lt_succ h) h_2)) },
  swap 4,
  { simp only [subtype.mk_lt_mk] at h_1,
    simp only [not_lt] at h,
    simp only [nat.add_succ_sub_one, add_zero],
    exfalso,
    exact lt_irrefl _ (lt_of_le_of_lt (nat.le_pred_of_lt (nat.lt_of_succ_le h)) h_3), },
  swap 4,
  { simp only [subtype.mk_lt_mk] at h_1,
    simp only [not_lt] at h_3,
    simp only [nat.add_succ_sub_one, add_zero],
    exact (nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) h_2)).symm, },
  -- Hope for the best from `linarith`:
  all_goals { simp at h_1 h_2 ‚ä¢; linarith, },
end

local attribute [simp] fin.pred_mk

/-- The fifth simplicial identity -/
lemma œÉ_comp_œÉ {n} {i j : fin (n+1)} (H : i ‚â§ j) :
  œÉ i.cast_succ ‚â´ œÉ j = œÉ j.succ ‚â´ œÉ i :=
begin
  ext k,
  dsimp [œÉ, fin.pred_above],
  rcases i with ‚ü®i, _‚ü©,
  rcases j with ‚ü®j, _‚ü©,
  rcases k with ‚ü®k, _‚ü©,
  simp only [subtype.mk_le_mk] at H,
  -- At this point `simp with push_cast` makes good progress, but neither `simp?` nor `squeeze_simp`
  -- return usable sets of lemmas.
  -- To avoid using a non-terminal simp, we make a `suffices` statement indicating the shape
  -- of the goal we're looking for, and then use `simpa with push_cast`.
  -- I'm not sure this is actually much more robust that a non-terminal simp.
  suffices : ite (_ < dite (i < k) _ _) _ _ =
    ite (_ < dite (j + 1 < k) _ _) _ _,
  { simpa with push_cast, },
  split_ifs,
  -- `split_ifs` created 12 goals.
  -- Most of them are dealt with `by simp at *; linarith`,
  -- but we pull out two harder ones to do by hand.
  swap 3,
  { simp only [not_lt] at h_2,
    exact false.elim
    (lt_irrefl (k - 1)
      (lt_of_lt_of_le (nat.pred_lt (id (ne_of_lt (lt_of_le_of_lt (zero_le i) h)).symm))
        (le_trans h_2 (nat.succ_le_of_lt h_1)))) },
  swap 3,
  { simp only [subtype.mk_lt_mk, not_lt] at h_1,
    exact false.elim
    (lt_irrefl j (lt_of_lt_of_le (nat.pred_lt_pred (nat.succ_ne_zero j) h_2) h_1)) },
  -- Deal with the rest automatically.
  all_goals { simp at *; linarith, },
end

end generators

section skeleton


/-- The functor that exhibits `simplex_category` as skeleton
of `NonemptyFinLinOrd` -/
def skeletal_functor : simplex_category ‚•§ NonemptyFinLinOrd.{u} :=
{ obj := Œª n, NonemptyFinLinOrd.of $ ulift (fin (n+1)),
  map := Œª m n f, ‚ü®Œª i, ‚ü®f i.down‚ü©, Œª ‚ü®i‚ü© ‚ü®j‚ü© h, show f i ‚â§ f j, from f.monotone h‚ü©, }

lemma skeletal : skeletal simplex_category :=
Œª X Y ‚ü®I‚ü©,
begin
  suffices : fintype.card (fin (X+1)) = fintype.card (fin (Y+1)),
  { simpa, },
  { apply fintype.card_congr,
    refine equiv.ulift.symm.trans (((skeletal_functor ‚ãô forget _).map_iso I).to_equiv.trans _),
    apply equiv.ulift }
end

namespace skeletal_functor

instance : full skeletal_functor.{u} :=
{ preimage := Œª m n f, ‚ü®Œª i, (f ‚ü®i‚ü©).down, Œª i j h, f.monotone h‚ü©,
  witness' := by { intros m n f, dsimp at *, ext1 ‚ü®i‚ü©, ext1, refl } }

instance : faithful skeletal_functor.{u} :=
{ map_injective' := Œª m n f g h,
  begin
    ext1 i, apply ulift.up.inj,
    show skeletal_functor.map f ‚ü®i‚ü© = skeletal_functor.map g ‚ü®i‚ü©,
    rw h,
  end }

instance : ess_surj skeletal_functor.{u} :=
{ mem_ess_image := Œª X, ‚ü®(fintype.card X - 1 : ‚Ñï), ‚ü®begin
    have aux : fintype.card X = fintype.card X - 1 + 1,
    { exact (nat.succ_pred_eq_of_pos $ fintype.card_pos_iff.mpr ‚ü®‚ä•‚ü©).symm, },
    let f := mono_equiv_of_fin X aux,
    have hf := (finset.univ.order_emb_of_fin aux).strict_mono,
    refine
    { hom := ‚ü®Œª i, f i.down, _‚ü©,
      inv := ‚ü®Œª i, ‚ü®f.symm i‚ü©, _‚ü©,
      hom_inv_id' := _,
      inv_hom_id' := _ },
    { rintro ‚ü®i‚ü© ‚ü®j‚ü© h, show f i ‚â§ f j, exact hf.monotone h, },
    { intros i j h, show f.symm i ‚â§ f.symm j, rw ‚Üê hf.le_iff_le,
      show f (f.symm i) ‚â§ f (f.symm j), simpa only [order_iso.apply_symm_apply], },
    { ext1 ‚ü®i‚ü©, ext1, exact f.symm_apply_apply i },
    { ext1 i, exact f.apply_symm_apply i },
  end‚ü©‚ü©,}

noncomputable instance is_equivalence : is_equivalence skeletal_functor.{u} :=
equivalence.equivalence_of_fully_faithfully_ess_surj skeletal_functor

end skeletal_functor

/-- The equivalence that exhibits `simplex_category` as skeleton
of `NonemptyFinLinOrd` -/
noncomputable def skeletal_equivalence : simplex_category ‚âå NonemptyFinLinOrd.{u} :=
functor.as_equivalence skeletal_functor.{u}

end skeleton

/--
`simplex_category` is a skeleton of `NonemptyFinLinOrd`.
-/
noncomputable
def is_skeleton_of : is_skeleton_of NonemptyFinLinOrd.{u} simplex_category skeletal_functor.{u} :=
{ skel := skeletal,
  eqv := skeletal_functor.is_equivalence }

end simplex_category
