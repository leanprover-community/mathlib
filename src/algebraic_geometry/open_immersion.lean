/-
Copyright (c) 2021 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
import algebraic_geometry.presheafed_space.has_colimits
import category_theory.limits.shapes.binary_products
import category_theory.limits.preserves.shapes.pullbacks
import topology.sheaves.functors
import algebraic_geometry.Scheme
import category_theory.limits.shapes.strict_initial

/-!
# Open immersions of structured spaces

We say that a morphism of presheafed spaces `f : X ‚ü∂ Y` is an open immersions if
the underlying map of spaces is an open embedding `f : X ‚ü∂ U ‚äÜ Y`,
and the sheaf map `Y(V) ‚ü∂ f _* X(V)` is an iso for each `V ‚äÜ U`.

Abbreviations are also provided for `SheafedSpace`, `LocallyRingedSpace` and `Scheme`.

## Main definitions

* `algebraic_geometry.PresheafedSpace.is_open_immersion`: the `Prop`-valued typeclass asserting
  that a PresheafedSpace hom `f` is an open_immersion.
* `algebraic_geometry.is_open_immersion`: the `Prop`-valued typeclass asserting
  that a Scheme morphism `f` is an open_immersion.
* `algebraic_geometry.PresheafedSpace.is_open_immersion.iso_restrict`: The source of an
  open immersion is isomorphic to the restriction of the target onto the image.

## Main results

* `algebraic_geometry.PresheafedSpace.is_open_immersion.comp`: The composition of two open
  immersions is an open immersion.
* `algebraic_geometry.PresheafedSpace.is_open_immersion.of_iso`: An iso is an open immersion.
* `algebraic_geometry.PresheafedSpace.is_open_immersion.to_iso`:
  A surjective open immersion is an isomorphism.
* `algebraic_geometry.PresheafedSpace.is_open_immersion.stalk_iso`: An open immersion induces
  an isomorphism on stalks.

-/

open topological_space category_theory opposite
open category_theory.limits
namespace algebraic_geometry

universes v u

variables {C : Type u} [category.{v} C]

/--
An open immersion of PresheafedSpaces is an open embedding `f : X ‚ü∂ U ‚äÜ Y` of the underlying
spaces, such that the sheaf map `Y(V) ‚ü∂ f _* X(V)` is an iso for each `V ‚äÜ U`.
-/
class PresheafedSpace.is_open_immersion {X Y : PresheafedSpace C} (f : X ‚ü∂ Y) : Prop :=
(base_open : open_embedding f.base)
(c_iso : ‚àÄ U : opens X, is_iso (f.c.app (op (base_open.is_open_map.functor.obj U))))

/--
A morphism of SheafedSpaces is an open immersion if it is an open immersion as a morphism
of PresheafedSpaces
-/
abbreviation SheafedSpace.is_open_immersion
  [has_products C] {X Y : SheafedSpace C} (f : X ‚ü∂ Y) : Prop :=
PresheafedSpace.is_open_immersion f

/--
A morphism of LocallyRingedSpaces is an open immersion if it is an open immersion as a morphism
of SheafedSpaces
-/
abbreviation LocallyRingedSpace.is_open_immersion {X Y : LocallyRingedSpace} (f : X ‚ü∂ Y) : Prop :=
SheafedSpace.is_open_immersion f.1

/--
A morphism of Schemes is an open immersion if it is an open immersion as a morphism
of LocallyRingedSpaces
-/
abbreviation is_open_immersion {X Y : Scheme} (f : X ‚ü∂ Y) : Prop :=
LocallyRingedSpace.is_open_immersion f

namespace PresheafedSpace.is_open_immersion

open PresheafedSpace

local notation `is_open_immersion` := PresheafedSpace.is_open_immersion

attribute [instance] is_open_immersion.c_iso

section

variables {X Y : PresheafedSpace C} {f : X ‚ü∂ Y} (H : is_open_immersion f)

/-- The functor `opens X ‚•§ opens Y` associated with an open immersion `f : X ‚ü∂ Y`. -/
abbreviation open_functor := H.base_open.is_open_map.functor

/-
We want to keep `eq_to_hom`s in the form of `F.map (eq_to_hom _)` so that the lemmas about
naturality can be applied.
-/
local attribute [-simp] eq_to_hom_map eq_to_iso_map

/-- An open immersion `f : X ‚ü∂ Y` induces an isomorphism `X ‚âÖ Y|_{f(X)}`. -/
@[simps] noncomputable
def iso_restrict : X ‚âÖ Y.restrict H.base_open :=
PresheafedSpace.iso_of_components (iso.refl _)
begin
  symmetry,
  fapply nat_iso.of_components,
  intro U,
  refine as_iso (f.c.app (op (H.open_functor.obj (unop U)))) ‚â™‚â´ X.presheaf.map_iso (eq_to_iso _),
  { induction U using opposite.rec,
    cases U,
    dsimp only [is_open_map.functor, functor.op, opens.map],
    congr' 2,
    erw set.preimage_image_eq _ H.base_open.inj,
    refl },
  { intros U V i,
    simp only [category_theory.eq_to_iso.hom, Top.presheaf.pushforward_obj_map, category.assoc,
      functor.op_map, iso.trans_hom, as_iso_hom, functor.map_iso_hom, ‚ÜêX.presheaf.map_comp],
    erw [f.c.naturality_assoc, ‚ÜêX.presheaf.map_comp],
    congr }
end

@[simp] lemma iso_restrict_hom_of_restrict : H.iso_restrict.hom ‚â´ Y.of_restrict _ = f :=
begin
  ext,
  { simp only [comp_c_app, iso_restrict_hom_c_app, nat_trans.comp_app,
      eq_to_hom_refl, of_restrict_c_app, category.assoc, whisker_right_id'],
    erw [category.comp_id, f.c.naturality_assoc, ‚ÜêX.presheaf.map_comp],
    transitivity f.c.app x ‚â´ X.presheaf.map (ùüô _),
    { congr },
    { erw [X.presheaf.map_id, category.comp_id] } },
  { simp }
end

@[simp] lemma iso_restrict_inv_of_restrict : H.iso_restrict.inv ‚â´ f = Y.of_restrict _ :=
by { rw iso.inv_comp_eq, simp }

instance mono [H : is_open_immersion f] : mono f :=
by { rw ‚Üê H.iso_restrict_hom_of_restrict, apply mono_comp }

/-- The composition of two open immersions is an open immersion. -/
instance comp {Z : PresheafedSpace C} (f : X ‚ü∂ Y) [hf : is_open_immersion f] (g : Y ‚ü∂ Z)
  [hg : is_open_immersion g] :
  is_open_immersion (f ‚â´ g) :=
{ base_open := hg.base_open.comp hf.base_open,
  c_iso := Œª U,
  begin
    generalize_proofs h,
    dsimp only [algebraic_geometry.PresheafedSpace.comp_c_app, unop_op, functor.op, comp_base,
      Top.presheaf.pushforward_obj_obj, opens.map_comp_obj],
    apply_with is_iso.comp_is_iso { instances := ff },
    swap,
    { have : (opens.map g.base).obj (h.functor.obj U) = hf.open_functor.obj U,
      { dsimp only [opens.map, is_open_map.functor, PresheafedSpace.comp_base],
        congr' 1,
        rw [coe_comp, ‚Üêset.image_image, set.preimage_image_eq _ hg.base_open.inj] },
      rw this,
      apply_instance },
    { have : h.functor.obj U = hg.open_functor.obj (hf.open_functor.obj U),
      { dsimp only [is_open_map.functor],
        congr' 1,
        rw [comp_base, coe_comp, ‚Üêset.image_image],
        congr },
      rw this,
      apply_instance }
  end }

/-- For an open immersion `f : X ‚ü∂ Y` and an open set `U ‚äÜ X`, we have the map `X(U) ‚ü∂ Y(U)`. -/
noncomputable
def inv_app (U : opens X) : X.presheaf.obj (op U) ‚ü∂ Y.presheaf.obj (op (H.open_functor.obj U)) :=
X.presheaf.map (eq_to_hom (by simp [opens.map, set.preimage_image_eq _ H.base_open.inj])) ‚â´
  inv (f.c.app (op (H.open_functor.obj U)))

@[simp, reassoc] lemma inv_naturality {U V : (opens X)·µí·µñ} (i : U ‚ü∂ V) :
  X.presheaf.map i ‚â´ H.inv_app (unop V) = H.inv_app (unop U) ‚â´
    Y.presheaf.map (H.open_functor.op.map i) :=
begin
  simp only [inv_app, ‚Üêcategory.assoc],
  rw [is_iso.comp_inv_eq],
  simp only [category.assoc, f.c.naturality, is_iso.inv_hom_id_assoc, ‚Üê X.presheaf.map_comp],
  erw ‚Üê X.presheaf.map_comp,
  congr
end

instance (U : opens X) : is_iso (H.inv_app U) := by { delta inv_app, apply_instance }

lemma inv_inv_app (U : opens X) :
  inv (H.inv_app U) = f.c.app (op (H.open_functor.obj U)) ‚â´
    X.presheaf.map (eq_to_hom (by simp [opens.map, set.preimage_image_eq _ H.base_open.inj])) :=
begin
  rw ‚Üê cancel_epi (H.inv_app U),
  rw is_iso.hom_inv_id,
  delta inv_app,
  simp [‚Üê functor.map_comp]
end

@[simp, reassoc] lemma inv_app_app (U : opens X) :
  H.inv_app U ‚â´ f.c.app (op (H.open_functor.obj U)) =
    X.presheaf.map (eq_to_hom (by simp [opens.map, set.preimage_image_eq _ H.base_open.inj])) :=
by rw [inv_app, category.assoc, is_iso.inv_hom_id, category.comp_id]

@[simp, reassoc] lemma app_inv_app (U : opens Y) :
  f.c.app (op U) ‚â´ H.inv_app ((opens.map f.base).obj U) =
  Y.presheaf.map ((hom_of_le (by exact set.image_preimage_subset f.base U)).op :
    op U ‚ü∂ op (H.open_functor.obj ((opens.map f.base).obj U))) :=
by { erw ‚Üê category.assoc, rw [is_iso.comp_inv_eq, f.c.naturality], congr }

/-- A variant of `app_inv_app` that gives an `eq_to_hom` instead of `hom_of_le`. -/
@[reassoc] lemma app_inv_app' (U : opens Y) (hU : (U : set Y) ‚äÜ set.range f.base) :
  f.c.app (op U) ‚â´ H.inv_app ((opens.map f.base).obj U) =
  Y.presheaf.map (eq_to_hom (by
    { apply has_le.le.antisymm,
      { exact set.image_preimage_subset f.base U.1 },
      { change U ‚äÜ _,
        refine has_le.le.trans_eq _ (@set.image_preimage_eq_inter_range _ _ f.base U.1).symm,
        exact set.subset_inter_iff.mpr ‚ü®Œª _ h, h, hU‚ü© } })).op :=
by { erw ‚Üê category.assoc, rw [is_iso.comp_inv_eq, f.c.naturality], congr }

/-- An isomorphism is an open immersion. -/
instance of_iso {X Y : PresheafedSpace C} (H : X ‚âÖ Y) : is_open_immersion H.hom :=
{ base_open := (Top.homeo_of_iso ((forget C).map_iso H)).open_embedding,
  c_iso := Œª _, infer_instance }

@[priority 100]
instance of_is_iso {X Y : PresheafedSpace C} (f : X ‚ü∂ Y) [is_iso f] : is_open_immersion f :=
algebraic_geometry.PresheafedSpace.is_open_immersion.of_iso (as_iso f)

instance of_restrict {X : Top} (Y : PresheafedSpace C) {f : X ‚ü∂ Y.carrier}
  (hf : open_embedding f) : is_open_immersion (Y.of_restrict hf) :=
{ base_open := hf,
  c_iso := Œª U,
  begin
    dsimp,
    have : (opens.map f).obj (hf.is_open_map.functor.obj U) = U,
    { cases U,
      dsimp only [opens.map, is_open_map.functor],
      congr' 1,
      rw set.preimage_image_eq _ hf.inj,
      refl },
    convert (show is_iso (Y.presheaf.map (ùüô _)), from infer_instance),
    { apply subsingleton.helim,
      rw this },
    { rw Y.presheaf.map_id,
      apply_instance }
  end }

/-- An open immersion is an iso if the underlying continuous map is epi. -/
lemma to_iso (f : X ‚ü∂ Y) [h : is_open_immersion f] [h' : epi f.base] : is_iso f :=
begin
  apply_with is_iso_of_components { instances := ff },
  { let : X ‚âÉ‚Çú Y := (homeomorph.of_embedding _ h.base_open.to_embedding).trans
    { to_fun := subtype.val, inv_fun := Œª x, ‚ü®x,
      by { rw set.range_iff_surjective.mpr ((Top.epi_iff_surjective _).mp h'), trivial }‚ü©,
      left_inv := Œª ‚ü®_,_‚ü©, rfl, right_inv := Œª _, rfl },
    convert is_iso.of_iso (Top.iso_of_homeo this),
    { ext, refl } },
  { apply_with nat_iso.is_iso_of_is_iso_app { instances := ff },
    intro U,
    have : U = op (h.open_functor.obj ((opens.map f.base).obj (unop U))),
    { induction U using opposite.rec,
      cases U,
      dsimp only [functor.op, opens.map],
      congr,
      exact (set.image_preimage_eq _ ((Top.epi_iff_surjective _).mp h')).symm },
    convert @@is_open_immersion.c_iso _ h ((opens.map f.base).obj (unop U)) }
end

instance stalk_iso [has_colimits C] [H : is_open_immersion f] (x : X) : is_iso (stalk_map f x) :=
begin
  rw ‚Üê H.iso_restrict_hom_of_restrict,
  rw PresheafedSpace.stalk_map.comp,
  apply_instance
end

end

end PresheafedSpace.is_open_immersion

namespace SheafedSpace.is_open_immersion

section prod

variables [has_limits C] {Œπ : Type v} (F : discrete Œπ ‚•§ SheafedSpace C) [has_colimit F] (i : Œπ)

lemma sigma_Œπ_open_embedding : open_embedding (colimit.Œπ F i).base :=
begin
  rw ‚Üê (show _ = (colimit.Œπ F i).base,
    from Œπ_preserves_colimits_iso_inv (SheafedSpace.forget C) F i),
  have : _ = _ ‚â´ colimit.Œπ (discrete.functor (F ‚ãô SheafedSpace.forget C).obj) i :=
    has_colimit.iso_of_nat_iso_Œπ_hom discrete.nat_iso_functor i,
  rw ‚Üê iso.eq_comp_inv at this,
  rw this,
  have : colimit.Œπ _ _ ‚â´ _ = _ := Top.sigma_iso_sigma_hom_Œπ (F ‚ãô SheafedSpace.forget C).obj i,
  rw ‚Üê iso.eq_comp_inv at this,
  rw this,
  simp_rw [‚Üê category.assoc, Top.open_embedding_iff_comp_is_iso,
    Top.open_embedding_iff_is_iso_comp],
  exact open_embedding_sigma_mk
end

lemma image_preimage_is_empty (j : Œπ) (h : i ‚â† j) (U : opens (F.obj i)) :
  (opens.map (colimit.Œπ (F ‚ãô SheafedSpace.forget_to_PresheafedSpace) j).base).obj
    ((opens.map (preserves_colimit_iso SheafedSpace.forget_to_PresheafedSpace F).inv.base).obj
    ((sigma_Œπ_open_embedding F i).is_open_map.functor.obj U)) = ‚àÖ :=
begin
  ext,
  apply iff_false_intro,
  rintro ‚ü®y, hy, eq‚ü©,
  replace eq := concrete_category.congr_arg
    (preserves_colimit_iso (SheafedSpace.forget C) F ‚â™‚â´
      has_colimit.iso_of_nat_iso discrete.nat_iso_functor ‚â™‚â´ Top.sigma_iso_sigma _).hom eq,
  simp_rw [category_theory.iso.trans_hom, ‚Üê Top.comp_app, ‚Üê PresheafedSpace.comp_base] at eq,
  rw Œπ_preserves_colimits_iso_inv at eq,
  change ((SheafedSpace.forget C).map (colimit.Œπ F i) ‚â´ _) y =
    ((SheafedSpace.forget C).map (colimit.Œπ F j) ‚â´ _) x at eq,
  rw [Œπ_preserves_colimits_iso_hom_assoc, Œπ_preserves_colimits_iso_hom_assoc,
    has_colimit.iso_of_nat_iso_Œπ_hom_assoc, has_colimit.iso_of_nat_iso_Œπ_hom_assoc,
    Top.sigma_iso_sigma_hom_Œπ, Top.sigma_iso_sigma_hom_Œπ] at eq,
  exact h (congr_arg sigma.fst eq)
end

instance sigma_Œπ_is_open_immersion [has_strict_terminal_objects C] :
  SheafedSpace.is_open_immersion (colimit.Œπ F i) :=
{ base_open := sigma_Œπ_open_embedding F i,
  c_iso := Œª U, begin
    have e : colimit.Œπ F i = _ :=
      (Œπ_preserves_colimits_iso_inv SheafedSpace.forget_to_PresheafedSpace F i).symm,
    have H : open_embedding (colimit.Œπ (F ‚ãô SheafedSpace.forget_to_PresheafedSpace) i ‚â´
      (preserves_colimit_iso SheafedSpace.forget_to_PresheafedSpace F).inv).base :=
      e ‚ñ∏ sigma_Œπ_open_embedding F i,
    suffices : is_iso ((colimit.Œπ (F ‚ãô SheafedSpace.forget_to_PresheafedSpace) i ‚â´
      (preserves_colimit_iso SheafedSpace.forget_to_PresheafedSpace F).inv).c.app
        (op (H.is_open_map.functor.obj U))),
    { convert this },
    rw [PresheafedSpace.comp_c_app,
      ‚Üê PresheafedSpace.colimit_presheaf_obj_iso_componentwise_limit_hom_œÄ],
    suffices : is_iso (limit.œÄ (PresheafedSpace.componentwise_diagram
      (F ‚ãô SheafedSpace.forget_to_PresheafedSpace)
      ((opens.map (preserves_colimit_iso SheafedSpace.forget_to_PresheafedSpace F).inv.base).obj
      (unop $ op $ H.is_open_map.functor.obj U))) (op i)),
    { resetI, apply_instance },
    apply limit_œÄ_is_iso_of_is_strict_terminal,
    intros j hj,
    induction j using opposite.rec,
    dsimp,
    convert (F.obj j).sheaf.is_terminal_of_empty,
    convert image_preimage_is_empty F i j (Œª h, hj (congr_arg op h.symm)) U,
    exact (congr_arg PresheafedSpace.hom.base e).symm
  end }

end prod

end SheafedSpace.is_open_immersion

end algebraic_geometry
