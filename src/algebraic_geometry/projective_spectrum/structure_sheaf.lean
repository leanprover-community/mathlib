/-
Copyright (c) 2022 Jujian Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jujian Zhang, Eric Wieser
-/
import algebraic_geometry.projective_spectrum.topology
import topology.sheaves.local_predicate
import ring_theory.localization.at_prime
import algebraic_geometry.locally_ringed_space

/-!
# The structure sheaf on `projective_spectrum ùíú`.

In `src/algebraic_geometry/topology.lean`, we have given a topology on `projective_spectrum ùíú`; in
this file we will construct a sheaf on `projective_spectrum ùíú`.

## Notation
- `R` is a commutative semiring;
- `A` is a commutative ring and an `R`-algebra;
- `ùíú : ‚Ñï ‚Üí submodule R A` is the grading of `A`;
- `U` is opposite object of some open subset of `projective_spectrum.Top`.

## Main definitions and results
* `projective_spectrum.Top`: the topological space of `projective_spectrum ùíú` endowed with the
  zariski topology
* `algebraic_geometry.projective_spectrum.structure_sheaf.homogeneous_localization`: given `x` in
  `projective_spectrum.Top ùíú`, homogeneous localization at `x` is the subring of `A‚Çì` (`A` localized
  at prime `x`) where the numerator and denominator have same grading.

Then we define the structure sheaf as the subsheaf of all dependent function
`f : Œ† x : U, homogeneous_localization x` such that `f` is locally expressible as ratio of two
elements of the *same grading*, i.e. `‚àÄ y ‚àà U, ‚àÉ (V ‚äÜ U) (i : ‚Ñï) (a b ‚àà ùíú i), ‚àÄ z ‚àà V, f z = a / b`.

* `algebraic_geometry.projective_spectrum.structure_sheaf.is_locally_fraction`: the predicate that
  a dependent function is locally expressible as ration of two elements of the same grading.
* `algebraic_geometry.projective_spectrum.structure_sheaf.sections_subring`: the dependent functions
  satisfying the above local property forms a subring of all dependent functions
  `Œ† x : U, homogeneous_localization x`.
* `algebraic_geometry.Proj.structure_sheaf`: the sheaf with `U ‚Ü¶ sections_subring U` and natural
  restriction map.

Then we establish that `Proj ùíú` is a `LocallyRingedSpace`:
* `algebraic_geometry.homogeneous_localization.is_local`: for any `x : projective_spectrum ùíú`,
  `homogeneous_localization x` is a local ring.
* `algebraic_geometry.Proj.stalk_iso'`: for any `x : projective_spectrum ùíú`, the stalk of
  `Proj.structure_sheaf` at `x` is isomorphic to `homogeneous_localization x`.
* `algebraic_geometry.Proj.to_LocallyRingedSpace`: `Proj` as a locally ringed space.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/

noncomputable theory

namespace algebraic_geometry

open_locale direct_sum big_operators pointwise
open direct_sum set_like

variables {R A: Type*}
variables [comm_ring R] [comm_ring A] [algebra R A]
variables (ùíú : ‚Ñï ‚Üí submodule R A) [graded_algebra ùíú]

local notation `at ` x := localization.at_prime x.as_homogeneous_ideal.to_ideal

open Top topological_space category_theory opposite

/--
The underlying topology of `Proj` is the projective spectrum of graded ring `A`.
-/
def projective_spectrum.Top : Top := Top.of (projective_spectrum ùíú)

namespace projective_spectrum.structure_sheaf

namespace homogeneous_localization

open set_like.graded_monoid submodule

variables {ùíú} (x : projective_spectrum.Top ùíú)

/--
If `x` is a point in `Proj ùíú`, then `y ‚àà A‚Çì` is said to satisfy `num_denom_same_deg` if and only if
`y = a / b` where `a` and `b` are both in `ùíú i` for some `i`.
-/
@[nolint has_inhabited_instance]
structure num_denom_same_deg :=
(deg : ‚Ñï)
(num denom : ùíú deg)
(denom_not_mem : (denom : A) ‚àâ x.as_homogeneous_ideal)

@[ext] lemma ext {c1 c2 : num_denom_same_deg x} (hdeg : c1.deg = c2.deg)
  (hnum : (c1.num : A) = c2.num) (hdenom : (c1.denom : A) = c2.denom) :
  c1 = c2 :=
begin
  rcases c1 with ‚ü®i1, ‚ü®n1, hn1‚ü©, ‚ü®d1, hd1‚ü©, h1‚ü©,
  rcases c2 with ‚ü®i2, ‚ü®n2, hn2‚ü©, ‚ü®d2, hd2‚ü©, h2‚ü©,
  dsimp only [subtype.coe_mk] at *,
  simp only,
  exact ‚ü®hdeg, by subst hdeg; subst hnum, by subst hdeg; subst hdenom‚ü©,
end

instance : has_one (num_denom_same_deg x) :=
{ one :=
  { deg := 0,
    num := ‚ü®1, one_mem‚ü©,
    denom := ‚ü®1, one_mem‚ü©,
    denom_not_mem := Œª rid, x.is_prime.ne_top $ (ideal.eq_top_iff_one _).mpr rid } }

@[simp] lemma deg_one : (1 : num_denom_same_deg x).deg = 0 := rfl
@[simp] lemma num_one : ((1 : num_denom_same_deg x).num : A) = 1 := rfl
@[simp] lemma denom_one : ((1 : num_denom_same_deg x).denom : A) = 1 := rfl

instance : has_zero (num_denom_same_deg x) :=
{ zero :=
  { deg := 0,
    num := 0,
    denom := ‚ü®1, one_mem‚ü©,
    denom_not_mem := Œª r, x.is_prime.ne_top $ (ideal.eq_top_iff_one _).mpr r } }

@[simp] lemma deg_zero : (0 : num_denom_same_deg x).deg = 0 := rfl
@[simp] lemma num_zero : (0 : num_denom_same_deg x).num = 0 := rfl
@[simp] lemma denom_zero : ((0 : num_denom_same_deg x).denom : A) = 1 := rfl

instance : has_mul (num_denom_same_deg x) :=
{ mul := Œª p q,
  { deg := p.deg + q.deg,
    num := ‚ü®p.num * q.num, mul_mem p.num.prop q.num.prop‚ü©,
    denom := ‚ü®p.denom * q.denom, mul_mem p.denom.prop q.denom.prop‚ü©,
    denom_not_mem := Œª r, or.elim (x.is_prime.mem_or_mem r) p.denom_not_mem q.denom_not_mem } }

lemma deg_mul (c1 c2 : num_denom_same_deg x) : (c1 * c2).deg = c1.deg + c2.deg := rfl
lemma num_mul (c1 c2 : num_denom_same_deg x) : ((c1 * c2).num : A) = c1.num * c2.num := rfl
lemma denom_mul (c1 c2 : num_denom_same_deg x) : ((c1 * c2).denom : A) = c1.denom * c2.denom := rfl

instance : has_add (num_denom_same_deg x) :=
{ add := Œª c1 c2,
  { deg := c1.deg + c2.deg,
    num := ‚ü®c1.denom * c2.num + c2.denom * c1.num,
      add_mem _ (mul_mem c1.denom.2 c2.num.2)
        (add_comm c2.deg c1.deg ‚ñ∏ mul_mem c2.denom.2 c1.num.2)‚ü©,
    denom := ‚ü®c1.denom * c2.denom, mul_mem c1.denom.2 c2.denom.2‚ü©,
    denom_not_mem := Œª r, or.elim (x.is_prime.mem_or_mem r) c1.denom_not_mem c2.denom_not_mem } }

lemma deg_add (c1 c2 : num_denom_same_deg x) : (c1 + c2).deg = c1.deg + c2.deg := rfl
lemma num_add (c1 c2 : num_denom_same_deg x) :
  ((c1 + c2).num : A) = c1.denom * c2.num + c2.denom * c1.num := rfl
lemma denom_add (c1 c2 : num_denom_same_deg x) :
  ((c1 + c2).denom : A) = c1.denom * c2.denom := rfl

instance : has_neg (num_denom_same_deg x) :=
{ neg := Œª c, ‚ü®c.deg, ‚ü®-c.num, neg_mem _ c.num.2‚ü©, c.denom, c.denom_not_mem‚ü© }

lemma deg_neg (c : num_denom_same_deg x) : (-c).deg = c.deg := rfl
lemma num_neg (c : num_denom_same_deg x) : ((-c).num : A) = -c.num := rfl
lemma denom_neg (c : num_denom_same_deg x) : ((-c).denom : A) = c.denom := rfl

instance : comm_monoid (num_denom_same_deg x) :=
{ one := 1,
  mul := (*),
  mul_assoc := Œª c1 c2 c3, ext _ (add_assoc _ _ _) (mul_assoc _ _ _) (mul_assoc _ _ _),
  one_mul := Œª c, ext _ (zero_add _) (one_mul _) (one_mul _),
  mul_one := Œª c, ext _ (add_zero _) (mul_one _) (mul_one _),
  mul_comm := Œª c1 c2, ext _ (add_comm _ _) (mul_comm _ _) (mul_comm _ _) }

instance : has_pow (num_denom_same_deg x) ‚Ñï :=
{ pow := Œª c n, ‚ü®n ‚Ä¢ c.deg, ‚ü®c.num ^ n, pow_mem n c.num.2‚ü©, ‚ü®c.denom ^ n, pow_mem n c.denom.2‚ü©,
    begin
      cases n,
      { simp only [pow_zero],
        exact Œª r, x.is_prime.ne_top $ (ideal.eq_top_iff_one _).mpr r, },
      { exact Œª r, c.denom_not_mem $ (x.is_prime.pow_mem_iff_mem n.succ (nat.zero_lt_succ _)).mp r }
    end‚ü© }

lemma deg_pow (c : num_denom_same_deg x) (n : ‚Ñï) : (c ^ n).deg = n ‚Ä¢ c.deg := rfl
lemma num_pow (c : num_denom_same_deg x) (n : ‚Ñï) : ((c ^ n).num : A) = c.num ^ n := rfl
lemma denom_pow (c : num_denom_same_deg x) (n : ‚Ñï) : ((c ^ n).denom : A) = c.denom ^ n := rfl

instance : has_scalar ‚Ñ§ (num_denom_same_deg x) :=
{ smul := Œª m c, ‚ü®c.deg, ‚ü®m ‚Ä¢ c.num, begin
  rw [zsmul_eq_mul],
    suffices : (m : A) ‚àà ùíú 0,
    { convert mul_mem this c.num.2,
      rw zero_add, },
    { induction m using int.induction_on with m ih m ih,
      { exact zero_mem _ },
      { exact add_mem _ ih one_mem, },
      { push_cast at ih ‚ä¢,
        exact sub_mem _ ih one_mem, } },
  end‚ü©, c.denom, c.denom_not_mem‚ü© }

lemma deg_zsmul (c : num_denom_same_deg x) (m : ‚Ñ§) : (m ‚Ä¢ c).deg = c.deg := rfl
lemma num_zsmul (c : num_denom_same_deg x) (m : ‚Ñ§) : ((m ‚Ä¢ c).num : A) = m ‚Ä¢ c.num := rfl
lemma denom_zsmul (c : num_denom_same_deg x) (m : ‚Ñ§) : ((m ‚Ä¢ c).denom : A) = c.denom := rfl

instance nat_scalar : has_scalar ‚Ñï (num_denom_same_deg x) :=
{ smul := Œª m c, (m : ‚Ñ§) ‚Ä¢ c }

def num_denom_same_deg.embedding (p : num_denom_same_deg x) : at x :=
localization.mk p.num ‚ü®p.denom, p.denom_not_mem‚ü©

def homogeneous_localization : Type* := quotient (setoid.ker $ num_denom_same_deg.embedding x)

variable {x}
def homogeneous_localization.val (y : homogeneous_localization x) : at x :=
quotient.lift_on' y (num_denom_same_deg.embedding x) $ Œª _ _, id

variable (x)
lemma homogeneous_localization.val_injective :
  function.injective (@homogeneous_localization.val _ _ _ _ _ ùíú _ x) :=
Œª a b, quotient.rec_on_subsingleton‚ÇÇ' a b $ Œª a b h, quotient.sound' h

instance homogeneous_localization.has_pow : has_pow (homogeneous_localization x) ‚Ñï :=
{ pow := Œª z n, (quotient.map' (^ n)
    (Œª c1 c2 (h : localization.mk _ _ = localization.mk _ _), begin
      change localization.mk _ _ = localization.mk _ _,
      simp only [num_pow, denom_pow],
      convert congr_arg (Œª z, z ^ n) h;
      erw localization.mk_pow;
      refl,
    end) : homogeneous_localization x ‚Üí homogeneous_localization x) z }

instance : has_scalar ‚Ñ§ (homogeneous_localization x) :=
{ smul := Œª m, quotient.map' ((‚Ä¢) m)
    (Œª c1 c2 (h : localization.mk _ _ = localization.mk _ _), begin
      change localization.mk _ _ = localization.mk _ _,
      simp only [num_zsmul, denom_zsmul],
      convert congr_arg (Œª z, m ‚Ä¢ z) h;
      rw [zsmul_eq_mul, zsmul_eq_mul, show (m : at x) = localization.mk m 1, begin
        induction m using int.induction_on with n ih n ih,
        { erw localization.mk_zero _, refl, },
        { push_cast,
          erw [ih, ‚Üê localization.mk_one, localization.add_mk, one_mul, one_mul, add_comm, one_mul],
          refl, },
        { push_cast at ih ‚ä¢,
          erw [ih, ‚Üê localization.mk_one, sub_eq_add_neg, localization.neg_mk, localization.add_mk,
            mul_one, one_mul, one_mul, add_comm, ‚Üê sub_eq_add_neg], },
      end, localization.mk_mul, one_mul];
      refl,
    end) }

instance homogeneous_localization.nat_scalar : has_scalar ‚Ñï (homogeneous_localization x) :=
{ smul := Œª n z, (n : ‚Ñ§) ‚Ä¢ z }

instance : has_neg (homogeneous_localization x) :=
{ neg := quotient.map' has_neg.neg
    (Œª c1 c2 (h : localization.mk _ _ = localization.mk _ _), begin
      change localization.mk _ _ = localization.mk _ _,
      simp only [num_neg, denom_neg, ‚Üêlocalization.neg_mk],
      exact congr_arg (Œª c, -c) h
    end) }

instance : has_add (homogeneous_localization x) :=
{ add := quotient.map‚ÇÇ' (+) (Œª c1 c2 (h : localization.mk _ _ = localization.mk _ _)
    c3 c4 (h' : localization.mk _ _ = localization.mk _ _), begin
    change localization.mk _ _ = localization.mk _ _,
    simp only [num_add, denom_add, ‚Üêlocalization.add_mk],
    convert congr_arg2 (+) h h';
    erw [localization.add_mk];
    refl,
  end) }

instance : has_sub (homogeneous_localization x) :=
{ sub := Œª z1 z2, z1 + (-z2) }

instance : has_mul (homogeneous_localization x) :=
{ mul := quotient.map‚ÇÇ' (*) (Œª c1 c2 (h : localization.mk _ _ = localization.mk _ _)
    c3 c4 (h' : localization.mk _ _ = localization.mk _ _), begin
    change localization.mk _ _ = localization.mk _ _,
    simp only [num_mul, denom_mul],
    convert congr_arg2 (*) h h';
    erw [localization.mk_mul];
    refl,
  end) }

instance : has_one (homogeneous_localization x) :=
{ one := quotient.mk' 1 }

instance : has_zero (homogeneous_localization x) :=
{ zero := quotient.mk' 0 }

lemma homogeneous_localization.zero_eq :
  (0 : homogeneous_localization x) = quotient.mk' 0 :=
rfl

lemma homogeneous_localization.one_eq :
  (1 : homogeneous_localization x) = quotient.mk' 1 :=
rfl

variable {x}
lemma zero_val : (0 : homogeneous_localization x).val= 0 :=
begin
  rw [homogeneous_localization.zero_eq, homogeneous_localization.val, quotient.lift_on'_mk'],
  change localization.mk _ _ = _,
  convert localization.mk_zero _,
end

lemma one_val : (1 : homogeneous_localization x).val= 1 :=
begin
  rw [homogeneous_localization.one_eq, homogeneous_localization.val, quotient.lift_on'_mk'],
  change localization.mk _ _ = _,
  simp only [num_one, denom_one],
  convert localization.mk_self _,
  refl,
end

lemma add_val (y1 y2 : homogeneous_localization x) :
  (y1 + y2).val = y1.val + y2.val :=
begin
  induction y1 using quotient.induction_on,
  induction y2 using quotient.induction_on,
  unfold homogeneous_localization.val has_add.add,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = localization.mk _ _ + localization.mk _ _,
  dsimp only,
  rw [localization.add_mk],
  refl,
end

lemma mul_val (y1 y2 : homogeneous_localization x) :
  (y1 * y2).val = y1.val * y2.val :=
begin
  induction y1 using quotient.induction_on,
  induction y2 using quotient.induction_on,
  unfold homogeneous_localization.val has_mul.mul,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = localization.mk _ _ * localization.mk _ _,
  dsimp only,
  rw [localization.mk_mul],
  refl,
end

lemma neg_val (y : homogeneous_localization x) :
  (-y).val = -y.val :=
begin
  induction y using quotient.induction_on,
  unfold homogeneous_localization.val has_neg.neg,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = - localization.mk _ _,
  dsimp only,
  rw [localization.neg_mk],
  refl,
end

lemma sub_val (y1 y2 : homogeneous_localization x) :
  (y1 - y2).val = y1.val - y2.val :=
begin
  rw [show y1 - y2 = y1 + (-y2), from rfl, add_val, neg_val],
  refl,
end

lemma nsmul_val (y : homogeneous_localization x) (n : ‚Ñï) :
  (n ‚Ä¢ y).val = n ‚Ä¢ y.val :=
begin
  induction y using quotient.induction_on,
  unfold homogeneous_localization.val has_scalar.smul,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = n ‚Ä¢ localization.mk _ _,
  dsimp only,
  rw [nsmul_eq_mul, show (n : at x) = localization.mk n 1, begin
    induction n with n ih,
    { erw localization.mk_zero, refl, },
    { rw [nat.succ_eq_add_one],
      push_cast,
      rw [ih, ‚Üê localization.mk_one, localization.add_mk, mul_one, one_mul, add_comm],
      congr' 1,
      erw one_mul,
      refl, },
  end, localization.mk_mul, one_mul],
  congr' 1,
  simp only [‚Üê subtype.val_eq_coe],
  rw [show ‚Üën * y.num.val = ‚Üë(n : ‚Ñ§) * y.num.val, by norm_cast, ‚Üê zsmul_eq_mul],
  refl,
end

lemma zsmul_val (y : homogeneous_localization x) (n : ‚Ñ§) :
  (n ‚Ä¢ y).val = n ‚Ä¢ y.val :=
begin
  induction y using quotient.induction_on,
  unfold homogeneous_localization.val has_scalar.smul,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = n ‚Ä¢ localization.mk _ _,
  dsimp only,
  rw [zsmul_eq_mul, show (n : at x) = localization.mk n 1, begin
    induction n using int.induction_on with n ih n ih,
    { erw localization.mk_zero, refl, },
    { push_cast,
      erw [ih, ‚Üê localization.mk_one, localization.add_mk, mul_one, one_mul, add_comm, one_mul],
      congr' 1, },
    { push_cast at ih ‚ä¢,
      rw neg_eq_iff_neg_eq at ih,
      erw [show -(n : at x) - 1 = - (n + 1), by ring, ‚Üê ih,
        show -(n : A) - 1 = - (n + 1), by ring, localization.neg_mk, neg_neg, ‚Üê localization.mk_one,
        localization.add_mk, one_mul, mul_one, localization.neg_mk, one_mul, add_comm], },
  end, localization.mk_mul, one_mul],
  congr' 1,
  simp only [‚Üê subtype.val_eq_coe],
  rw [‚Üê zsmul_eq_mul],
  refl,
end

lemma pow_val (y : homogeneous_localization x) (n : ‚Ñï) :
  (y ^ n).val = y.val ^ n :=
begin
  induction y using quotient.induction_on,
  unfold homogeneous_localization.val has_pow.pow,
  simp only [quotient.lift_on‚ÇÇ'_mk, quotient.lift_on'_mk],
  change localization.mk _ _ = (localization.mk _ _) ^ n,
  rw localization.mk_pow,
  dsimp only,
  congr' 1,
end

instance : comm_ring (homogeneous_localization x) :=
(homogeneous_localization.val_injective x).comm_ring _ zero_val one_val add_val mul_val neg_val
  sub_val nsmul_val zsmul_val pow_val
end homogeneous_localization

namespace homogeneous_localization
variables {ùíú} {x : projective_spectrum.Top ùíú}

/-- numerator of an element in `homogeneous_localization x`-/
def homogeneous_localization.num (f : homogeneous_localization x) : A :=
(quotient.out' f).num

/-- denominator of an element in `homogeneous_localization x`-/
def homogeneous_localization.denom (f : homogeneous_localization x) : A :=
(quotient.out' f).denom

/-- For an element in `homogeneous_localization x`, degree is the natural number `i` such that
  `ùíú i` contains both numerator and denominator. -/
def homogeneous_localization.deg (f : homogeneous_localization x) : ‚Ñï :=
(quotient.out' f).deg

lemma homogeneous_localization.denom_not_mem (f : homogeneous_localization x) :
  f.denom ‚àâ x.as_homogeneous_ideal :=
(quotient.out' f).denom_not_mem

lemma homogeneous_localization.num_mem (f : homogeneous_localization x) : f.num ‚àà ùíú f.deg :=
(quotient.out' f).num.2

lemma homogeneous_localization.denom_mem (f : homogeneous_localization x) : f.denom ‚àà ùíú f.deg :=
(quotient.out' f).denom.2

lemma homogeneous_localization.eq' (f : homogeneous_localization x) :
  f = quotient.mk' (quotient.out' f) :=
(quotient.out_eq' f).symm

lemma homogeneous_localization.eq_num_div_denom (f : homogeneous_localization x) :
  f.val = localization.mk f.num ‚ü®f.denom, f.denom_not_mem‚ü© :=
begin
  have := (quotient.out_eq' f).symm,
  apply_fun homogeneous_localization.val at this,
  rw this,
  unfold homogeneous_localization.val,
  simp only [quotient.lift_on'_mk'],
  refl,
end

lemma ext_iff_val (f g : homogeneous_localization x) : f = g ‚Üî f.val = g.val :=
{ mp := Œª h, h ‚ñ∏ rfl,
  mpr := Œª h, begin
    induction f using quotient.induction_on,
    induction g using quotient.induction_on,
    rw quotient.eq,
    unfold homogeneous_localization.val at h,
    simp only [quotient.lift_on'_mk] at h,
    exact h,
  end }

end homogeneous_localization

end projective_spectrum.structure_sheaf

end algebraic_geometry
