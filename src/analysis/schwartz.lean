/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import analysis.calculus.cont_diff
import analysis.normed_space.basic
import analysis.locally_convex.with_seminorms
import analysis.normed_space.multilinear
import topology.algebra.uniform_filter_basis
import analysis.inner_product_space.basic
import tactic.positivity
import algebra.order.pointwise

/-!
# Schwartz space

## Main definitions

* `schwartz`

## Main statements

* `foo_bar_unique`

## Notation



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

## Tags

Foobars, barfoos
-/

open filter
open_locale big_operators ennreal nnreal topological_space

noncomputable theory

variables {R R' 𝕜 E F ι : Type*}


section iterated_fderiv

variables [nontrivially_normed_field 𝕜]
variables [normed_add_comm_group E] [normed_space 𝕜 E]
variables [normed_add_comm_group F] [normed_space 𝕜 F]

@[simp] lemma iterated_fderiv_zero_map_apply {n : ℕ} {x : E} :
  iterated_fderiv 𝕜 n (0 : E → F) x = 0 :=
(congr_fun iterated_fderiv_within_zero_fun x).trans (pi.zero_apply _)

lemma cont_diff.differentiable_at_iterated_fderiv {n k : ℕ} {f : E → F} (hf : cont_diff 𝕜 n f)
  (h : k < n):
  differentiable 𝕜 (iterated_fderiv 𝕜 k f) :=
(cont_diff_iff_continuous_differentiable.mp hf).2 k (by simp only [h, with_top.coe_lt_coe])

-- iterated_fderiv_add
lemma iterated_fderiv_within_add {n : ℕ} {f g : E → F} {s : set E} (hs : is_open s)
  (hf : cont_diff_on 𝕜 n f s) (hg : cont_diff_on 𝕜 n g s) :
  ∀ (x : E) (hx : x ∈ s),
  iterated_fderiv_within 𝕜 n (f + g) s x =
  iterated_fderiv_within 𝕜 n f s x + iterated_fderiv_within 𝕜 n g s x :=
begin
  induction n with k hk,
  { intros x hx, ext, simp },
  specialize hk (hf.of_le $ with_top.coe_le_coe.mpr $ k.le_succ),
  specialize hk (hg.of_le $ with_top.coe_le_coe.mpr $ k.le_succ),
  intros x hx,
  -- Using linearity of the multilinear map:
  ext m,
  rw [continuous_multilinear_map.add_apply],
  simp_rw iterated_fderiv_within_succ_apply_left m,
  rw [←continuous_multilinear_map.add_apply],
  congr,
  rw ←continuous_linear_map.add_apply,
  congr,
  -- Using linearity of `fderiv`:
  have hf' : differentiable_within_at 𝕜 (iterated_fderiv_within 𝕜 k f s) s x :=
  ((hf.differentiable_on_iterated_fderiv_within (by simp [lt_add_one k]) hs.unique_diff_on)
    .differentiable_at (hs.mem_nhds hx))
    .differentiable_within_at,
  have hg' : differentiable_within_at 𝕜 (iterated_fderiv_within 𝕜 k g s) s x :=
  ((hg.differentiable_on_iterated_fderiv_within (by simp [lt_add_one k]) hs.unique_diff_on)
    .differentiable_at (hs.mem_nhds hx))
    .differentiable_within_at,
  let hs' : unique_diff_within_at 𝕜 s x := hs.unique_diff_on.unique_diff_within_at hx,
  rw ←fderiv_within_add hs' hf' hg',
  exact fderiv_within_congr hs' hk (hk x hx),
end

-- iterated_fderiv_add
lemma iterated_fderiv_add_apply {n : ℕ} {f g : E → F} {x : E} (hf : cont_diff 𝕜 n f)
  (hg : cont_diff 𝕜 n g):
  iterated_fderiv 𝕜 n (f + g) x = iterated_fderiv 𝕜 n f x + iterated_fderiv 𝕜 n g x :=
begin
  simp_rw [←cont_diff_on_univ, ←iterated_fderiv_within_univ] at *,
  exact iterated_fderiv_within_add is_open_univ hf hg _ (set.mem_univ _),
end

lemma iterated_fderiv_add {n : ℕ} {f g : E → F} (hf : cont_diff 𝕜 n f)
  (hg : cont_diff 𝕜 n g):
  iterated_fderiv 𝕜 n (f + g) = iterated_fderiv 𝕜 n f + iterated_fderiv 𝕜 n g :=
funext (λ _, iterated_fderiv_add_apply hf hg)

-- iterated_fderiv_add
lemma iterated_fderiv_neg {n : ℕ} {f : E → F} :
  iterated_fderiv 𝕜 n (-f) = -iterated_fderiv 𝕜 n f :=
begin
  induction n with k hk,
  { ext, simp },
  ext x m,
  rw [pi.neg_apply, continuous_multilinear_map.neg_apply],
  simp_rw iterated_fderiv_succ_apply_left m,
  rw [←continuous_multilinear_map.neg_apply],
  congr,
  rw ←continuous_linear_map.neg_apply,
  congr,
  rw ←fderiv_neg,
  congr,
  ext,
  rw hk,
  rw pi.neg_apply,
end

lemma iterated_fderiv_neg_apply {n : ℕ} {f : E → F} {x : E}  :
  iterated_fderiv 𝕜 n (-f) x = -iterated_fderiv 𝕜 n f x :=
congr_fun iterated_fderiv_neg x

variables [semiring R] [module R F] [smul_comm_class 𝕜 R F] [has_continuous_const_smul R F]

lemma smul_continuous_multilinear_map {k : ℕ} {c : R}
  (m : continuous_multilinear_map 𝕜 (λ (i : fin k), E) F):
  (c • continuous_linear_map.id 𝕜 F).comp_continuous_multilinear_map m = c • m :=
by { ext x, simp }

instance {k : ℕ}: has_continuous_const_smul R (continuous_multilinear_map 𝕜 (λ (i : fin k), E) F) :=
⟨λ c, begin
  simp_rw ←smul_continuous_multilinear_map,
  refine (continuous_linear_map.comp_continuous_multilinear_mapL 𝕜 _ F F
    (c • continuous_linear_map.id 𝕜 F)).2,
end⟩

-- iterated_fderiv_const_smul
lemma iterated_fderiv_const_smul {n : ℕ} {f : E → F} (hf : cont_diff 𝕜 n f) (c : R) :
  iterated_fderiv 𝕜 n (λ y, c • f y) = c • iterated_fderiv 𝕜 n f :=
begin
  induction n with k hk,
  { ext, simp },
  specialize hk (hf.of_le $ with_top.coe_le_coe.mpr $ k.le_succ),
  ext x m,
  rw [pi.smul_apply, continuous_multilinear_map.smul_apply],
  simp_rw iterated_fderiv_succ_apply_left m,
  rw [←continuous_multilinear_map.smul_apply],
  congr,
  rw ←continuous_linear_map.smul_apply,
  congr,
  have hf' : differentiable_at 𝕜 (iterated_fderiv 𝕜 k f) x :=
  (cont_diff.differentiable_at_iterated_fderiv hf (lt_add_one k)).differentiable_at,
  rw ←fderiv_const_smul hf',
  congr,
  exact hk,
end

lemma iterated_fderiv_const_smul_apply {n : ℕ} {f : E → F} {x : E} (hf : cont_diff 𝕜 n f) (c : R) :
  iterated_fderiv 𝕜 n (λ y, c • f y) x = c • iterated_fderiv 𝕜 n f x :=
(congr_fun (iterated_fderiv_const_smul hf c) x)

variables {n : with_top ℕ} (c : R)

/- The scalar multiplication is smooth. -/
lemma cont_diff_const_smul {c : R} : cont_diff 𝕜 n (λ p : F, c • p) :=
(c • continuous_linear_map.id 𝕜 F).cont_diff

lemma cont_diff_within_at.const_smul {n : with_top ℕ} {f : E → F} {s : set E} {x : E} (c : R)
  (hf : cont_diff_within_at 𝕜 n f s x) : cont_diff_within_at 𝕜 n (λ y, c • f y) s x :=
cont_diff_const_smul.cont_diff_within_at.comp x hf set.subset_preimage_univ

lemma cont_diff.const_smul {n : with_top ℕ} {f : E → F} (c : R)
  (hf : cont_diff 𝕜 n f) : cont_diff 𝕜 n (λ y, c • f y) :=
begin
  rw cont_diff_iff_cont_diff_at at hf ⊢,
  intro x,
  specialize hf x,
  rw ←cont_diff_within_at_univ at hf ⊢,
  exact hf.const_smul _,
end

end iterated_fderiv

variables [normed_add_comm_group E] [normed_space ℝ E]
variables [normed_add_comm_group F] [normed_space ℝ F]

variables (E F)

/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of ∥x∥. -/
structure schwartz :=
  (to_fun : E → F)
  (smooth' : cont_diff ℝ ⊤ to_fun)
  (decay' : ∀ (k n : ℕ), ∃ (C : ℝ) (hC : 0 < C), ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n to_fun x∥ ≤ C)

notation `𝓢(` E `, ` F `)` := schwartz E F

variables {E F}

namespace schwartz

-- General nonsense for `fun_like` structures

instance : has_coe 𝓢(E, F) (E → F) := ⟨to_fun⟩

instance fun_like : fun_like 𝓢(E, F) E (λ _, F) :=
{ coe := λ f, f.to_fun,
  coe_injective' := λ f g h, by cases f; cases g; congr' }

/-- Helper instance for when there's too many metavariables to apply `fun_like.has_coe_to_fun`. -/
instance : has_coe_to_fun 𝓢(E, F) (λ _, E → F) := ⟨λ p, p.to_fun⟩

def decay (f : 𝓢(E, F)) (k n : ℕ) : ∃ (C : ℝ) (hC : 0 < C),
  ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ C :=
f.decay' k n

def smooth (f : 𝓢(E, F)) : cont_diff ℝ ⊤ f := f.smooth'

@[ext] lemma ext {f g : 𝓢(E, F)} (h : ∀ x, (f : E → F) x = g x) : f = g := fun_like.ext f g h

section aux

lemma seminorm_add_le_aux (k n : ℕ) (f g : 𝓢(E, F)) (x : E) :
  ∥x∥^k * ∥iterated_fderiv ℝ n (f+g) x∥ ≤
  ∥x∥^k * ∥iterated_fderiv ℝ n f x∥
  + ∥x∥^k * ∥iterated_fderiv ℝ n g x∥ :=
begin
  rw ←mul_add,
  refine mul_le_mul_of_nonneg_left _ (by positivity),
  convert norm_add_le _ _,
  refine iterated_fderiv_add_apply _ _,
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
  { exact g.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

variables [is_R_or_C 𝕜] [normed_space 𝕜 F]
variables [semiring R] [module R 𝕜] [module R F] [smul_comm_class ℝ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R 𝕜 F]

lemma seminorm_smul_aux (k n : ℕ) (f : 𝓢(E, F)) (c : R) (x : E) :
  ∥x∥ ^ k * ∥iterated_fderiv ℝ n (λ y, c • f y) x∥ =
  ∥c • (1 : 𝕜)∥ * ∥x∥ ^ k * ∥iterated_fderiv ℝ n f x∥ :=
begin
  nth_rewrite 2 mul_comm,
  rw mul_assoc,
  congr,
  rw iterated_fderiv_const_smul_apply ,
  { rw ←smul_one_smul 𝕜 c,
    rw norm_smul,
    apply_instance },
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

lemma seminorm_neg_aux (k n : ℕ) (f : 𝓢(E, F)) (x : E) :
  ∥x∥ ^ k * ∥iterated_fderiv ℝ n (-f) x∥ = ∥x∥ ^ k * ∥iterated_fderiv ℝ n f x∥ :=
begin
  nth_rewrite 3 ←norm_neg,
  congr,
  exact iterated_fderiv_neg_apply,
end

variables [normed_space ℂ F]

lemma seminorm_smul_aux' (k n : ℕ) (f : 𝓢(E, F)) (c : ℂ) (x : E) :
  ∥x∥ ^ k * ∥iterated_fderiv ℝ n (λ y, c • f y) x∥ =
  ∥c∥ * ∥x∥ ^ k * ∥iterated_fderiv ℝ n f x∥ :=
begin
  nth_rewrite 2 mul_comm,
  rw mul_assoc,
  congr,
  rw iterated_fderiv_const_smul_apply ,
  { rw norm_smul },
  { exact f.smooth.of_le (le_of_lt $ with_top.coe_lt_top _) },
end

end aux

section smul

variables [normed_space ℂ F]
variables [semiring R] [module R ℂ] [module R F] [smul_comm_class ℝ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R ℂ F]
variables [semiring R'] [module R' ℂ] [module R' F] [smul_comm_class ℝ R' F]
variables [has_continuous_const_smul R' F] [is_scalar_tower R' ℂ F]

--instance (𝕜 : Type*) [is_R_or_C 𝕜] [normed_space 𝕜 F] [module R 𝕜] [is_scalar_tower R 𝕜 F]:
-- Note that we define the scalar multiplication only in the case that `F` is a vector space
-- over `ℂ`. The reason for this is that the type-system cannot infer instances if we were to
-- replace `ℂ` by `[is_R_or_C 𝕜]`. This is mathemically no problem, because the usual Schwartz
-- space is `𝓢(E, ℂ)` and the space `𝓢(E, ℝ)` is never used in mathematics.
instance : has_smul R 𝓢(E, F) :=
⟨λ c f, { to_fun := c • f,
  smooth' := f.smooth.const_smul c,
  decay' := λ k n, begin
    rcases f.decay k n with ⟨C, hC, hf⟩,
    refine ⟨C * (∥c • (1 : ℂ)∥+1), by positivity, _⟩,
    intros x,
    specialize hf x,
    have hc : 0 ≤ ∥c • (1 : ℂ)∥ := by positivity,
    refine le_trans _ ((mul_le_mul_of_nonneg_right hf hc).trans _),
    { refine eq.le _,
      nth_rewrite 1 mul_comm,
      rw ←mul_assoc,
      refine seminorm_smul_aux k n f c x },
    rw [mul_le_mul_left hC, le_add_iff_nonneg_right],
    exact zero_le_one,
  end}⟩

lemma coe_smul {f : 𝓢(E, F)} {c : R} : c • (f : E → F) = c • f := rfl

@[simp] lemma smul_apply {f : 𝓢(E, F)} {c : R} {x : E} : (c • f) x = c • (f x) := rfl

instance [has_smul R R'] [is_scalar_tower R R' F] : is_scalar_tower R R' 𝓢(E, F) :=
⟨λ a b f, ext $ λ x, smul_assoc a b (f x)⟩

instance [smul_comm_class R R' F] : smul_comm_class R R' 𝓢(E, F) :=
⟨λ a b f, ext $ λ x, smul_comm a b (f x)⟩

end smul

section zero

instance : has_zero 𝓢(E, F) :=
⟨{ to_fun := λ _, 0,
  smooth' := cont_diff_const,
  decay' := λ k n, ⟨1, zero_lt_one, λ _, by simp [iterated_fderiv_within_zero_fun]⟩ }⟩
-- todo: `iterated_fderiv_within_zero_fun` should be `simp`
-- (and be called `iterated_fderiv_zero_fun`)

lemma coe_zero : ↑(0 : 𝓢(E, F)) = (0 : E → F) := rfl

@[simp] lemma coe_fn_zero : coe_fn (0 : 𝓢(E, F)) = (0 : E → F) := rfl

@[simp] lemma zero_apply {x : E} : (0 : 𝓢(E, F)) x = 0 := rfl

end zero

section neg

instance : has_neg 𝓢(E, F) :=
⟨λ f, ⟨-f, f.smooth.neg,
  begin
    intros k n,
    rcases f.decay k n with ⟨C, hC, hf⟩,
    use [C, hC],
    intro x,
    refine le_trans (eq.le _) (hf x),
    exact seminorm_neg_aux k n f x,
  end⟩ ⟩

end neg

section add

instance : has_add 𝓢(E, F) :=
⟨λ f g, ⟨f + g, f.smooth.add g.smooth,
  begin
    intros k n,
    rcases f.decay k n with ⟨Cf, hCf, hf⟩,
    rcases g.decay k n with ⟨Cg, hCg, hg⟩,
    refine ⟨Cf + Cg, by positivity, λ x, _⟩,
    specialize hf x,
    specialize hg x,
    refine le_trans _ (add_le_add hf hg),
    exact seminorm_add_le_aux k n f g x,
  end⟩ ⟩

lemma coe_add (f g : 𝓢(E, F)) : (f : E → F) + g = f + g := rfl

@[simp] lemma add_apply {f g : 𝓢(E, F)} {x : E} : (f + g) x = f x + g x := rfl

end add

instance : has_sub 𝓢(E, F) :=
⟨λ f g, ⟨f - g, f.smooth.sub g.smooth,
  begin
    intros k n,
    rcases f.decay k n with ⟨Cf, hCf, hf⟩,
    rcases g.decay k n with ⟨Cg, hCg, hg⟩,
    refine ⟨Cf + Cg, by positivity, λ x, _⟩,
    specialize hf x,
    specialize hg x,
    refine le_trans _ (add_le_add hf hg),
    rw sub_eq_add_neg,
    rw ←seminorm_neg_aux k n g x,
    convert seminorm_add_le_aux k n f (-g) x, -- for some reason exact fails with timeout
  end⟩ ⟩

@[simp] lemma sub_apply {f g : 𝓢(E, F)} {x : E} : (f - g) x = f x - g x := rfl

variables [normed_space ℂ F]

instance : add_comm_group 𝓢(E, F) :=
fun_like.coe_injective.add_comm_group _ rfl (λ _ _, rfl) (λ _, rfl) (λ _ _, rfl) (λ _ _, rfl)
  (λ _ _, rfl)

variables (E F)

/-- Coercion as an additive homomorphism. -/
def coe_hom : 𝓢(E, F) →+ (E → F) :=
{ to_fun := λ f, f, map_zero' := coe_zero, map_add' := coe_add }

variables {E F}

lemma coe_coe_hom : (coe_hom E F : 𝓢(E, F) → (E → F)) = coe_fn := rfl

lemma coe_hom_injective : function.injective (coe_hom E F) :=
by { rw coe_coe_hom, exact fun_like.coe_injective }

section module

variables [normed_space ℂ F]
variables [semiring R] [module R ℂ] [module R F] [smul_comm_class ℝ R F]
variables [has_continuous_const_smul R F] [is_scalar_tower R ℂ F]

--variables [semiring R] [module R ℝ] [module R F] [smul_comm_class ℝ R F]
--variables [has_continuous_const_smul R F] [is_scalar_tower R ℝ F]

instance : module R 𝓢(E, F) :=
coe_hom_injective.module R (coe_hom E F) (λ _ _, rfl)

end module

section seminorms

variables [has_smul ℝ F]

@[protected]
def seminorm_aux (k n : ℕ) (f : 𝓢(E, F)) : ℝ :=
Inf {c | 0 ≤ c ∧ ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ c}

lemma bounds_nonempty (k n : ℕ) (f : 𝓢(E, F)) :
  ∃ (c : ℝ), c ∈ {c : ℝ | 0 ≤ c ∧ ∀ (x : E), ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ c} :=
let ⟨M, hMp, hMb⟩ := f.decay k n in ⟨M, le_of_lt hMp, hMb⟩

lemma bounds_bdd_below (k n : ℕ) (f : 𝓢(E, F)) :
  bdd_below { c | 0 ≤ c ∧ ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ c } :=
⟨0, λ _ ⟨hn, _⟩, hn⟩

lemma seminorm_aux_nonneg (k n : ℕ) (f : 𝓢(E, F)) : 0 ≤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (λ _ ⟨hx, _⟩, hx)

lemma le_seminorm_aux (k n : ℕ) (f : 𝓢(E, F)) (x : E) :
  ∥x∥ ^ k * ∥iterated_fderiv ℝ n ⇑f x∥ ≤ f.seminorm_aux k n :=
le_cInf (bounds_nonempty k n f) (λ y ⟨_, h⟩, h x)


section

open tactic tactic.positivity

/-- Extension for the `positivity` tactic: seminorms are nonnegative. -/
@[positivity]
meta def _root_.tactic.positivity_schwartz_seminorm : expr → tactic strictness
| `(schwartz.seminorm_aux %%a %%b %%c) := nonnegative <$> mk_app ``seminorm_aux_nonneg [a, b, c]
| _ := failed

end

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma seminorm_aux_le_bound (k n : ℕ) (f : 𝓢(E, F)) {M : ℝ} (hMp: 0 ≤ M)
  (hM : ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ M) :
  f.seminorm_aux k n ≤ M :=
cInf_le (bounds_bdd_below k n f) ⟨hMp, hM⟩

lemma seminorm_aux_zero (k n : ℕ) :
  (0 : 𝓢(E, F)).seminorm_aux k n = 0 :=
le_antisymm (seminorm_aux_le_bound k n _ rfl.le (λ _, by simp)) (by positivity)

lemma seminorm_aux_add_le (k n : ℕ) (f g : 𝓢(E, F)) :
  (f + g).seminorm_aux k n ≤ f.seminorm_aux k n + g.seminorm_aux k n :=
(f + g).seminorm_aux_le_bound k n (by positivity) $ λ x, (seminorm_add_le_aux k n f g x).trans $
  add_le_add (f.le_seminorm_aux k n x) (g.le_seminorm_aux k n x)

lemma seminorm_aux_smul_le (k n : ℕ) (r : ℂ) (f : 𝓢(E, F)) :
  (r • f).seminorm_aux k n ≤ ∥r∥ * f.seminorm_aux k n :=
begin
  refine (r • f).seminorm_aux_le_bound k n (by positivity) (λ x, _),
  refine (seminorm_smul_aux' k n f r x).le.trans _,
  rw mul_assoc,
  refine mul_le_mul_of_nonneg_left (f.le_seminorm_aux k n x) (norm_nonneg _),
end

lemma seminorm_aux_neg_le (k n : ℕ) (f : 𝓢(E, F)) :
  (-f).seminorm_aux k n ≤ f.seminorm_aux k n :=
seminorm_aux_le_bound k n (-f) (by positivity)
  (λ x, (seminorm_neg_aux k n f x).le.trans (le_seminorm_aux k n f x))

lemma seminorm_aux_neg (k n : ℕ) (f : 𝓢(E, F)) :
  (-f).seminorm_aux k n = f.seminorm_aux k n :=
begin
  refine (f.seminorm_aux_neg_le k n).antisymm _,
  nth_rewrite 0 ←neg_neg f,
  exact (-f).seminorm_aux_neg_le k n,
end

@[protected]
def seminorm (k n : ℕ) : seminorm ℂ 𝓢(E, F) := seminorm.of_le (schwartz.seminorm_aux k n)
  (schwartz.seminorm_aux_zero k n)
  (schwartz.seminorm_aux_add_le k n)
  (schwartz.seminorm_aux_neg k n)
  (schwartz.seminorm_aux_smul_le k n)

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
lemma seminorm_le_bound (k n : ℕ) (f : 𝓢(E, F)) {M : ℝ} (hMp: 0 ≤ M)
  (hM : ∀ x, ∥x∥^k * ∥iterated_fderiv ℝ n f x∥ ≤ M) :
  schwartz.seminorm k n f ≤ M := f.seminorm_aux_le_bound k n hMp hM

lemma le_seminorm (k n : ℕ) (f : 𝓢(E, F)) (x : E) :
  ∥x∥ ^ k * ∥iterated_fderiv ℝ n ⇑f x∥ ≤ schwartz.seminorm k n f := f.le_seminorm_aux k n x

end seminorms

variables (E F)

def _root_.schwartz_seminorm_family : seminorm_family ℂ 𝓢(E, F) (ℕ × ℕ) := λ n, schwartz.seminorm n.1 n.2

variables {E F}

instance : topological_space 𝓢(E, F) := (schwartz_seminorm_family E F).module_filter_basis.topology'

instance : has_continuous_smul ℂ 𝓢(E, F) :=
  (schwartz_seminorm_family E F).module_filter_basis.has_continuous_smul

instance : topological_add_group 𝓢(E, F) :=
  (schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis.is_topological_add_group

instance : uniform_space 𝓢(E, F) :=
  (schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis.uniform_space

instance : uniform_add_group 𝓢(E, F) :=
  (schwartz_seminorm_family E F).module_filter_basis.to_add_group_filter_basis.uniform_add_group

variables (E F)

lemma _root_.schwartz_with_seminorms : with_seminorms (schwartz_seminorm_family E F) := ⟨rfl⟩

variables {E F}

instance : locally_convex_space ℝ 𝓢(E, F) :=
  seminorm_family.to_locally_convex_space (schwartz_with_seminorms E F)

variables (f g : 𝓢(E, F)) (x : E) (c : ℂ)
variables (fi : ℕ → 𝓢(E, F)) (T : 𝓢(E, F) →L[ℝ] 𝓢(E, F)) (φ : 𝓢(E, F) →L[ℝ] ℂ)

end schwartz
