/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import analysis.special_functions.gamma
import analysis.special_functions.polar_coord

/-!
# Gaussian integral

We prove the formula `‚à´ x, exp (-b * x^2) = sqrt (œÄ / b)`, in `integral_gaussian`.
-/

noncomputable theory

namespace complex

lemma mul_of_real_re (z : ‚ÑÇ) (x : ‚Ñù) : (z * x).re = z.re * x :=
by simp only [mul_re, of_real_re, of_real_im, mul_zero, tsub_zero]

end complex

open real set measure_theory filter asymptotics
open complex (hiding exp continuous_exp abs_of_nonneg)
notation `cexp` := complex.exp
open_locale real topological_space

lemma exp_neg_mul_sq_is_o_exp_neg {b : ‚Ñù} (hb : 0 < b) :
  (Œª x:‚Ñù, exp (-b * x^2)) =o[at_top] (Œª x:‚Ñù, exp (-x)) :=
begin
  have A : (Œª (x : ‚Ñù), -x - -b * x ^ 2) = (Œª x, x * (b * x + (- 1))), by { ext x, ring },
  rw [is_o_exp_comp_exp_comp, A],
  apply tendsto.at_top_mul_at_top tendsto_id,
  apply tendsto_at_top_add_const_right at_top (-1 : ‚Ñù),
  exact tendsto.const_mul_at_top hb tendsto_id,
end

lemma rpow_mul_exp_neg_mul_sq_is_o_exp_neg {b : ‚Ñù} (hb : 0 < b) (s : ‚Ñù) :
  (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) =o[at_top] (Œª x:‚Ñù, exp (-(1/2) * x)) :=
begin
  apply ((is_O_refl (Œª x:‚Ñù, x ^ s) at_top).mul_is_o (exp_neg_mul_sq_is_o_exp_neg hb)).trans,
  convert Gamma_integrand_is_o s,
  simp_rw [mul_comm],
end

lemma integrable_on_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
  integrable_on (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) (Ioi 0) :=
begin
  rw [‚Üê Ioc_union_Ioi_eq_Ioi (zero_le_one : (0 : ‚Ñù) ‚â§ 1), integrable_on_union],
  split,
  { rw [‚Üêintegrable_on_Icc_iff_integrable_on_Ioc],
    refine integrable_on.mul_continuous_on _ _ is_compact_Icc,
    { refine (interval_integrable_iff_integrable_Icc_of_le zero_le_one).mp _,
      exact interval_integral.interval_integrable_rpow' hs },
    { exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_on } },
  { have B : (0 : ‚Ñù) < 1/2, by norm_num,
    apply integrable_of_is_O_exp_neg B _ (is_o.is_O (rpow_mul_exp_neg_mul_sq_is_o_exp_neg hb _)),
    assume x hx,
    have N : x ‚â† 0, { refine (zero_lt_one.trans_le _).ne', exact hx },
    apply ((continuous_at_rpow_const _ _ (or.inl N)).mul _).continuous_within_at,
    exact (continuous_exp.comp (continuous_const.mul (continuous_pow 2))).continuous_at },
end

lemma integrable_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
  integrable (Œª x:‚Ñù, x ^ s * exp (-b * x^2)) :=
begin
  rw [‚Üê integrable_on_univ, ‚Üê @Iio_union_Ici _ _ (0 : ‚Ñù), integrable_on_union,
      integrable_on_Ici_iff_integrable_on_Ioi],
  refine ‚ü®_, integrable_on_rpow_mul_exp_neg_mul_sq hb hs‚ü©,
  rw ‚Üê (measure.measure_preserving_neg (volume : measure ‚Ñù)).integrable_on_comp_preimage
    ((homeomorph.neg ‚Ñù).to_measurable_equiv.measurable_embedding),
  simp only [function.comp, neg_sq, neg_preimage, preimage_neg_Iio, neg_neg, neg_zero],
  apply integrable.mono' (integrable_on_rpow_mul_exp_neg_mul_sq hb hs),
  { apply measurable.ae_strongly_measurable,
    exact (measurable_id'.neg.pow measurable_const).mul
      ((measurable_id'.pow measurable_const).const_mul (-b)).exp },
  { have : measurable_set (Ioi (0 : ‚Ñù)) := measurable_set_Ioi,
    filter_upwards [ae_restrict_mem this] with x hx,
    have h'x : 0 ‚â§ x := le_of_lt hx,
    rw [real.norm_eq_abs, abs_mul, abs_of_nonneg (exp_pos _).le],
    apply mul_le_mul_of_nonneg_right _ (exp_pos _).le,
    simpa [abs_of_nonneg h'x] using abs_rpow_le_abs_rpow (-x) s }
end

lemma integrable_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) :
  integrable (Œª x:‚Ñù, exp (-b * x^2)) :=
begin
  have A : (-1 : ‚Ñù) < 0, by norm_num,
  convert integrable_rpow_mul_exp_neg_mul_sq hb A,
  simp,
end

lemma integrable_on_Ioi_exp_neg_mul_sq_iff {b : ‚Ñù} :
  integrable_on (Œª x:‚Ñù, exp (-b * x^2)) (Ioi 0) ‚Üî 0 < b :=
begin
  refine ‚ü®Œª h, _, Œª h, (integrable_exp_neg_mul_sq h).integrable_on‚ü©,
  by_contra' hb,
  have : ‚à´‚Åª x:‚Ñù in Ioi 0, 1 ‚â§ ‚à´‚Åª x:‚Ñù in Ioi 0, ‚Äñexp (-b * x^2)‚Äñ‚Çä,
  { apply lintegral_mono (Œª x, _),
    simp only [neg_mul, ennreal.one_le_coe_iff, ‚Üê to_nnreal_one, to_nnreal_le_iff_le_coe,
      real.norm_of_nonneg (exp_pos _).le, coe_nnnorm, one_le_exp_iff, right.nonneg_neg_iff],
    exact mul_nonpos_of_nonpos_of_nonneg hb (sq_nonneg _) },
  simpa using this.trans_lt h.2,
end

lemma integrable_exp_neg_mul_sq_iff {b : ‚Ñù} : integrable (Œª x:‚Ñù, exp (-b * x^2)) ‚Üî 0 < b :=
‚ü®Œª h, integrable_on_Ioi_exp_neg_mul_sq_iff.mp h.integrable_on, integrable_exp_neg_mul_sq‚ü©

lemma integrable_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) : integrable (Œª x:‚Ñù, x * exp (-b * x^2)) :=
begin
  convert integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : ‚Ñù) < 1),
  simp,
end

lemma norm_cexp_neg_mul_sq (b : ‚ÑÇ) (x : ‚Ñù) : ‚Äñcomplex.exp (-b * x^2)‚Äñ = exp (-b.re * x^2) :=
by rw [complex.norm_eq_abs, complex.abs_exp, ‚Üêof_real_pow, mul_of_real_re, neg_re]

lemma integrable_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) : integrable (Œª x:‚Ñù, cexp (-b * x^2)) :=
begin
  refine ‚ü®(complex.continuous_exp.comp
    (continuous_const.mul (continuous_of_real.pow 2))).ae_strongly_measurable, _‚ü©,
  rw ‚Üêhas_finite_integral_norm_iff,
  simp_rw norm_cexp_neg_mul_sq,
  exact (integrable_exp_neg_mul_sq hb).2,
end

lemma integral_mul_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
  ‚à´ r:‚Ñù in Ioi 0, ‚Üër * cexp (-b * r ^ 2) = (2 * b)‚Åª¬π :=
begin
  have hb' : b ‚â† 0 := by { contrapose! hb, rw [hb, zero_re], },
  have I : integrable (Œª x:‚Ñù, ‚Üëx * cexp (-b * x^2)),
  { refine ‚ü®continuous.ae_strongly_measurable _, _‚ü©,
    { exact continuous_of_real.mul (complex.continuous_exp.comp
      (continuous_const.mul (continuous_of_real.pow 2))) },
    have := (integrable_mul_exp_neg_mul_sq hb).has_finite_integral,
    rw ‚Üêhas_finite_integral_norm_iff at this ‚ä¢,
    convert this,
    ext1 x,
    rw [norm_mul, norm_mul, norm_cexp_neg_mul_sq b, complex.norm_eq_abs, abs_of_real,
      real.norm_eq_abs, norm_of_nonneg (exp_pos _).le] },
    refine tendsto_nhds_unique
      (interval_integral_tendsto_integral_Ioi _ I.integrable_on filter.tendsto_id) _,
  have A : ‚àÄ x:‚ÑÇ, has_deriv_at (Œª x, - (2 * b)‚Åª¬π * cexp (-b * x^2)) (x * cexp (- b * x^2)) x,
  { intro x,
    convert (((has_deriv_at_pow 2 x)).const_mul (-b)).cexp.const_mul (- (2 * b)‚Åª¬π) using 1,
    field_simp [hb'],
    ring },
  have : ‚àÄ (y : ‚Ñù), ‚à´ x in 0..(id y), ‚Üëx * cexp (-b * x^2)
      = (- (2 * b)‚Åª¬π * cexp (-b * y^2)) - (- (2 * b)‚Åª¬π * cexp (-b * 0^2)) :=
    Œª y, interval_integral.integral_eq_sub_of_has_deriv_at
      (Œª x hx, (A x).comp_of_real) I.interval_integrable,
  simp_rw this,
  have L : tendsto (Œª (x : ‚Ñù), (2 * b)‚Åª¬π - (2 * b)‚Åª¬π * cexp (-b * x ^ 2)) at_top
    (ùìù ((2 * b)‚Åª¬π - (2 * b)‚Åª¬π * 0)),
  { refine tendsto_const_nhds.sub (tendsto.const_mul _ _),
    rw tendsto_zero_iff_norm_tendsto_zero,
    simp_rw norm_cexp_neg_mul_sq b,
    apply tendsto_exp_at_bot.comp,
    exact tendsto.neg_const_mul_at_top (neg_lt_zero.2 hb) (tendsto_pow_at_top two_ne_zero) },
  simpa using L,
end

lemma integral_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) :
  ‚à´ r in Ioi 0, r * exp (-b * r ^ 2) = (2 * b)‚Åª¬π :=
begin
  rw ‚Üêof_real_inj,
  convert integral_mul_cexp_neg_mul_sq (_ : 0 < (b:‚ÑÇ).re),
  { rw ‚Üêintegral_of_real, simp },
  { simp },
  { rwa of_real_re, }
end

/-- The *square* of the Gaussian integral `‚à´ x:‚Ñù, exp (-b * x^2)` is equal to `œÄ / b`. -/
lemma integral_gaussian_sq_complex {b : ‚ÑÇ} (hb : 0 < b.re) :
  (‚à´ x:‚Ñù, cexp (-b * x^2)) ^ 2 = œÄ / b :=
begin
  /- Adapted from sgouezel's proof of `integral_gaussian`. We compute `(‚à´ exp(-b x^2))^2` as an
  integral over `‚Ñù^2`, and then make a polar change of coordinates. We are left with
  `‚à´ r * exp (-b r^2)`, which has been computed in `integral_mul_cexp_neg_mul_sq` using the fact
  that this function has an obvious primitive. -/
  calc
  (‚à´ x:‚Ñù, cexp (-b * (x:‚ÑÇ)^2)) ^ 2
      = ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (-b * ((p.1) : ‚ÑÇ) ^ 2) * cexp (-b * ((p.2) : ‚ÑÇ) ^ 2) :
    by { rw [pow_two, ‚Üê integral_prod_mul], refl }
  ... = ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (- b * (p.1 ^ 2 + p.2 ^ 2)) :
    by { congr, ext1 p, rw [‚Üê complex.exp_add, mul_add], }
  ... = ‚à´ p in polar_coord.target, (p.1) ‚Ä¢ cexp (- b * ((p.1 * cos p.2) ^ 2 + (p.1 * sin p.2)^2)) :
    begin
      rw ‚Üê integral_comp_polar_coord_symm,
      simp only [polar_coord_symm_apply, of_real_mul, of_real_cos, of_real_sin],
    end
  ... = (‚à´ r in Ioi (0 : ‚Ñù), r * cexp (-b * r^2)) * (‚à´ Œ∏ in Ioo (-œÄ) œÄ, 1) :
    begin
      rw ‚Üê set_integral_prod_mul,
      congr' with p : 1,
      rw mul_one,
      congr,
      conv_rhs { rw [‚Üê one_mul ((p.1 : ‚ÑÇ)^2), ‚Üê sin_sq_add_cos_sq (p.2 : ‚ÑÇ)], },
      ring_exp,
    end
  ... = ‚ÜëœÄ / b :
    begin
      have : 0 ‚â§ œÄ + œÄ, by linarith [real.pi_pos],
      simp only [integral_const, measure.restrict_apply', measurable_set_Ioo, univ_inter,
        volume_Ioo, sub_neg_eq_add, ennreal.to_real_of_real, this],
      rw [‚Üêtwo_mul, real_smul, mul_one, of_real_mul, of_real_bit0, of_real_one,
        integral_mul_cexp_neg_mul_sq hb],
      field_simp [(by { contrapose! hb, rw [hb, zero_re] } : b ‚â† 0)],
      ring,
    end
end

theorem integral_gaussian (b : ‚Ñù) : ‚à´ x, exp (-b * x^2) = sqrt (œÄ / b) :=
begin
  /- First we deal with the crazy case where `b ‚â§ 0`: then both sides vanish. -/
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos],
    { exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb },
    { simpa only [not_lt, integrable_exp_neg_mul_sq_iff] using hb } },
  /- Assume now `b > 0`. Then both sides are non-negative and their squares agree. -/
  refine (sq_eq_sq _ (sqrt_nonneg _)).1 _,
  { exact integral_nonneg (Œª x, (exp_pos _).le) },
  rw [‚Üêof_real_inj, of_real_pow, ‚Üêintegral_of_real, sq_sqrt (div_pos pi_pos hb).le, of_real_div],
  convert integral_gaussian_sq_complex (by rwa of_real_re : 0 < (b:‚ÑÇ).re),
  { ext1 x,
    rw [of_real_exp, of_real_mul, of_real_pow, of_real_neg] },
end

lemma is_connected_halfplane : is_connected {x:‚ÑÇ | 0 < re x} :=
begin
  refine (convex.is_path_connected _ (by { use 1, simp })).is_connected,
  intros x hx y hy a b ha hb hab,
  rw [mem_set_of, add_re, smul_re, smul_re, smul_eq_mul, smul_eq_mul],
  rw mem_set_of at hx hy,
  rcases lt_or_eq_of_le ha with ha|rfl,
  { exact add_pos_of_pos_of_nonneg (mul_pos ha hx) (mul_nonneg hb hy.le) },
  { rw zero_add at hab, subst hab, simpa using hy },
end

lemma is_preconnected.constant_of_finite_range
  {Œ± Œ≤ : Type} [topological_space Œ±] [topological_space Œ≤] [t1_space Œ≤]
  {S : set Œ±} (hS : is_preconnected S) {T : set Œ≤} {f : Œ± ‚Üí Œ≤}
  (hc : continuous_on f S) (hTf : finite T) (hTm : ‚àÄ (a : Œ±), a ‚àà S ‚Üí f a ‚àà T) :
  ‚àÄ (x y : Œ±), (x ‚àà S) ‚Üí (y ‚àà S) ‚Üí f x = f y :=
begin
  intros x y hx hy,
  let F : S ‚Üí T := (Œª x:S, ‚ü®f x.val, hTm x.val x.property‚ü©),
  suffices : F ‚ü®x, hx‚ü© = F ‚ü®y, hy‚ü©,
  { rw ‚Üêsubtype.coe_inj at this, exact this },
  haveI : discrete_topology T := by apply discrete_of_t1_of_finite,
  exact (is_preconnected_iff_preconnected_space.mp hS).constant
    (continuous_induced_rng.mpr $ continuous_on_iff_continuous_restrict.mp hc)
end

/-- If `f, g` are continuous functions `0 < re z`, and `(f z) ^ 2 = (g z) ^ 2` holds on this region,
then as soon as `f 1 = g 1` we have `f z = g z` for all `z`. -/
lemma eq_of_sq_eq_of_continuous {f g : ‚ÑÇ ‚Üí ‚ÑÇ}
  (h_one : f 1 = g 1) (hsq : ‚àÄ x:‚ÑÇ, 0 < re x ‚Üí (f x) ^ 2 = (g x) ^ 2)
  (hf : continuous_on f {x : ‚ÑÇ | 0 < re x}) (hg : continuous_on g {x : ‚ÑÇ | 0 < re x})
  (hg_ne : ‚àÄ x:‚ÑÇ, 0 < re x ‚Üí g x ‚â† 0)
  {z : ‚ÑÇ} (hz : 0 < re z) : f z = g z :=
begin
  let r := Œª (z : ‚ÑÇ), f z / g z,
  suffices : r z = 1, { rwa div_eq_one_iff_eq at this, exact hg_ne z hz },
  rw ‚Üê (by { rwa div_eq_one_iff_eq, apply hg_ne, simp } : r 1 = 1),
  have r_mem : ‚àÄ (x : ‚ÑÇ), 0 < re x ‚Üí r x ‚àà ({-1, 1} : set ‚ÑÇ),
  { intros x hx,
    specialize hg_ne x hx,
    specialize hsq x hx,
    rwa [mem_insert_iff, mem_singleton_iff, or.comm, ‚Üêsq_eq_one_iff, div_pow, div_eq_one_iff_eq],
    contrapose! hg_ne,
    rwa sq_eq_zero_iff at hg_ne },
  exact is_connected_halfplane.is_preconnected.constant_of_finite_range
    (hf.div hg hg_ne) (finite.of_fintype _) r_mem z 1 hz (by simp),
end

lemma continuous_at_gaussian_integral (b : ‚ÑÇ) (hb : 0 < re b) :
  continuous_at (Œª c:‚ÑÇ, ‚à´ x:‚Ñù, cexp (-c * x^2)) b :=
begin
  -- We show the integral is *differentiable* as a function of `b`, using the rather powerful
  -- theorem `has_deriv_at_integral_of_dominated_loc_of_deriv_le`, and deduce continuity from that.
  apply has_deriv_at.continuous_at,
  -- set up the variables to be used
  let Œµ := (re b) / 2,
  let f  : ‚ÑÇ ‚Üí ‚Ñù ‚Üí ‚ÑÇ := Œª (c : ‚ÑÇ) (x : ‚Ñù), cexp (-c * x ^ 2),
  let f' : ‚ÑÇ ‚Üí ‚Ñù ‚Üí ‚ÑÇ := Œª (c : ‚ÑÇ) (x : ‚Ñù), -x^2 * cexp (-c * x ^ 2),
  let bd : ‚Ñù ‚Üí ‚Ñù     := Œª (x : ‚Ñù), x ^ 2 * exp (-b.re / 2 * x ^ 2),
  -- the hypotheses
  have eps_pos : 0 < Œµ := div_pos hb two_pos,
  have f_meas : ‚àÄ·∂† (c:‚ÑÇ) in ùìù b, ae_strongly_measurable (f c) volume,
  { apply eventually_of_forall,
    intro c,
    apply continuous.ae_strongly_measurable,
    exact complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2)), },
  have f_int : integrable (f b) volume,
  { split,
    apply continuous.ae_strongly_measurable,
    exact complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2)),
    rw ‚Üêhas_finite_integral_norm_iff,
    simp_rw norm_cexp_neg_mul_sq b,
    exact (integrable_exp_neg_mul_sq hb).has_finite_integral, },
  have f'b_meas : ae_strongly_measurable (f' b) volume,
  { apply continuous.ae_strongly_measurable,
    exact (continuous_of_real.pow 2).neg.mul
      (complex.continuous_exp.comp (continuous_const.mul (continuous_of_real.pow 2))) },
  have f'_le_bd : ‚àÄ·µê (x : ‚Ñù), ‚àÄ (c : ‚ÑÇ), c ‚àà metric.ball b Œµ ‚Üí ‚Äñf' c x‚Äñ ‚â§ bd x,
  { refine ae_of_all _ (Œª x c hc, _),
    have : b.re / 2 < c.re,
    { rw [metric.mem_ball, dist_comm, dist_eq_norm_sub] at hc,
      dsimp only [Œµ] at hc,
      have := (re_le_abs $ _).trans_lt hc,
      rw sub_re at this,
      linarith },
    rw [norm_mul, norm_cexp_neg_mul_sq, norm_neg, ‚Üêof_real_pow, complex.norm_eq_abs, abs_of_real,
      abs_sq],
    apply mul_le_mul_of_nonneg_left,
    rw exp_le_exp,
    apply mul_le_mul_of_nonneg_right, linarith,
    apply sq_nonneg,
    apply sq_nonneg },
  have integrable_bd : integrable bd,
  { dsimp [bd],
    convert integrable_rpow_mul_exp_neg_mul_sq eps_pos (by norm_num : (-1 : ‚Ñù) < 2),
    ext1 x, congr' 1,
    { rw [‚Üêrpow_nat_cast, nat.cast_bit0, nat.cast_one] },
    { rw neg_div } },
  have f_der : ‚àÄ·µê x:‚Ñù, ‚àÄ (c : ‚ÑÇ), c ‚àà metric.ball b Œµ ‚Üí has_deriv_at (Œª d:‚ÑÇ, f d x) (f' c x) c,
  { refine ae_of_all _ (Œª x c hc, _),
    dsimp only [f, f'],
    conv {congr, skip, rw mul_comm, },
    refine has_deriv_at.comp c (complex.has_deriv_at_exp _) _,
    simp_rw neg_mul,
    refine has_deriv_at.neg _,
    apply has_deriv_at_mul_const, },
  exact and.elim_right (has_deriv_at_integral_of_dominated_loc_of_deriv_le eps_pos f_meas f_int
    f'b_meas f'_le_bd integrable_bd f_der),
end

theorem integral_gaussian_complex {b : ‚ÑÇ} (hb : 0 < re b) :
  ‚à´ x:‚Ñù, cexp (-b * x^2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) :=
begin
  have nv : ‚àÄ {b : ‚ÑÇ}, (0 < re b) ‚Üí (b ‚â† 0),
  { intros b hb, contrapose! hb, rw hb, simp },
  convert eq_of_sq_eq_of_continuous _ (Œª c hc, _) _ _ (Œª c hc, _) hb,
  { -- first check equality at 1
    have : ‚àÄ (x : ‚Ñù), complex.exp (-1 * x ^ 2) = real.exp (-1 * x ^ 2),
    { intro x,
      simp only [of_real_exp, neg_mul, one_mul, of_real_neg, of_real_pow] },
    simp_rw [this, integral_of_real],
    conv_rhs {  congr, rw [‚Üêof_real_one, ‚Üêof_real_div], skip,
                rw [‚Üêof_real_one, ‚Üêof_real_bit0, ‚Üêof_real_div]  },
    rw [‚Üêof_real_cpow, of_real_inj],
    convert integral_gaussian (1 : ‚Ñù),
    rwa [sqrt_eq_rpow],
    rw [div_one],
    exact pi_pos.le },
  { -- squares of both sides agree
    rw [integral_gaussian_sq_complex hc, sq],
    conv_lhs { rw ‚Üêcpow_one (‚ÜëœÄ / c)},
    rw ‚Üê cpow_add _ _ (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)),
    norm_num },
  { -- integral is continuous
    exact continuous_at.continuous_on (Œª b hb, continuous_at_gaussian_integral b hb), },
  { -- `(œÄ / b) ^ (1 / 2 : ‚ÑÇ)` is continuous
    refine continuous_at.continuous_on (Œª b hb, (continuous_at_cpow_const (or.inl _)).comp
      (continuous_at_const.div continuous_at_id (nv hb))),
    rw [div_re, of_real_im, of_real_re, zero_mul, zero_div, add_zero],
    exact div_pos (mul_pos pi_pos hb) (norm_sq_pos.mpr (nv hb)), },
  { -- RHS doesn't vanish
    rw [ne.def, cpow_eq_zero_iff, not_and_distrib],
    exact or.inl (div_ne_zero (of_real_ne_zero.mpr pi_ne_zero) (nv hc)) },
end

/- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`. -/
lemma integral_gaussian_complex_Ioi {b : ‚ÑÇ} (hb : 0 < re b) :
  ‚à´ x:‚Ñù in Ioi 0, cexp (-b * x^2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) / 2 :=
begin
  have full_integral := integral_gaussian_complex hb,
  have : measurable_set (Ioi (0:‚Ñù)) := measurable_set_Ioi,
  rw [‚Üêintegral_add_compl this (integrable_cexp_neg_mul_sq hb), compl_Ioi] at full_integral,
  suffices : ‚à´ x:‚Ñù in Iic 0, cexp (-b * x^2) = ‚à´ x:‚Ñù in Ioi 0, cexp (-b * x^2),
  { rw [this, ‚Üêmul_two] at full_integral,
    rwa eq_div_iff, exact two_ne_zero },
  have : ‚àÄ (c : ‚Ñù), ‚à´ x in 0 .. c, cexp (-b * x^2) = ‚à´ x in -c .. 0, cexp (-b * x^2),
  { intro c,
    have := @interval_integral.integral_comp_sub_left _ _ _ _ 0 c (Œª x, cexp (-b * x^2)) 0,
    simpa [zero_sub, neg_sq, neg_zero] using this },
  have t1 := interval_integral_tendsto_integral_Ioi _
     ((integrable_cexp_neg_mul_sq hb).integrable_on) tendsto_id,
  have t2 : tendsto (Œª c:‚Ñù, ‚à´ x:‚Ñù in 0..c,
    cexp (-b * x^2)) at_top (ùìù ‚à´ x:‚Ñù in Iic 0, cexp (-b * x^2)),
  { simp_rw this,
    refine interval_integral_tendsto_integral_Iic _ _ tendsto_neg_at_top_at_bot,
    apply (integrable_cexp_neg_mul_sq hb).integrable_on },
  exact tendsto_nhds_unique t2 t1,
end

/- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`. -/
lemma integral_gaussian_Ioi (b : ‚Ñù) : ‚à´ x in Ioi 0, exp (-b * x^2) = sqrt (œÄ / b) / 2 :=
begin
  rcases le_or_lt b 0 with hb|hb,
  { rw [integral_undef, sqrt_eq_zero_of_nonpos, zero_div],
    exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb,
    rwa [‚Üêintegrable_on, integrable_on_Ioi_exp_neg_mul_sq_iff, not_lt] },
  rw [‚Üêof_real_inj, ‚Üêintegral_of_real],
  convert integral_gaussian_complex_Ioi (by rwa of_real_re : 0 < (b:‚ÑÇ).re),
  { ext1 x, simp, },
  { rw [sqrt_eq_rpow, ‚Üêof_real_div, of_real_div, of_real_cpow],
    norm_num,
    exact (div_pos pi_pos hb).le, }
end

namespace complex

/-- The special-value formula `Œì(1/2) = ‚àöœÄ`, which is equivalent to the Gaussian integral. -/
lemma Gamma_one_half_eq : Gamma (1 / 2) = sqrt œÄ :=
begin
  -- first reduce to real integrals
  have hh : (1 / 2 : ‚ÑÇ) = ‚Üë(1 / 2 : ‚Ñù),
  { simp only [one_div, of_real_inv, of_real_bit0, of_real_one] },
  have hh2 : (1 / 2 : ‚ÑÇ).re = 1 / 2,
  { convert of_real_re (1 / 2 : ‚Ñù) },
  replace hh2 : 0 < (1 / 2 : ‚ÑÇ).re := by { rw hh2, exact one_half_pos, },
  rw [Gamma_eq_integral _ hh2, hh, Gamma_integral_of_real, of_real_inj, real.Gamma_integral],
  -- now do change-of-variables
  rw ‚Üêintegral_comp_rpow_Ioi_of_pos zero_lt_two,
  have : eq_on (Œª x:‚Ñù, (2 * x^((2:‚Ñù) - 1)) ‚Ä¢ (real.exp (-x^(2:‚Ñù)) * (x^(2:‚Ñù)) ^ (1 / (2:‚Ñù) - 1)))
  (Œª x:‚Ñù, 2 * real.exp ((-1) * x ^ (2:‚Ñï))) (Ioi 0),
  { intros x hx, dsimp only,
    have : (x^(2:‚Ñù)) ^ (1 / (2:‚Ñù) - 1) = x‚Åª¬π,
    { rw ‚Üêrpow_mul (le_of_lt hx), norm_num,
      rw [rpow_neg (le_of_lt hx), rpow_one] },
    rw [smul_eq_mul, this],
    field_simp [(ne_of_lt hx).symm],
    norm_num, ring },
  rw [set_integral_congr measurable_set_Ioi this, integral_mul_left, integral_gaussian_Ioi],
  field_simp, ring,
end

end complex
