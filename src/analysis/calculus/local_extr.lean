/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/

import analysis.calculus.deriv

/-! # Local extrema of smooth functions

## Main definitions

* `is_local_min f a` : `f a ‚â§ f x` in some neighborhood of `a`;
* `is_local_max f a` : `f x ‚â§ f a` in some neighborhood of `a`;
* `is_local_extr f a` : one of the above.

## Main statements

Rolle's Theorem, Lagrange's and Cauchy's Mean Value Theorems.

TODO:

* if `deriv f` is positive on an interval, then `f` is strictly increasing;
* similarly for negative/nonpositive/nonnegative;
* in particular, if `‚àÄ x, deriv f x = 0`, then `f = const`.
* possibly move some lemmas to other file(s)
-/

universes u v

open filter set
open_locale topological_space classical

section defs

variables {Œ± : Type u} {Œ≤ : Type v} [topological_space Œ±] [preorder Œ≤]
  (f : Œ± ‚Üí Œ≤) (a : Œ±)

/-- `a` is a local minimum of `f` if `f a ‚â§ f x` in a neighborhood of `a`. -/
def is_local_min : Prop := {x | f a ‚â§ f x} ‚àà ùìù a

/-- `a` is a local maximum of `f` if `f x ‚â§ f a` in a neighborhood of `a`. -/
def is_local_max : Prop := {x | f x ‚â§ f a} ‚àà ùìù a

/-- `a` is a local extremum of `f` if it is either a local minimum, or a local maximum. -/
def is_local_extr : Prop := is_local_min f a ‚à® is_local_max f a

variables {f a}

lemma is_local_min_const {b : Œ≤} : is_local_min (Œª _, b) a :=
univ_mem_sets' $ Œª _, le_refl _

lemma is_local_max_const {b : Œ≤} : is_local_max (Œª _, b) a :=
univ_mem_sets' $ Œª _, le_refl _

end defs

section ordered_comm_monoid

variables {Œ± : Type u} {G : Type v} [topological_space Œ±] [ordered_comm_monoid G]
  {f g : Œ± ‚Üí G} {a : Œ±}

lemma is_local_min.add (hf : is_local_min f a) (hg : is_local_min g a) : is_local_min (f + g) a :=
mem_sets_of_superset (inter_mem_sets hf hg) $ Œª x ‚ü®hfx, hgx‚ü©, add_le_add' hfx hgx

lemma is_local_max.add (hf : is_local_max f a) (hg : is_local_max g a) : is_local_max (f + g) a :=
mem_sets_of_superset (inter_mem_sets hf hg) $ Œª x ‚ü®hfx, hgx‚ü©, add_le_add' hfx hgx

lemma is_local_min.add_left (hf : is_local_min f a) (b : G) :
  is_local_min (Œª x, b + f x) a :=
is_local_min_const.add hf

lemma is_local_min.add_right (hf : is_local_min f a) (b : G) :
  is_local_min (Œª x, f x + b) a :=
hf.add is_local_min_const

lemma is_local_max.add_left (hf : is_local_max f a) (b : G) :
  is_local_max (Œª x, b + f x) a :=
is_local_max_const.add hf

lemma is_local_max.add_right (hf : is_local_max f a) (b : G) :
  is_local_max (Œª x, f x + b) a :=
hf.add is_local_max_const

end ordered_comm_monoid

section ordered_comm_group

variables {Œ± : Type u} {G : Type v} [topological_space Œ±] [ordered_comm_group G]
  {f g : Œ± ‚Üí G} {a : Œ±}

lemma is_local_min.neg (hf : is_local_min f a) : is_local_max (-f) a :=
mem_sets_of_superset hf $ Œª x, neg_le_neg

lemma is_local_max.neg (hf : is_local_max f a) : is_local_min (-f) a :=
mem_sets_of_superset hf $ Œª x, neg_le_neg

lemma is_local_min.sub (hf : is_local_min f a) (hg : is_local_max g a) :
  is_local_min (f - g) a :=
hf.add hg.neg

lemma is_local_max.sub (hf : is_local_max f a) (hg : is_local_min g a) :
  is_local_max (f - g) a :=
hf.add hg.neg

end ordered_comm_group

section vector_space

variables {E : Type u} [normed_group E] [normed_space ‚Ñù E] {f : E ‚Üí ‚Ñù} {a : E}
  {f' : E ‚ÜíL[‚Ñù] ‚Ñù}

lemma is_local_min.has_fderiv_at_eq_zero (h : is_local_min f a) (hf : has_fderiv_at f f' a) :
  f' = 0 :=
begin
  suffices : ‚àÄ v : E, (0:‚Ñù) ‚â§ f' v,
  { ext v,
    exact le_antisymm (by simpa using this (-v)) (this v) },
  refine Œª v, ge_of_tendsto at_top_ne_bot (hf.lim_real v) _,
  apply mp_sets (mem_at_top (1:‚Ñù)),
  have : tendsto (Œª b:‚Ñù, a + b‚Åª¬π ‚Ä¢ v) at_top (ùìù (a + (0:‚Ñù) ‚Ä¢ v)),
    from tendsto_const_nhds.add (tendsto_smul tendsto_inverse_at_top_nhds_0 tendsto_const_nhds),
  rw [zero_smul, add_zero] at this,
  apply mem_sets_of_superset (mem_map.1 $ this h),
  simp only [mem_set_of_eq, smul_eq_mul, mem_preimage],
  assume c hfc hc,
  exact mul_nonneg (le_trans zero_le_one hc) (sub_nonneg.2 hfc)
end

/-- The derivative at a local minimum equals zero. -/
lemma is_local_min.fderiv_eq_zero (h : is_local_min f a) : fderiv ‚Ñù f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_fderiv_at_eq_zero hf.has_fderiv_at
else fderiv_zero_of_not_differentiable_at hf

lemma is_local_max.has_fderiv_at_eq_zero (h : is_local_max f a) (hf : has_fderiv_at f f' a) :
  f' = 0 :=
neg_eq_zero.1 $ h.neg.has_fderiv_at_eq_zero hf.neg

lemma is_local_max.fderiv_eq_zero (h : is_local_max f a) : fderiv ‚Ñù f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_fderiv_at_eq_zero hf.has_fderiv_at
else fderiv_zero_of_not_differentiable_at hf

lemma is_local_extr.has_fderiv_at_eq_zero (h : is_local_extr f a) :
  has_fderiv_at f f' a ‚Üí f' = 0 :=
h.elim is_local_min.has_fderiv_at_eq_zero is_local_max.has_fderiv_at_eq_zero

lemma is_local_extr.fderiv_eq_zero (h : is_local_extr f a) : fderiv ‚Ñù f a = 0 :=
h.elim is_local_min.fderiv_eq_zero is_local_max.fderiv_eq_zero

end vector_space

section real

variables {f : ‚Ñù ‚Üí ‚Ñù} {f' : ‚Ñù} {a b : ‚Ñù}

lemma is_local_min.has_deriv_at_eq_zero (h : is_local_min f a) (hf : has_deriv_at f f' a) :
  f' = 0 :=
by simpa using continuous_linear_map.ext_iff.1
  (h.has_fderiv_at_eq_zero (has_deriv_at_iff_has_fderiv_at.1 hf)) 1

lemma is_local_min.deriv_eq_zero (h : is_local_min f a) : deriv f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_deriv_at_eq_zero hf.has_deriv_at
else deriv_zero_of_not_differentiable_at hf

lemma is_local_max.has_deriv_at_eq_zero (h : is_local_max f a) (hf : has_deriv_at f f' a) :
  f' = 0 :=
neg_eq_zero.1 $ h.neg.has_deriv_at_eq_zero hf.neg

lemma is_local_max.deriv_eq_zero (h : is_local_max f a) : deriv f a = 0 :=
if hf : differentiable_at ‚Ñù f a then h.has_deriv_at_eq_zero hf.has_deriv_at
else deriv_zero_of_not_differentiable_at hf

lemma is_local_extr.has_deriv_at_eq_zero (h : is_local_extr f a) :
  has_deriv_at f f' a ‚Üí f' = 0 :=
h.elim is_local_min.has_deriv_at_eq_zero is_local_max.has_deriv_at_eq_zero

lemma is_local_extr.deriv_eq_zero (h : is_local_extr f a) : deriv f a = 0 :=
h.elim is_local_min.deriv_eq_zero is_local_max.deriv_eq_zero

end real

section MVT

variables (f f' : ‚Ñù ‚Üí ‚Ñù) {a b : ‚Ñù} (hab : a < b) (hfc : continuous f) (hfI : f a = f b)

include hab hfc hfI

lemma exists_global_extr_Ioo :
  ‚àÉ c ‚àà Ioo a b, (‚àÄ x ‚àà Icc a b, f c ‚â§ f x) ‚à® (‚àÄ x ‚àà Icc a b, f x ‚â§ f c) :=
begin
  -- Consider absolute min and max points
  obtain ‚ü®c, cmem, cle‚ü© : ‚àÉ c ‚àà Icc a b, ‚àÄ x ‚àà Icc a b, f c ‚â§ f x,
    from exists_forall_le_of_compact_of_continuous f hfc (Icc a b) compact_Icc
      (ne_empty_of_mem $ left_mem_Icc.2 $ le_of_lt hab),
  obtain ‚ü®C, Cmem, Cge‚ü© : ‚àÉ C ‚àà Icc a b, ‚àÄ x ‚àà Icc a b, f x ‚â§ f C,
    from exists_forall_ge_of_compact_of_continuous f hfc (Icc a b) compact_Icc
      (ne_empty_of_mem $ left_mem_Icc.2 $ le_of_lt hab),
  by_cases hc : f c = f a,
  { by_cases hC : f C = f a,
    { have : ‚àÄ x ‚àà Icc a b, f x = f a,
        from Œª x hx, le_antisymm (hC ‚ñ∏ Cge x hx) (hc ‚ñ∏ cle x hx),
      -- `f` is a constant, so we can take any point in `Ioo a b`
      rcases dense hab with ‚ü®c', hc'‚ü©,
      refine ‚ü®c', hc', or.inl _‚ü©,
      assume x hx,
      rw [this x hx, ‚Üê hC],
      exact Cge c' ‚ü®le_of_lt hc'.1, le_of_lt hc'.2‚ü© },

    { refine ‚ü®C, ‚ü®lt_of_le_of_ne Cmem.1 $ mt _ hC, lt_of_le_of_ne Cmem.2 $ mt _ hC‚ü©, or.inr Cge‚ü©,
      exacts [Œª h, by rw h, Œª h, by rw [h, hfI]] } },
  { refine ‚ü®c, ‚ü®lt_of_le_of_ne cmem.1 $ mt _ hc, lt_of_le_of_ne cmem.2 $ mt _ hc‚ü©, or.inl cle‚ü©,
      exacts [Œª h, by rw h, Œª h, by rw [h, hfI]] }
end

lemma exists_local_extr_Ioo : ‚àÉ c ‚àà Ioo a b, is_local_extr f c :=
by rcases exists_global_extr_Ioo f hab hfc hfI with ‚ü®c, cmem, hc | hc‚ü©; use [c, cmem]; [left,right];
  exact mem_nhds_iff_exists_Ioo_subset.2 ‚ü®a, b, cmem, Œª x hx, hc _ ‚ü®le_of_lt hx.1, le_of_lt hx.2‚ü©‚ü©

variable (hff' : ‚àÄ x ‚àà Ioo a b, has_deriv_at f (f' x) x)

include hff'

/-- Rolle's Theorem `has_deriv_at` version -/
lemma exists_has_deriv_at_eq_zero :
  ‚àÉ c ‚àà Ioo a b, f' c = 0 :=
let ‚ü®c, cmem, hc‚ü© := exists_local_extr_Ioo f hab hfc hfI in
  ‚ü®c, cmem, hc.has_deriv_at_eq_zero $ hff' c cmem‚ü©

omit hfI

/-- Cauchy version of the Mean Value Theorem -/
lemma exists_ratio_has_deriv_at_eq_ratio_slope
  (g g' : ‚Ñù ‚Üí ‚Ñù) (hgc : continuous g) (hgg' : ‚àÄ x ‚àà Ioo a b, has_deriv_at g (g' x) x) :
  ‚àÉ c ‚àà Ioo a b, (g b - g a) * f' c = (f b - f a) * g' c :=
begin
  let h := Œª x, (g b - g a) * f x - (f b - f a) * g x,
  have hI : h a = h b,
  { simp only [h], ring },
  let h' := Œª x, (g b - g a) * f' x - (f b - f a) * g' x,
  have hhh' : ‚àÄ x ‚àà Ioo a b, has_deriv_at h (h' x) x,
  { assume x hx,
    convert ((has_deriv_at_const x (g b - g a)).mul (hff' x hx)).sub
      ((has_deriv_at_const x (f b - f a)).mul (hgg' x hx)),
    simp only [h', mul_zero, add_zero] },
  have hhc : continuous h,
    from (continuous_const.mul hfc).sub (continuous_const.mul hgc),
  rcases exists_has_deriv_at_eq_zero h h' hab hhc hI hhh' with ‚ü®c, cmem, hc‚ü©,
  exact ‚ü®c, cmem, sub_eq_zero.1 hc‚ü©
end

lemma exists_has_deriv_at_eq_slope : ‚àÉ c ‚àà Ioo a b, f' c = (f b - f a) / (b - a) :=
begin
  rcases exists_ratio_has_deriv_at_eq_ratio_slope f f' hab hfc hff' id 1 continuous_id (Œª x hx, has_deriv_at_id x) with ‚ü®c, cmem, hc‚ü©,
  use [c, cmem],
  simp only [id, pi.one_apply, mul_one] at hc,
  rw [‚Üê hc, mul_div_cancel_left],
  exact ne_of_gt (sub_pos.2 hab)
end

omit hff'

lemma exists_deriv_eq_zero : ‚àÉ c ‚àà Ioo a b, deriv f c = 0 :=
let ‚ü®c, cmem, hc‚ü© := exists_local_extr_Ioo f hab hfc hfI in
  ‚ü®c, cmem, hc.deriv_eq_zero‚ü©

lemma exists_ratio_deriv_eq_ratio_slope (hfd : ‚àÄ x ‚àà Ioo a b, differentiable_at ‚Ñù f x)
  (g : ‚Ñù ‚Üí ‚Ñù) (hgc : continuous g) (hgd : ‚àÄ x ‚àà Ioo a b, differentiable_at ‚Ñù g x):
  ‚àÉ c ‚àà Ioo a b, (g b - g a) * (deriv f c) = (f b - f a) * (deriv g c) :=
exists_ratio_has_deriv_at_eq_ratio_slope f (deriv f) hab hfc
  (Œª x hx, (hfd x hx).has_deriv_at) g (deriv g) hgc (Œª x hx, (hgd x hx).has_deriv_at)

lemma exists_deriv_eq_slope (hfd : ‚àÄ x ‚àà Ioo a b, differentiable_at ‚Ñù f x) :
  ‚àÉ c ‚àà Ioo a b, deriv f c = (f b - f a) / (b - a) :=
exists_has_deriv_at_eq_slope f (deriv f) hab hfc (Œª x hx, (hfd x hx).has_deriv_at)

end MVT
