/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Yury Kudryashov
-/
import analysis.convex.strict
import analysis.convex.topology
import analysis.normed_space.ordered
import analysis.normed_space.pointwise

/-!
# Strictly convex spaces

This file defines strictly convex spaces. A normed space is strictly convex if all closed balls are
strictly convex. This does **not** mean that the norm is strictly convex (in fact, it never is).

## Main definitions

`strict_convex_space`: a typeclass saying that a given normed space over a normed linear ordered
field (e.g., `‚Ñù` or `‚Ñö`) is strictly convex. The definition requires strict convexity of a closed
ball of positive radius with center at the origin; strict convexity of any other closed ball follows
from this assumption.

## Main results

In a strictly convex space, we prove

- `strict_convex_closed_ball`: a closed ball is strictly convex.
- `combo_mem_ball_of_ne`, `open_segment_subset_ball_of_ne`, `norm_combo_lt_of_ne`:
  a nontrivial convex combination of two points in a closed ball belong to the corresponding open
  ball;
- `norm_add_lt_of_not_same_ray`, `same_ray_iff_norm_add`, `dist_add_dist_eq_iff`:
  the triangle inequality `dist x y + dist y z ‚â§ dist x z` is a strict inequality unless `y` belongs
  to the segment `[x -[‚Ñù] z]`.

We also provide several lemmas that can be used as alternative constructors for `strict_convex ‚Ñù E`:

- `strict_convex_space.of_strict_convex_closed_unit_ball`: if `closed_ball (0 : E) 1` is strictly
  convex, then `E` is a strictly convex space;

- `strict_convex_space.of_norm_add`: if `‚à•x + y‚à• = ‚à•x‚à• + ‚à•y‚à•` implies `same_ray ‚Ñù x y` for all
  `x y : E`, then `E` is a strictly convex space.

## Implementation notes

While the definition is formulated for any normed linear ordered field, most of the lemmas are
formulated only for the case `ùïú = ‚Ñù`.

## Tags

convex, strictly convex
-/

open set metric
open_locale convex pointwise

/-- A *strictly convex space* is a normed space where the closed balls are strictly convex. We only
require balls of positive radius with center at the origin to be strictly convex in the definition,
then prove that any closed ball is strictly convex in `strict_convex_closed_ball` below.

See also `strict_convex_space.of_strict_convex_closed_unit_ball`. -/
class strict_convex_space (ùïú E : Type*) [normed_linear_ordered_field ùïú] [normed_group E]
  [normed_space ùïú E] : Prop :=
(strict_convex_closed_ball : ‚àÄ r : ‚Ñù, 0 < r ‚Üí strict_convex ùïú (closed_ball (0 : E) r))

variables (ùïú : Type*) {E : Type*} [normed_linear_ordered_field ùïú]
  [normed_group E] [normed_space ùïú E]

/-- A closed ball in a strictly convex space is strictly convex. -/
lemma strict_convex_closed_ball [strict_convex_space ùïú E] (x : E) (r : ‚Ñù) :
  strict_convex ùïú (closed_ball x r) :=
begin
  cases le_or_lt r 0 with hr hr,
  { exact (subsingleton_closed_ball x hr).strict_convex },
  rw ‚Üê vadd_closed_ball_zero,
  exact (strict_convex_space.strict_convex_closed_ball r hr).vadd _,
end

variables [normed_space ‚Ñù E]

/-- A real normed vector space is strictly convex provided that the unit ball is strictly convex. -/
lemma strict_convex_space.of_strict_convex_closed_unit_ball
  [linear_map.compatible_smul E E ùïú ‚Ñù] (h : strict_convex ùïú (closed_ball (0 : E) 1)) :
  strict_convex_space ùïú E :=
‚ü®Œª r hr, by simpa only [smul_closed_unit_ball_of_nonneg hr.le] using h.smul r‚ü©

/-- If `‚à•x + y‚à• = ‚à•x‚à• + ‚à•y‚à•` implies that `x y : E` are in the same ray, then `E` is a strictly
convex space. -/
lemma strict_convex_space.of_norm_add (h : ‚àÄ x y : E, ‚à•x + y‚à• = ‚à•x‚à• + ‚à•y‚à• ‚Üí same_ray ‚Ñù x y) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_strict_convex_closed_unit_ball ‚Ñù (Œª x hx y hy hne a b ha hb hab, _),
  have hx' := hx, have hy' := hy,
  rw [‚Üê closure_closed_ball, closure_eq_interior_union_frontier,
    frontier_closed_ball (0 : E) one_ne_zero] at hx hy,
  cases hx, { exact (convex_closed_ball _ _).combo_interior_self_mem_interior hx hy' ha hb.le hab },
  cases hy, { exact (convex_closed_ball _ _).combo_self_interior_mem_interior hx' hy ha.le hb hab },
  rw [interior_closed_ball (0 : E) one_ne_zero, mem_ball_zero_iff],
  have hx‚ÇÅ : ‚à•x‚à• = 1, from mem_sphere_zero_iff_norm.1 hx,
  have hy‚ÇÅ : ‚à•y‚à• = 1, from mem_sphere_zero_iff_norm.1 hy,
  have ha' : ‚à•a‚à• = a, from real.norm_of_nonneg ha.le,
  have hb' : ‚à•b‚à• = b, from real.norm_of_nonneg hb.le,
  calc ‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• < ‚à•a ‚Ä¢ x‚à• + ‚à•b ‚Ä¢ y‚à• : (norm_add_le _ _).lt_of_ne (Œª H, hne _)
  ... = 1 : by simpa only [norm_smul, hx‚ÇÅ, hy‚ÇÅ, mul_one, ha', hb'],
  simpa only [norm_smul, hx‚ÇÅ, hy‚ÇÅ, ha', hb', mul_one, smul_comm a, smul_right_inj ha.ne',
    smul_right_inj hb.ne'] using (h _ _ H).norm_smul_eq.symm
end

lemma strict_convex_space.of_norm_add_lt_aux {a b c d : ‚Ñù} (ha : 0 < a) (hab : a + b = 1)
  (hc : 0 < c) (hd : 0 < d) (hcd : c + d = 1) (hca : c ‚â§ a) {x y : E} (hy : ‚à•y‚à• ‚â§ 1)
  (hxy : ‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• < 1) :
  ‚à•c ‚Ä¢ x + d ‚Ä¢ y‚à• < 1 :=
begin
  have hbd : b ‚â§ d,
  { refine le_of_add_le_add_left (hab.trans_le _),
    rw ‚Üêhcd,
    exact add_le_add_right hca _ },
  have h‚ÇÅ : 0 < c / a := div_pos hc ha,
  have h‚ÇÇ : 0 ‚â§ d - c / a * b,
  { rw [sub_nonneg, mul_comm_div, ‚Üêle_div_iff' hc],
    exact div_le_div hd.le hbd hc hca },
  calc ‚à•c ‚Ä¢ x + d ‚Ä¢ y‚à• = ‚à•(c / a) ‚Ä¢ (a ‚Ä¢ x + b ‚Ä¢ y) + (d - c / a * b) ‚Ä¢ y‚à•
        : by rw [smul_add, ‚Üêmul_smul, ‚Üêmul_smul, div_mul_cancel _ ha.ne', sub_smul,
            add_add_sub_cancel]
    ... ‚â§ ‚à•(c / a) ‚Ä¢ (a ‚Ä¢ x + b ‚Ä¢ y)‚à• + ‚à•(d - c / a * b) ‚Ä¢ y‚à• : norm_add_le _ _
    ... = c / a * ‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• + (d - c / a * b) * ‚à•y‚à•
        : by rw [norm_smul_of_nonneg h‚ÇÅ.le, norm_smul_of_nonneg h‚ÇÇ]
    ... < c / a * 1 + (d - c / a * b) * 1
        : add_lt_add_of_lt_of_le (mul_lt_mul_of_pos_left hxy h‚ÇÅ) (mul_le_mul_of_nonneg_left hy h‚ÇÇ)
    ... = 1 : begin
      nth_rewrite 0 ‚Üêhab,
      rw [mul_add, div_mul_cancel _ ha.ne', mul_one, add_add_sub_cancel, hcd],
    end,
end

/-- Strict convexity is equivalent to `‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• < 1` for all `x` and `y` of norm at most `1`
and all strictly positive `a` and `b` such that `a + b = 1`. This shows that we only need to check
it for fixed `a` and `b`. -/
lemma strict_convex_space.of_norm_add_lt {a b : ‚Ñù} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1)
  (h : ‚àÄ x y : E, ‚à•x‚à• ‚â§ 1 ‚Üí ‚à•y‚à• ‚â§ 1 ‚Üí x ‚â† y ‚Üí ‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• < 1) :
  strict_convex_space ‚Ñù E :=
begin
  refine strict_convex_space.of_strict_convex_closed_unit_ball _ (Œª x hx y hy hxy c d hc hd hcd, _),
  rw [interior_closed_ball (0 : E) one_ne_zero, mem_ball_zero_iff],
  rw mem_closed_ball_zero_iff at hx hy,
  obtain hca | hac := le_total c a,
  { exact strict_convex_space.of_norm_add_lt_aux ha hab hc hd hcd hca hy (h _ _ hx hy hxy) },
  rw add_comm at ‚ä¢ hab hcd,
  refine strict_convex_space.of_norm_add_lt_aux hb hab hd hc hcd _ hx _,
  { refine le_of_add_le_add_right (hcd.trans_le _),
    rw ‚Üêhab,
    exact add_le_add_left hac _ },
  { rw add_comm,
    exact h _ _ hx hy hxy }
end

variables [strict_convex_space ‚Ñù E] {x y z : E} {a b r : ‚Ñù}

/-- If `x ‚â† y` belong to the same closed ball, then a convex combination of `x` and `y` with
positive coefficients belongs to the corresponding open ball. -/
lemma combo_mem_ball_of_ne (hx : x ‚àà closed_ball z r) (hy : y ‚àà closed_ball z r) (hne : x ‚â† y)
  (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) : a ‚Ä¢ x + b ‚Ä¢ y ‚àà ball z r :=
begin
  rcases eq_or_ne r 0 with rfl|hr,
  { rw [closed_ball_zero, mem_singleton_iff] at hx hy,
    exact (hne (hx.trans hy.symm)).elim },
  { simp only [‚Üê interior_closed_ball _ hr] at hx hy ‚ä¢,
    exact strict_convex_closed_ball ‚Ñù z r hx hy hne ha hb hab }
end

/-- If `x ‚â† y` belong to the same closed ball, then the open segment with endpoints `x` and `y` is
included in the corresponding open ball. -/
lemma open_segment_subset_ball_of_ne (hx : x ‚àà closed_ball z r) (hy : y ‚àà closed_ball z r)
  (hne : x ‚â† y) : open_segment ‚Ñù x y ‚äÜ ball z r :=
(open_segment_subset_iff _).2 $ Œª a b, combo_mem_ball_of_ne hx hy hne

/-- If `x` and `y` are two distinct vectors of norm at most `r`, then a convex combination of `x`
and `y` with positive coefficients has norm strictly less than `r`. -/
lemma norm_combo_lt_of_ne (hx : ‚à•x‚à• ‚â§ r) (hy : ‚à•y‚à• ‚â§ r) (hne : x ‚â† y) (ha : 0 < a) (hb : 0 < b)
  (hab : a + b = 1) : ‚à•a ‚Ä¢ x + b ‚Ä¢ y‚à• < r :=
begin
  simp only [‚Üê mem_ball_zero_iff, ‚Üê mem_closed_ball_zero_iff] at hx hy ‚ä¢,
  exact combo_mem_ball_of_ne hx hy hne ha hb hab
end

/-- In a strictly convex space, if `x` and `y` are not in the same ray, then `‚à•x + y‚à• < ‚à•x‚à• +
‚à•y‚à•`. -/
lemma norm_add_lt_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : ‚à•x + y‚à• < ‚à•x‚à• + ‚à•y‚à• :=
begin
  simp only [same_ray_iff_inv_norm_smul_eq, not_or_distrib, ‚Üê ne.def] at h,
  rcases h with ‚ü®hx, hy, hne‚ü©,
  rw ‚Üê norm_pos_iff at hx hy,
  have hxy : 0 < ‚à•x‚à• + ‚à•y‚à• := add_pos hx hy,
  have := combo_mem_ball_of_ne (inv_norm_smul_mem_closed_unit_ball x)
    (inv_norm_smul_mem_closed_unit_ball y) hne (div_pos hx hxy) (div_pos hy hxy)
    (by rw [‚Üê add_div, div_self hxy.ne']),
  rwa [mem_ball_zero_iff, div_eq_inv_mul, div_eq_inv_mul, mul_smul, mul_smul,
    smul_inv_smul‚ÇÄ hx.ne', smul_inv_smul‚ÇÄ hy.ne', ‚Üê smul_add, norm_smul,
    real.norm_of_nonneg (inv_pos.2 hxy).le, ‚Üê div_eq_inv_mul, div_lt_one hxy] at this
end

lemma lt_norm_sub_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : ‚à•x‚à• - ‚à•y‚à• < ‚à•x - y‚à• :=
begin
  nth_rewrite 0 ‚Üêsub_add_cancel x y at ‚ä¢ h,
  exact sub_lt_iff_lt_add.2 (norm_add_lt_of_not_same_ray $ Œª H', h $ H'.add_left same_ray.rfl),
end

lemma abs_lt_norm_sub_of_not_same_ray (h : ¬¨same_ray ‚Ñù x y) : |‚à•x‚à• - ‚à•y‚à•| < ‚à•x - y‚à• :=
begin
  refine abs_sub_lt_iff.2 ‚ü®lt_norm_sub_of_not_same_ray h, _‚ü©,
  rw norm_sub_rev,
  exact lt_norm_sub_of_not_same_ray (mt same_ray.symm h),
end

/-- In a strictly convex space, two vectors `x`, `y` are in the same ray if and only if the triangle
inequality for `x` and `y` becomes an equality. -/
lemma same_ray_iff_norm_add : same_ray ‚Ñù x y ‚Üî ‚à•x + y‚à• = ‚à•x‚à• + ‚à•y‚à• :=
‚ü®same_ray.norm_add, Œª h, not_not.1 $ Œª h', (norm_add_lt_of_not_same_ray h').ne h‚ü©

/-- In a strictly convex space, two vectors `x`, `y` are not in the same ray if and only if the
triangle inequality for `x` and `y` is strict. -/
lemma not_same_ray_iff_norm_add_lt : ¬¨ same_ray ‚Ñù x y ‚Üî ‚à•x + y‚à• < ‚à•x‚à• + ‚à•y‚à• :=
same_ray_iff_norm_add.not.trans (norm_add_le _ _).lt_iff_ne.symm

lemma same_ray_iff_norm_sub : same_ray ‚Ñù x y ‚Üî ‚à•x - y‚à• = |‚à•x‚à• - ‚à•y‚à•| :=
‚ü®same_ray.norm_sub, Œª h, not_not.1 $ Œª h', (abs_lt_norm_sub_of_not_same_ray h').ne' h‚ü©

lemma not_same_ray_iff_abs_lt_norm_sub : ¬¨ same_ray ‚Ñù x y ‚Üî |‚à•x‚à• - ‚à•y‚à•| < ‚à•x - y‚à• :=
same_ray_iff_norm_sub.not.trans $ ne_comm.trans (abs_norm_sub_norm_le _ _).lt_iff_ne.symm

/-- In a strictly convex space, the triangle inequality turns into an equality if and only if the
middle point belongs to the segment joining two other points. -/
lemma dist_add_dist_eq_iff : dist x y + dist y z = dist x z ‚Üî y ‚àà [x -[‚Ñù] z] :=
by simp only [mem_segment_iff_same_ray, same_ray_iff_norm_add, dist_eq_norm',
  sub_add_sub_cancel', eq_comm]

lemma norm_midpoint_lt_iff (h : ‚à•x‚à• = ‚à•y‚à•) : ‚à•(1/2 : ‚Ñù) ‚Ä¢ (x + y)‚à• < ‚à•x‚à• ‚Üî x ‚â† y :=
by rw [norm_smul, real.norm_of_nonneg (one_div_nonneg.2 zero_le_two), ‚Üêinv_eq_one_div,
    ‚Üêdiv_eq_inv_mul, div_lt_iff (@zero_lt_two ‚Ñù _ _), mul_two, ‚Üênot_same_ray_iff_of_norm_eq h,
    not_same_ray_iff_norm_add_lt, h]

namespace isometry

variables {F : Type*} [normed_group F] [normed_space ‚Ñù F]

lemma map_neg_of_map_zero {f : F ‚Üí E} (hi : isometry f) (h0 : f 0 = 0) (x : F) :
  f (-x) = -(f x) :=
begin
  have hn : ‚à•f (-x)‚à• = ‚à•-(f x)‚à•,
  { rw [hi.norm_map_of_map_zero h0, norm_neg, norm_neg, hi.norm_map_of_map_zero h0] },
  rw [‚Üêsame_ray_iff_of_norm_eq hn, same_ray_iff_norm_add, ‚Üêsub_eq_add_neg, ‚Üêdist_eq_norm,
      hi.dist_eq, dist_eq_norm, hi.norm_map_of_map_zero h0, norm_neg, norm_neg,
      hi.norm_map_of_map_zero h0, sub_eq_add_neg, ‚Üêneg_add, norm_neg],
  exact (same_ray.refl x).norm_add
end

lemma map_smul_nonneg_of_map_zero {f : F ‚Üí E} (hi : isometry f) (h0 : f 0 = 0) {r : ‚Ñù}
  (hr : 0 ‚â§ r) (x : F) :
  f (r ‚Ä¢ x) = r ‚Ä¢ f x :=
begin
  have hn : ‚à•f (r ‚Ä¢ x)‚à• = ‚à•r ‚Ä¢ f x‚à•,
  { rw [hi.norm_map_of_map_zero h0, norm_smul, norm_smul,
        hi.norm_map_of_map_zero h0] },
  rw ‚Üêsame_ray_iff_of_norm_eq hn,
  refine same_ray.nonneg_smul_right _ hr,
  rw [same_ray_iff_norm_add, ‚Üêneg_neg (f x), ‚Üêsub_eq_add_neg, ‚Üêhi.map_neg_of_map_zero h0,
      ‚Üêdist_eq_norm, hi.dist_eq, dist_eq_norm, sub_neg_eq_add, norm_neg,
      hi.norm_map_of_map_zero h0, hi.norm_map_of_map_zero h0, norm_neg],
  exact (same_ray_nonneg_smul_left x hr).norm_add
end

lemma map_smul_of_map_zero {f : F ‚Üí E} (hi : isometry f) (h0 : f 0 = 0) (r : ‚Ñù) (x : F) :
  f (r ‚Ä¢ x) = r ‚Ä¢ f x :=
begin
  rcases le_or_lt 0 r with (h|h),
  { exact hi.map_smul_nonneg_of_map_zero h0 h x },
  { rw [‚Üêneg_neg r, neg_smul, hi.map_neg_of_map_zero h0, neg_smul (-r)],
    congr,
    rw ‚Üêneg_pos at h,
    exact hi.map_smul_nonneg_of_map_zero h0 h.le x }
end

lemma map_add_eq_smul_sub_map_sub {f : F ‚Üí E} (hi : isometry f) (x y : F) :
  f (x + y) = (2 : ‚Ñï) ‚Ä¢ f x - f (x - y) :=
begin
  set g : F ‚Üí E := Œª v, f (x + v) - f x with hg,
  have hg0 : g 0 = 0, { rw hg, simp },
  have hfg : ‚àÄ v, f (x + v) = g v + f x, { simp [hg] },
  have hig : isometry g,
  { intros u v, simp [hg, hi.edist_eq] },
  rw [sub_eq_add_neg x, hfg, hfg, hig.map_neg_of_map_zero hg0],
  abel
end

lemma map_add_eq_smul_sub_map_sub_rev {f : F ‚Üí E} (hi : isometry f) (x y : F) :
  f (x + y) = (2 : ‚Ñï) ‚Ä¢ f y - f (y - x) :=
by rw [add_comm, hi.map_add_eq_smul_sub_map_sub]

lemma map_add_of_map_zero {f : F ‚Üí E} (hi : isometry f) (h0 : f 0 = 0) (x y : F):
  f (x + y) = f x + f y :=
calc f (x + y) = (2‚Åª¬π : ‚Ñù) ‚Ä¢ (2 : ‚Ñù) ‚Ä¢ f (x + y) : by simp
     ...       = (2‚Åª¬π : ‚Ñù) ‚Ä¢ (f (x + y) + f (x + y)) : by simp [two_smul]
     ...       = (2‚Åª¬π : ‚Ñù) ‚Ä¢ ((2 : ‚Ñï) ‚Ä¢ f x - f (x - y) + ((2 : ‚Ñï) ‚Ä¢ f y - f (y - x))) :
      by rw [‚Üêhi.map_add_eq_smul_sub_map_sub, ‚Üêhi.map_add_eq_smul_sub_map_sub_rev]
     ...       = f x + f y :
      begin
        rw [‚Üêneg_sub x, hi.map_neg_of_map_zero h0, sub_neg_eq_add, sub_add_add_cancel, smul_add,
            two_smul, two_smul, ‚Üêtwo_smul ‚Ñù (f x), ‚Üêtwo_smul ‚Ñù (f y), ‚Üêmul_smul, ‚Üêmul_smul],
        simp
      end

/-- An isometry of real normed spaces with strictly convex codomain is a linear isometry if it
maps 0 to 0.  Unlike Mazur-Ulam, this does not require the isometry to be surjective.  -/
def linear_isometry_of_map_zero {f : F ‚Üí E} (hi : isometry f) (h0 : f 0 = 0) :
  F ‚Üí‚Çó·µ¢[‚Ñù] E :=
{ to_fun := f,
  map_add' := hi.map_add_of_map_zero h0,
  map_smul' := hi.map_smul_of_map_zero h0,
  norm_map' := hi.norm_map_of_map_zero h0 }

@[simp] lemma coe_linear_isometry_of_map_zero {f : F ‚Üí E} (hi : isometry f)
  (h0 : f 0 = 0) :
  ‚áë(hi.linear_isometry_of_map_zero h0) = f :=
rfl

variables {PF : Type*} {PE : Type*} [metric_space PF] [metric_space PE]
variables [normed_add_torsor F PF] [normed_add_torsor E PE]
include E F

/-- An isometry of `normed_add_torsor`s for real normed spaces, strictly convex in the case of
the codomain, induces a linear isometry at any point.  Unlike Mazur-Ulam, this does not require
the isometry to be surjective.  -/
def linear_isometry_at {f : PF ‚Üí PE} (hi : isometry f) (p : PF) : F ‚Üí‚Çó·µ¢[‚Ñù] E :=
linear_isometry_of_map_zero
  (show isometry (Œª x : F, f (x +·µ• p) -·µ• f p), begin
    intros x y,
    simp_rw [edist_dist, dist_vsub_cancel_right, hi.dist_eq, dist_eq_norm_vsub,
             vadd_vsub_vadd_cancel_right, vsub_eq_sub]
   end) (by simp)

@[simp] lemma coe_linear_isometry_at {f : PF ‚Üí PE} (hi : isometry f) (p : PF) :
  ‚áë(hi.linear_isometry_at p) = Œª x : F, f (x +·µ• p) -·µ• f p :=
rfl

lemma linear_isometry_at_apply {f : PF ‚Üí PE} (hi : isometry f) (p : PF) (v : F) :
  hi.linear_isometry_at p v = f (v +·µ• p) -·µ• f p :=
rfl

lemma linear_isometry_at_apply_vsub {f : PF ‚Üí PE} (hi : isometry f)
  (p‚ÇÅ p‚ÇÇ : PF) :
  hi.linear_isometry_at p‚ÇÅ (p‚ÇÇ -·µ• p‚ÇÅ) +·µ• f p‚ÇÅ = f p‚ÇÇ :=
by simp

lemma linear_isometry_at_eq {f : PF ‚Üí PE} (hi : isometry f) (p‚ÇÅ p‚ÇÇ : PF) :
  hi.linear_isometry_at p‚ÇÅ = hi.linear_isometry_at p‚ÇÇ :=
begin
  ext x,
  rw [hi.linear_isometry_at_apply, hi.linear_isometry_at_apply,
      ‚Üêhi.linear_isometry_at_apply_vsub p‚ÇÅ (x +·µ• p‚ÇÇ), ‚Üêhi.linear_isometry_at_apply_vsub p‚ÇÅ p‚ÇÇ,
      vadd_vsub_vadd_cancel_right, ‚Üêlinear_isometry.map_sub, hi.linear_isometry_at_apply,
      vsub_sub_vsub_cancel_right, vadd_vsub]
end

/-- An isometry of `normed_add_torsor`s for real normed spaces, strictly convex in the case of
the codomain, is an affine isometry.  Unlike Mazur-Ulam, this does not require the isometry to
be surjective.  -/
noncomputable def affine_isometry_of_strict_convex_space {f : PF ‚Üí PE} (hi : isometry f) :
  PF ‚Üí·µÉ‚Å±[‚Ñù] PE :=
{ to_fun := f,
  linear := (hi.linear_isometry_at (classical.arbitrary PF)).to_linear_map,
  map_vadd' := Œª p v, begin
    rw hi.linear_isometry_at_eq (classical.arbitrary PF) p,
    simp
  end,
  norm_map := (hi.linear_isometry_at _).norm_map }

@[simp] lemma coe_affine_isometry_of_strict_convex_space {f : PF ‚Üí PE} (hi : isometry f) :
  ‚áë(hi.affine_isometry_of_strict_convex_space) = f :=
rfl

lemma affine_isometry_of_strict_convex_space_apply {f : PF ‚Üí PE} (hi : isometry f)
  (p : PF) :
  hi.affine_isometry_of_strict_convex_space p = f p :=
rfl

end isometry
