/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.convex.function
import measure_theory.integral.average

/-!
# Jensen's inequality for integrals

In this file  we prove several forms of Jensen's inequality for integrals.

- for convex sets: `convex.average_mem`, `convex.set_average_mem`, `convex.integral_mem`;

- for convex functions: `convex.on.average_mem_epigraph`, `convex_on.map_average_le`,
  `convex_on.set_average_mem_epigraph`, `convex_on.map_set_average_le`, `convex_on.map_integral_le`;

## Tags

convex, integral, center mass, average value, Jensen's inequality
-/

open measure_theory measure_theory.measure metric set filter topological_space function
open_locale topological_space big_operators ennreal convex

variables {Œ± E F : Type*} {m0 : measurable_space Œ±}
  [normed_group E] [normed_space ‚Ñù E] [complete_space E]
  [topological_space.second_countable_topology E] [measurable_space E] [borel_space E]
  [normed_group F] [normed_space ‚Ñù F] [complete_space F]
  [topological_space.second_countable_topology F] [measurable_space F] [borel_space F]
  {Œº : measure Œ±} {s : set E}

/-- If `Œº` is a probability measure on `Œ±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Œº`-a.e. points to `s`, then the expected value of `f` belongs to `s`:
`‚à´ x, f x ‚àÇŒº ‚àà s`. See also `convex.sum_mem` for a finite sum version of this lemma. -/
lemma convex.integral_mem [is_probability_measure Œº] {s : set E} (hs : convex ‚Ñù s)
  (hsc : is_closed s) {f : Œ± ‚Üí E} (hf : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) :
  ‚à´ x, f x ‚àÇŒº ‚àà s :=
begin
  obtain ‚ü®y‚ÇÄ, h‚ÇÄ‚ü© : s.nonempty,
  { rcases hf.exists with ‚ü®x‚ÇÄ, h‚ÇÄ‚ü©, exact ‚ü®f x‚ÇÄ, h‚ÇÄ‚ü© },
  rcases hfi.ae_measurable with ‚ü®g, hgm, hfg‚ü©,
  rw [integral_congr_ae hfg], rw [integrable_congr hfg] at hfi,
  have hg : ‚àÄ·µê x ‚àÇŒº, g x ‚àà closure s,
    from (hfg.rw (Œª x y, y ‚àà s) hf).mono (Œª x hx, subset_closure hx),
  set G : ‚Ñï ‚Üí simple_func Œ± E := simple_func.approx_on _ hgm s y‚ÇÄ h‚ÇÄ,
  have : tendsto (Œª n, (G n).integral Œº) at_top (ùìù $ ‚à´ x, g x ‚àÇŒº),
    from tendsto_integral_approx_on_of_measurable hfi _ hg _ (integrable_const _),
  refine hsc.mem_of_tendsto this (eventually_of_forall $ Œª n, hs.sum_mem _ _ _),
  { exact Œª _ _, ennreal.to_real_nonneg },
  { rw [‚Üê ennreal.to_real_sum, (G n).sum_range_measure_preimage_singleton, measure_univ,
      ennreal.one_to_real],
    exact Œª _ _, measure_ne_top _ _ },
  { simp only [simple_func.mem_range, forall_range_iff],
    exact Œª x, simple_func.approx_on_mem hgm _ _ _ },
end

/-- If `Œº` is a non-zero finite measure on `Œ±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Œº`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`‚®ç x, f x ‚àÇŒº ‚àà s`. See also `convex.center_mass_mem` for a finite sum version of this lemma. -/
lemma convex.average_mem [is_finite_measure Œº] {s : set E} (hs : convex ‚Ñù s) (hsc : is_closed s)
  (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) :
  ‚®ç x, f x ‚àÇŒº ‚àà s :=
begin
  haveI : is_probability_measure ((Œº univ)‚Åª¬π ‚Ä¢ Œº),
    from is_probability_measure_smul hŒº,
  refine hs.integral_mem hsc (ae_mono' _ hfs) hfi.to_average,
  exact absolutely_continuous.smul (refl _) _
end

/-- If `Œº` is a non-zero finite measure on `Œ±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Œº`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`‚®ç x, f x ‚àÇŒº ‚àà s`. See also `convex.center_mass_mem` for a finite sum version of this lemma. -/
lemma convex.set_average_mem {t : set Œ±} {s : set E} (hs : convex ‚Ñù s) (hsc : is_closed s)
  (h0 : Œº t ‚â† 0) (ht : Œº t ‚â† ‚àû) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº.restrict t, f x ‚àà s)
  (hfi : integrable_on f t Œº) :
  ‚®ç x in t, f x ‚àÇŒº ‚àà s :=
begin
  haveI : fact (Œº t < ‚àû) := ‚ü®ht.lt_top‚ü©,
  refine hs.average_mem hsc _ hfs hfi,
  rwa [ne.def, restrict_eq_zero]
end

lemma convex_on.average_mem_epigraph [is_finite_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : convex_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  (‚®ç x, f x ‚àÇŒº, ‚®ç x, g (f x) ‚àÇŒº) ‚àà {p : E √ó ‚Ñù | p.1 ‚àà s ‚àß g p.1 ‚â§ p.2} :=
have ht_mem : ‚àÄ·µê x ‚àÇŒº, (f x, g (f x)) ‚àà {p : E √ó ‚Ñù | p.1 ‚àà s ‚àß g p.1 ‚â§ p.2},
  from hfs.mono (Œª x hx, ‚ü®hx, le_rfl‚ü©),
by simpa only [average_pair hfi hgi]
  using hg.convex_epigraph.average_mem (hsc.epigraph hgc) hŒº ht_mem (hfi.prod_mk hgi)

lemma concave_on.average_mem_hypograph [is_finite_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : concave_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  (‚®ç x, f x ‚àÇŒº, ‚®ç x, g (f x) ‚àÇŒº) ‚àà {p : E √ó ‚Ñù | p.1 ‚àà s ‚àß p.2 ‚â§ g p.1} :=
by simpa only [mem_set_of_eq, pi.neg_apply, average_neg, neg_le_neg_iff]
  using hg.neg.average_mem_epigraph hgc.neg hsc hŒº hfs hfi hgi.neg

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is convex and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points to `s`, then the value of `g` at the average value of `f` is less than or equal to
the average value of `g ‚àò f` provided that both `f` and `g ‚àò f` are integrable. See also
`convex_on.map_center_mass_le` for a finite sum version of this lemma. -/
lemma convex_on.map_average_le [is_finite_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : convex_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  g (‚®ç x, f x ‚àÇŒº) ‚â§ ‚®ç x, g (f x) ‚àÇŒº :=
(hg.average_mem_epigraph hgc hsc hŒº hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is concave and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points to `s`, then the average value of `g ‚àò f` is less than or equal to the value of `g`
at the average value of `f` provided that both `f` and `g ‚àò f` are integrable. See also
`concave_on.le_map_center_mass` for a finite sum version of this lemma. -/
lemma concave_on.le_map_average [is_finite_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : concave_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) (hŒº : Œº ‚â† 0) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  ‚®ç x, g (f x) ‚àÇŒº ‚â§ g (‚®ç x, f x ‚àÇŒº) :=
(hg.average_mem_hypograph hgc hsc hŒº hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is convex and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is
less than or equal to the average value of `g ‚àò f` over `t` provided that both `f` and `g ‚àò f` are
integrable. -/
lemma convex_on.set_average_mem_epigraph {s : set E} {g : E ‚Üí ‚Ñù} (hg : convex_on ‚Ñù s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Œ±} (h0 : Œº t ‚â† 0)
  (ht : Œº t ‚â† ‚àû) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº.restrict t, f x ‚àà s) (hfi : integrable_on f t Œº)
  (hgi : integrable_on (g ‚àò f) t Œº) :
  (‚®ç x in t, f x ‚àÇŒº, ‚®ç x in t, g (f x) ‚àÇŒº) ‚àà {p : E √ó ‚Ñù | p.1 ‚àà s ‚àß g p.1 ‚â§ p.2} :=
begin
  haveI : fact (Œº t < ‚àû) := ‚ü®ht.lt_top‚ü©,
  refine hg.average_mem_epigraph hgc hsc _ hfs hfi hgi,
  rwa [ne.def, restrict_eq_zero]
end

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is concave and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points of a set `t` to `s`, then the average value of `g ‚àò f` over `t` is less than or
equal to the value of `g` at the average value of `f` over `t` provided that both `f` and `g ‚àò f`
are integrable. -/
lemma concave_on.set_average_mem_hypograph {s : set E} {g : E ‚Üí ‚Ñù} (hg : concave_on ‚Ñù s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Œ±} (h0 : Œº t ‚â† 0)
  (ht : Œº t ‚â† ‚àû) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº.restrict t, f x ‚àà s) (hfi : integrable_on f t Œº)
  (hgi : integrable_on (g ‚àò f) t Œº) :
  (‚®ç x in t, f x ‚àÇŒº, ‚®ç x in t, g (f x) ‚àÇŒº) ‚àà {p : E √ó ‚Ñù | p.1 ‚àà s ‚àß p.2 ‚â§ g p.1} :=
by simpa only [mem_set_of_eq, pi.neg_apply, average_neg, neg_le_neg_iff]
  using hg.neg.set_average_mem_epigraph hgc.neg hsc h0 ht hfs hfi hgi.neg

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is convex and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is
less than or equal to the average value of `g ‚àò f` over `t` provided that both `f` and `g ‚àò f` are
integrable. -/
lemma convex_on.map_set_average_le {s : set E} {g : E ‚Üí ‚Ñù} (hg : convex_on ‚Ñù s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Œ±} (h0 : Œº t ‚â† 0)
  (ht : Œº t ‚â† ‚àû) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº.restrict t, f x ‚àà s) (hfi : integrable_on f t Œº)
  (hgi : integrable_on (g ‚àò f) t Œº) :
  g (‚®ç x in t, f x ‚àÇŒº) ‚â§ ‚®ç x in t, g (f x) ‚àÇŒº :=
(hg.set_average_mem_epigraph hgc hsc h0 ht hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is concave and continuous on a convex closed
set `s`, `Œº` is a finite non-zero measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending
`Œº`-a.e. points of a set `t` to `s`, then the average value of `g ‚àò f` over `t` is less than or
equal to the value of `g` at the average value of `f` over `t` provided that both `f` and `g ‚àò f`
are integrable. -/
lemma concave_on.le_map_set_average {s : set E} {g : E ‚Üí ‚Ñù} (hg : concave_on ‚Ñù s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Œ±} (h0 : Œº t ‚â† 0)
  (ht : Œº t ‚â† ‚àû) {f : Œ± ‚Üí E} (hfs : ‚àÄ·µê x ‚àÇŒº.restrict t, f x ‚àà s) (hfi : integrable_on f t Œº)
  (hgi : integrable_on (g ‚àò f) t Œº) :
  ‚®ç x in t, g (f x) ‚àÇŒº ‚â§ g (‚®ç x in t, f x ‚àÇŒº) :=
(hg.set_average_mem_hypograph hgc hsc h0 ht hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is convex and continuous on a convex closed
set `s`, `Œº` is a probability measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending `Œº`-a.e.  points
to `s`, then the value of `g` at the expected value of `f` is less than or equal to the expected
value of `g ‚àò f` provided that both `f` and `g ‚àò f` are integrable. See also
`convex_on.map_center_mass_le` for a finite sum version of this lemma. -/
lemma convex_on.map_integral_le [is_probability_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : convex_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  g (‚à´ x, f x ‚àÇŒº) ‚â§ ‚à´ x, g (f x) ‚àÇŒº :=
by simpa only [average_eq_integral]
  using hg.map_average_le hgc hsc (is_probability_measure.ne_zero Œº) hfs hfi hgi

/-- **Jensen's inequality**: if a function `g : E ‚Üí ‚Ñù` is concave and continuous on a convex closed
set `s`, `Œº` is a probability measure on `Œ±`, and `f : Œ± ‚Üí E` is a function sending `Œº`-a.e.  points
to `s`, then the expected value of `g ‚àò f` is less than or equal to the value of `g` at the expected
value of `f` provided that both `f` and `g ‚àò f` are integrable. -/
lemma concave_on.le_map_integral [is_probability_measure Œº] {s : set E} {g : E ‚Üí ‚Ñù}
  (hg : concave_on ‚Ñù s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Œ± ‚Üí E}
  (hfs : ‚àÄ·µê x ‚àÇŒº, f x ‚àà s) (hfi : integrable f Œº) (hgi : integrable (g ‚àò f) Œº) :
  ‚à´ x, g (f x) ‚àÇŒº ‚â§ g (‚à´ x, f x ‚àÇŒº) :=
by simpa only [average_eq_integral]
  using hg.le_map_average hgc hsc (is_probability_measure.ne_zero Œº) hfs hfi hgi
