/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import measure_theory.group.add_circle
import dynamics.ergodic.ergodic
import measure_theory.covering.density_theorem
import data.set.pointwise.iterate

/-!
# Ergodic maps of the additive circle

This file contains proofs of ergodicity for maps of the additive circle.

# Main definitions:

 * `add_circle.ergodic_zsmul`: given `n : ‚Ñ§` such that `1 < |n|`, the self map `y ‚Ü¶ n ‚Ä¢ y` on
   the additive circle is ergodic (wrt the Haar measure).

# TODO

 * The map `y ‚Ü¶ n ‚Ä¢ y + x` is ergodic for any `x` (and `1 < |n|`).

-/

open set function measure_theory measure_theory.measure filter metric
open_locale measure_theory nnreal ennreal topological_space pointwise

namespace add_circle

variables {T : ‚Ñù} [hT : fact (0 < T)]
include hT

/-- If a null-measurable subset `s` of the circle is invariant under rotation by an arbitrarily
small rational angle, then it must be almost empty or almost full. -/
lemma ae_empty_or_univ_of_forall_vadd_eq_self
  {s : set $ add_circle T} (hs : null_measurable_set s volume)
  {Œπ : Type*} {l : filter Œπ} [l.ne_bot] {u : Œπ ‚Üí add_circle T}
  (hu‚ÇÅ : ‚àÄ i, (u i) +·µ• s = s) (hu‚ÇÇ : tendsto (add_order_of ‚àò u) l at_top) :
  s =·µê[volume] (‚àÖ : set $ add_circle T) ‚à® s =·µê[volume] univ :=
begin
  have hT‚ÇÄ : 0 < T := hT.out,
  have hT‚ÇÅ : ennreal.of_real T ‚â† 0 := by simpa,
  rw [ae_eq_empty, ae_eq_univ_iff_measure_eq hs, add_circle.measure_univ],
  cases (eq_or_ne (volume s) 0) with h h, { exact or.inl h, },
  right,
  obtain ‚ü®d, -, hd‚ü© := exists_mem_of_measure_ne_zero_of_ae h
    (is_doubling_measure.ae_tendsto_measure_inter_div (volume : measure $ add_circle T) s 1),
  let I : Œπ ‚Üí set (add_circle T) := Œª j, closed_ball d (T / (2 * ‚Üë(add_order_of $ u j))),
  replace hd : tendsto (Œª j, volume (s ‚à© I j) / volume (I j)) l (ùìù 1),
  { let Œ¥ : Œπ ‚Üí ‚Ñù := Œª j, T / (2 * ‚Üë(add_order_of $ u j)),
    have hŒ¥‚ÇÄ : ‚àÄ·∂† j in l, 0 < Œ¥ j :=
      (hu‚ÇÇ.eventually_gt_at_top 0).mono (Œª j hj, div_pos hT‚ÇÄ $ by positivity),
    have hŒ¥‚ÇÅ : tendsto Œ¥ l (ùìù[>] 0),
    { refine tendsto_nhds_within_iff.mpr ‚ü®_, hŒ¥‚ÇÄ‚ü©,
      replace hu‚ÇÇ : tendsto (Œª j, (T‚Åª¬π * 2) * (add_order_of $ u j)) l at_top :=
        (tendsto_coe_nat_at_top_iff.mpr hu‚ÇÇ).const_mul_at_top (by positivity : 0 < T‚Åª¬π * 2),
      convert hu‚ÇÇ.inv_tendsto_at_top,
      ext j,
      simp only [Œ¥, pi.inv_apply, mul_inv_rev, inv_inv, div_eq_inv_mul, ‚Üê mul_assoc], },
    have hw : ‚àÄ·∂† j in l, d ‚àà closed_ball d (1 * Œ¥ j) := hŒ¥‚ÇÄ.mono (Œª j hj, by simp [hj.le]),
    exact hd _ Œ¥ hŒ¥‚ÇÅ hw, },
  suffices : ‚àÄ·∂† j in l, volume (s ‚à© I j) / volume (I j) = volume s / ennreal.of_real T,
  { replace hd := hd.congr' this,
    rwa [tendsto_const_nhds_iff, ennreal.div_eq_one_iff hT‚ÇÅ ennreal.of_real_ne_top] at hd, },
  refine (hu‚ÇÇ.eventually_gt_at_top 0).mono (Œª j hj, _),
  have huj : is_of_fin_add_order (u j) := add_order_of_pos_iff.mp hj,
  have huj' : 1 ‚â§ (‚Üë(add_order_of $ u j) : ‚Ñù), { norm_cast, exact nat.succ_le_iff.mpr hj, },
  have hI‚ÇÄ : volume (I j) ‚â† 0 := (measure_closed_ball_pos _ d $ by positivity).ne.symm,
  have hI‚ÇÅ : volume (I j) ‚â† ‚ä§ := measure_ne_top _ _,
  have hI‚ÇÇ : volume (I j) * ‚Üë(add_order_of $ u j) = ennreal.of_real T,
  { rw [volume_closed_ball, mul_div, mul_div_mul_left T _ two_ne_zero,
      min_eq_right (div_le_self hT‚ÇÄ.le huj'), mul_comm, ‚Üê nsmul_eq_mul, ‚Üê ennreal.of_real_nsmul,
      nsmul_eq_mul, mul_div_cancel'],
    exact nat.cast_ne_zero.mpr hj.ne', },
  rw [ennreal.div_eq_div_iff hT‚ÇÅ ennreal.of_real_ne_top hI‚ÇÄ hI‚ÇÅ,
    volume_of_add_preimage_eq s _ (u j) d huj (hu‚ÇÅ j) closed_ball_ae_eq_ball, nsmul_eq_mul,
    ‚Üê mul_assoc, hI‚ÇÇ],
end

lemma pre_ergodic_zsmul {n : ‚Ñ§} (hn : 1 < |n|) :
  pre_ergodic (Œª (y : add_circle T), n ‚Ä¢ y) :=
‚ü®begin
  intros s hs hs',
  let u : ‚Ñï ‚Üí add_circle T := Œª j, ‚Üë(((‚Üë1 : ‚Ñù) / ‚Üë(n.nat_abs^j)) * T),
  replace hn : 1 < n.nat_abs, { rwa [int.abs_eq_nat_abs, nat.one_lt_cast] at hn, },
  have hu‚ÇÄ : ‚àÄ j, add_order_of (u j) = n.nat_abs^j,
  { exact Œª j, add_order_of_div_of_gcd_eq_one (pow_pos (pos_of_gt hn) j) (gcd_one_left _), },
  have hnu : ‚àÄ j, n^j ‚Ä¢ (u j) = 0 := Œª j, by rw [‚Üê add_order_of_dvd_iff_zsmul_eq_zero, hu‚ÇÄ,
    int.coe_nat_pow, ‚Üê int.abs_eq_nat_abs, ‚Üê abs_pow, abs_dvd],
  have hu‚ÇÅ : ‚àÄ j, (u j) +·µ• s = s := Œª j, vadd_eq_self_of_preimage_zsmul_eq_self hs' (hnu j),
  have hu‚ÇÇ : tendsto (Œª j, add_order_of $ u j) at_top at_top,
  { simp_rw hu‚ÇÄ, exact nat.tendsto_pow_at_top_at_top_of_one_lt hn, },
  exact ae_empty_or_univ_of_forall_vadd_eq_self hs.null_measurable_set hu‚ÇÅ hu‚ÇÇ,
end‚ü©

lemma ergodic_zsmul {n : ‚Ñ§} (hn : 1 < |n|) : ergodic (Œª (y : add_circle T), n ‚Ä¢ y) :=
{ .. measure_preserving_zsmul volume (abs_pos.mp $ lt_trans zero_lt_one hn),
  .. pre_ergodic_zsmul hn, }

end add_circle
