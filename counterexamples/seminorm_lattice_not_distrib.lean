/-
Copyright (c) 2022 Pierre-Alexandre Bazin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pierre-Alexandre Bazin
-/
import analysis.seminorm
/-!
# The lattice of seminorms is not distributive

We provide an example of three seminorms over the ‚Ñù-vector space ‚Ñù√ó‚Ñù whih dont verify the lattice
distributivity property (p ‚äî q1) ‚äì (p ‚äî q2) ‚â§ p ‚äî (q1 ‚äì q2).

This proves the lattice (seminorm ‚Ñù ‚Ñù√ó‚Ñù) is not distributive.

## References

* https://en.wikipedia.org/wiki/Seminorm#Examples
-/

private lemma bdd_below_range_add {ùïú E : Type*} [normed_field ùïú] [add_comm_group E] [module ùïú E]
  (x : E) (p q : seminorm ùïú E) :
  bdd_below (set.range (Œª (u : E), p u + q (x - u))) :=
by { use 0, rintro _ ‚ü®x, rfl‚ü©, exact add_nonneg (p.nonneg _) (q.nonneg _) }

noncomputable def p : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
{ to_fun := Œª x, |x.fst| ‚äî |x.snd|,
  triangle' := Œª x y, begin
    apply sup_le,
    { apply le_trans (abs_add _ _), apply add_le_add, exact le_sup_left, exact le_sup_left },
    { apply le_trans (abs_add _ _), apply add_le_add, exact le_sup_right, exact le_sup_right }
  end,
  smul' := Œª a x, begin
    change |a * x.fst| ‚äî |a * x.snd| = |a| * (|x.fst| ‚äî |x.snd|),
    rw abs_mul, rw abs_mul,
    cases (le_or_lt (|x.fst|) (|x.snd|)),
    { rw sup_eq_right.mpr h, exact sup_eq_right.mpr (mul_le_mul_of_nonneg_left h (abs_nonneg _)) },
    { have h := le_of_lt h,
      rw sup_eq_left.mpr h, exact sup_eq_left.mpr (mul_le_mul_of_nonneg_left h (abs_nonneg _)) }
  end }

noncomputable def q1 : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
{ to_fun := Œª x, 4 * |x.fst|,
  triangle' := Œª x y, begin
    rw [‚Üê mul_add, mul_le_mul_left], { exact abs_add _ _ }, { norm_num }
  end,
  smul' := Œª a x, begin
    change 4 * |a * x.fst| = |a| * (4 * |x.fst|),
    rw abs_mul, ring
  end }

noncomputable def q2 : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
{ to_fun := Œª x, 4 * |x.snd|,
  triangle' := Œª x y, begin
    rw [‚Üê mul_add, mul_le_mul_left], { exact abs_add _ _ }, { norm_num }
  end,
  smul' := Œª a x, begin
    change 4 * |a * x.snd| = |a| * (4 * |x.snd|),
    rw abs_mul, ring
  end }

lemma eq_one : (p ‚äî (q1 ‚äì q2)) (1, 1) = 1 := begin
  change |1| ‚äî |1| ‚äî (q1 ‚äì q2) (1, 1) = 1,
  rw [sup_idem, abs_one, sup_eq_left],
  apply cinfi_le_of_le (bdd_below_range_add _ _ _) ((0, 1) : ‚Ñù√ó‚Ñù),
  simp only [prod.mk_sub_mk, sub_zero, sub_self], change (4 * |(0:‚Ñù)| + 4 * |(0:‚Ñù)| ‚â§ 1),
  simp only [abs_zero, mul_zero, add_zero, zero_le_one]
end

/-- This is a counterexample to the distributivity of the lattice (seminorm ‚Ñù (‚Ñù√ó‚Ñù)). -/
lemma not_distrib : ¬¨((p ‚äî q1) ‚äì (p ‚äî q2) ‚â§ p ‚äî (q1 ‚äì q2)) := begin
  intro le_sup_inf,
  have c : ¬¨(4/3 ‚â§ (1:‚Ñù)) := by norm_num,
  apply c, nth_rewrite 2 ‚Üê eq_one,
  apply le_trans _ (le_sup_inf _),
  apply le_cinfi, intro x,
  cases le_or_lt x.fst (1/3) with h1 h1,
  { cases le_or_lt x.snd (2/3) with h2 h2,
    { calc 4/3 = 4 * (1 - 2/3) : by norm_num
           ... ‚â§ 4 * (1 - x.snd) : (mul_le_mul_left zero_lt_four).mpr (sub_le_sub_left h2 _)
           ... ‚â§ 4 * |1 - x.snd| : (mul_le_mul_left zero_lt_four).mpr (le_abs_self _)
           ... = q2 ((1, 1) - x) : rfl
           ... ‚â§ (p ‚äî q2) ((1, 1) - x) : le_sup_right
           ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : le_add_of_nonneg_left ((p ‚äî q1).nonneg _) },
    { calc 4/3 = 2/3 + (1 - 1/3) : by norm_num
           ... ‚â§ x.snd + (1 - x.fst) : add_le_add (le_of_lt h2) (sub_le_sub_left h1 _)
           ... ‚â§ |x.snd| + |1 - x.fst| : add_le_add (le_abs_self _) (le_abs_self _)
           ... ‚â§ p x + p ((1, 1) - x) : add_le_add le_sup_right le_sup_left
           ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : add_le_add le_sup_left le_sup_left } },
  { calc 4/3 = 4 * (1/3) : by norm_num
         ... ‚â§ 4 * x.fst : (mul_le_mul_left zero_lt_four).mpr (le_of_lt h1)
         ... ‚â§ 4 * |x.fst| : (mul_le_mul_left zero_lt_four).mpr (le_abs_self _)
         ... = q1 x : rfl
         ... ‚â§ (p ‚äî q1) x : le_sup_right
         ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : le_add_of_nonneg_right ((p ‚äî q2).nonneg _) }
end
